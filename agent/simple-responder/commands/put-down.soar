sp {apply*process-message*put-down
   (state <s> ^name responder
              ^operator <o>
              ^io.input-link.message <message>)
   (<o> ^name process-message
        ^message <msgID>)
   (<message> ^id <msgID>
          ^words <w1>)
   (<w1> ^word put-down
         ^next <w2>)
   (<w2> ^word <x>
         ^next <w3>)
   (<w3> ^word <y>
         ^next <w4>)
   (<w4> ^word <z>
         -^next)
-->
   (<s> ^desired <d>)
   (<d> ^message <msgID>
        ^put-down-at <l>)
   (<l> ^x <x>
        ^y <y>
        ^z <z>)
}

sp {apply*process-message*put-down*current-location
   (state <s> ^name responder
              ^operator <o>
              ^io.input-link.message <message>)
   (<o> ^name process-message
        ^message <msgID>)
   (<message> ^id <msgID>
          ^words <w1>)
   (<w1> ^word put-down
        -^next)
-->
   (<s> ^desired <d>)
   (<d> ^message <msgID>
        ^put-down <l>)
}

sp {put-down*propose*put-down*location
   (state <s> ^desired <d>)
   (<d> ^put-down-at <loc>
       -^status << complete error >>)
   (<loc> ^x <x> ^y <y> ^z <z>)
-->
   (<s> ^operator <o> +)
   (<o> ^name put-down
        ^actions.put-down.location <loc>)
   (<loc> ^x <x> ^y <y> ^z <z>)
}


sp {put-down*propose*put-down*current-location
   (state <s> ^desired <d>)
   (<d> ^put-down
       -^status << complete error >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name put-down
        ^actions.put-down.location current)
}
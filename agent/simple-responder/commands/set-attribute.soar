sp {apply*process-message*set-attribute*id
   (state <s> ^name responder
              ^operator <o>
              ^io.input-link <il>)
   (<il> ^message <message>
         ^objects.object.id <id>)
   (<o> ^name process-message
        ^message <msgID>)
   (<message> ^id <msgID>
          ^words <w1>)
   (<w1> ^word set
         ^next <w2>)
   (<w2> ^word <id>
         ^next <w3>)
   (<w3> ^word <att>
         ^next <w4>)
   (<w4> ^word to
         ^next <w5>)
   (<w5> ^word <value>)
-->
   (<s> ^desired <d>)
   (<d> ^message <msgID>
        ^set-attribute <sa>)
   (<sa> ^id <id>
         ^attribute <att>
         ^value <value>)
}

sp {apply*process-message*set-attribute*name
   (state <s> ^name responder
              ^operator <o>
              ^io.input-link <il>)
   (<il> ^message <message>
         ^objects.object <obj>)
   (<obj> ^name <name>
          ^id <id>)
   (<o> ^name process-message
        ^message <msgID>)
   (<message> ^id <msgID>
          ^words <w1>)
   (<w1> ^word set
         ^next <w2>)
   (<w2> ^word <name>
         ^next <w3>)
   (<w3> ^word <att>
         ^next <w4>)
   (<w4> ^word to
         ^next <w5>)
   (<w5> ^word <value>)
-->
   (<s> ^desired <d>)
   (<d> ^message <msgID>
        ^set-attribute <sa>)
   (<sa> ^id <id>
         ^attribute <att>
         ^value <value>)
}

sp {propose*set-attribute
   (state <s> ^desired <d>)
   (<d> ^set-attribute <sa>
       -^status << complete error >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name set-attribute
        ^actions.action <sa>)
}

sp {elaborate*desired*complete*set-attribute*id
   (state <s> ^name responder
              ^io.input-link.objects.object <obj>
              ^desired <d>)
   (<d> ^set-attribute <sa>)
   (<sa> ^id <id>
         ^attribute <att>
         ^value <value>)
   (<obj> ^id <id>
          ^<att> <value>)
-->
   (<d> ^status complete)
}
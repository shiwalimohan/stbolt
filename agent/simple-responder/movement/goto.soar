sp {goto*propose*goto*location
   (state <s> ^desired <d>)
   (<d> ^location <loc>
       -^status << complete error >>)
   (<loc> ^x <x> ^y <y>)
-->
   (<s> ^operator <o> +)
   (<o> ^name goto
        ^actions.destination <dest>)
   (<dest> ^x <x> ^y <y>)
}

sp {goto*propose*goto*place
   (state <s> ^desired <d>)
   (<d> ^place <place>
       -^status << complete error >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name goto
        ^actions.goto.place <place>)
}

sp {goto*elaborate*destination*with-t
   (state <s> ^desired.location.t <t>
              ^operator <o> +)
   (<o> ^name goto
        ^actions.destination <dest>)
-->
   (<dest> ^t <t>)
}

sp {goto*elaborate*destination*no-t
   (state <s> -^desired.location.t
               ^io.input-link.self.location.t <t>
              ^operator <o> +)
   (<o> ^name goto
        ^actions.destination <dest>)
-->
   (<dest> ^t <t>)
}
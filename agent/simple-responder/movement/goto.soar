sp {goto*propose*goto
   (state <s> ^desired <d>)
   (<d> ^type goto
       -^status << complete error >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name goto)
}

sp {stop*propose*stop
   (state <s> ^desired <d>)
   (<d> ^type stop
       -^status << complete error >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name stop)
}

sp {grab*propose*grab
   (state <s> ^desired <d>)
   (<d> ^type grab-object
       -^status << complete error >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name grab-object)
}

sp {drop*propose*drop
   (state <s> ^desired <d>)
   (<d> ^type drop-object
       -^status << complete error >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name drop-object)
}

sp {goto*elaborate*goto-operator*x*y
   (state <s> ^desired <d>
              ^operator <o> +)
   (<d> ^type goto
        ^x <x> ^y <y>)
   (<o> ^name goto)
-->
   (<o> ^x <x> ^y <y>)
}

sp {goto*elaborate*goto-operator*place
   (state <s> ^desired <d>
              ^operator <o> +)
   (<d> ^type goto
        ^place <place>)
   (<o> ^name goto)
-->
   (<o> ^place <place>)
}


sp {goto*elaborate*goto-operator*t
   (state <s> ^desired <d>
              ^operator <o> +)
   (<d> ^type goto
        ^t <t>)
   (<o> ^name goto)
-->
   (<o> ^t <t>)
}

sp {stop*apply*stop
   (state <s> ^io.output-link <out>
              ^operator <o>)
   (<o> ^name stop)
-->
   (<out> ^stop <d>)
}
sp {grab*apply*grab
   (state <s> ^io.output-link <out>
              ^operator <o>)
   (<o> ^name grab-object)
-->
   (<out> ^grab-object <d>)
}

sp {drop*apply*drop
   (state <s> ^io.output-link <out>
              ^operator <o>)
   (<o> ^name drop-object)
-->
   (<out> ^drop-object <d>)
}

sp {goto*apply*goto*place
   (state <s> ^io.output-link <out>
              ^operator <o>)
   (<o> ^name goto
        ^place <place>)
-->
   (<out> ^goto <d>)
   (<d> ^place <place>)
}

sp {goto*apply*goto*no-t
   (state <s> ^io.output-link <out>
              ^operator <o>)
   (<o> ^name goto
        ^x <x> ^y <y> -^t)
-->
   (<out> ^destination <d>)
   (<d> ^x <x> ^y <y> ^t 0)
}

sp {goto*apply*goto*with-t
   (state <s> ^io.output-link <out>
              ^operator <o>)
   (<o> ^name goto
        ^x <x> ^y <y> ^t <t>)
-->
   (<out> ^destination <d>)
   (<d> ^x <x> ^y <y> ^t <t>)
}

sp {goto*distance-left
   (state <s> ^io.input-link.self <self>
              ^desired <d>)
   (<self> ^name robot
           ^pose.x <curX>
           ^pose.y <curY>)
   (<d> ^x <goalX> ^y <goalY>)
-->
   # Straight line distance from current position to goal squared
   (<s> ^distance-left (+ (* (- <goalX> <curX>) (- <goalX> <curX>)) 
   						  (* (- <goalY> <curY>) (- <goalY> <curY>))))
}

sp {goto*mark-complete
   (state <s> ^desired <d>
              ^distance-left < 0.01) # Probably should be tweaked
   (<d> ^type goto)
-->
   (<d> ^status complete)
}

#sp {stop*mark-complete
#   (state <s> ^desired <d>)
#   (<d> ^type stop)
#-->
#   (<d> ^status complete)
#}
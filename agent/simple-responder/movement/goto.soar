sp {goto*propose*goto
   (state <s> ^desired <d>)
   (<d> ^type goto
       -^status << complete error >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name goto)
}

sp {goto*elaborate*goto-operator*x*y
   (state <s> ^desired <d>
              ^operator <o> +)
   (<d> ^type goto
        ^x <x> ^y <y>)
   (<o> ^name goto)
-->
   (<o> ^x <x> ^y <y>)
}

sp {goto*elaborate*goto-operator*t
   (state <s> ^desired <d>
              ^operator <o> +)
   (<d> ^type goto
        ^t <t>)
   (<o> ^name goto)
-->
   (<o> ^t <t>)
}

sp {goto*apply*goto*no-t
   (state <s> ^io.output-link <out>
              ^operator <o>)
   (<o> ^name goto
        ^x <x> ^y <y> -^t)
-->
   (<out> ^command.destination <d>)
   (<d> ^x <x> ^y <y> ^t 0)
}

sp {goto*apply*goto*with-t
   (state <s> ^io.output-link <out>
              ^operator <o>)
   (<o> ^name goto
        ^x <x> ^y <y> ^t <t>)
-->
   (<out> ^command.destination <d>)
   (<d> ^x <x> ^y <y> ^t <t>)
}

sp {goto*distance-left
   (state <s> ^io.input-link.self <sens>
              ^desired <d>)
   (<sens> ^name robot
           ^pose.x <curX>
           ^pose.y <curY>)
   (<d> ^x <goalX> ^y <goalY>)
-->
   # Straight line distance from current position to goal squared
   (<s> ^distance-left (+ (* (- <goalX> <curX>) (- <goalX> <curX>)) 
   						  (* (- <goalY> <curY>) (- <goalY> <curY>))))
}

sp {goto*mark-complete
   (state <s> ^desired <d>
              ^distance-left < 0.0001) # Probably should be tweaked
   (<d> ^type goto)
-->
   (<d> ^status complete)
}
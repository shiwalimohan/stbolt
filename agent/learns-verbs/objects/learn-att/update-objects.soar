# Once the attribute is stored, update the objects with the new knowledge
sp {learn-att*propose*update-objects
   (state <s> ^name learn-att
             -^updated)
-->
   (<s> ^operator <o> +)
   (<o> ^name update-objects
        ^category object-processing)
}
              
# Updates all objects that have that attribute with the external representation
sp {learn-att*apply*update-objects
   (state <s> ^name learn-att
              ^operator.name update-objects
              ^internal <internal-att>
              ^external <external-att>
              ^topstate.object <obj>)
   (<obj> ^mapping.att-val-pair.att <att>)
   (<att> ^internal <internal-att>)
-->
   (<att> ^external <external-att>)
}      

# Remove att-val-pairs during the merge
sp {learn-att*apply*update-objects*remove*att-val-pair
   (state <s> ^name learn-att
              ^operator.name update-objects
              ^external <external-att>
              ^topstate.object.mapping <map>)
   (<map> ^att-val-pair <av-pair>)
   (<av-pair> ^att.external <external-att>
              ^att-type external)
-->
   (<map> ^att-val-pair <av-pair> -)
}

sp {learn-att*apply*update-objects*mark*updated
   (state <s> ^name learn-att
              ^operator.name update-objects)
-->
   (<s> ^updated true)
}
              
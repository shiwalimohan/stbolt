# shared-att-val
#	Query for a group of objects that share a common attribute

# Basic query structure for common-attribute-question
# Requires that there are more than 1 group-obj
sp {elaborate*ask-query*shared-att-val*type-info*multiple
   (state <s> ^operator <o> +)
   (<o> ^name ask-query
        ^query <q>)
   (<q> ^type shared-att-val
        ^group-obj <obj1>
        ^group-obj <> <obj1>)
-->
   (<o> ^type common-attribute-question
        ^expected-response bare-attribute-response)
}

# Basic query structure for attribute-presence-question
# Requires that there is only 1 group-obj
sp {elaborate*ask-query*shared-att-val*type-info*one
   (state <s> ^operator <o>)
   (<o> ^name ask-query
        ^query <q>)
   (<q> ^type shared-att-val
        ^group-obj <obj1>
        -^group-obj <> <obj1>)
-->
   (<o> ^type attribute-presence-question
        ^expected-response bare-attribute-response)
}

# Adds group-obj(s) to query structure
sp {elaborate*ask-query*shared-att-val*group-obj
   (state <s> ^operator <o>)
   (<o> ^name ask-query
        ^query <q>
        ^fields <f>)
   (<q> ^type shared-att-val
        ^group-obj.external-desc <external-desc>)
-->
   (<f> ^object <external-desc>)
}


# Adds exception(s) to query structure
sp {elaborate*ask-query*shared-att-val*exception
   (state <s> ^operator <o>)
   (<o> ^name ask-query
        ^query <q>
        ^fields <f>)
   (<q> ^type shared-att-val
        ^exception <att>)
-->
   (<f> ^exception <att>)
}

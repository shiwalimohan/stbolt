# complete-lookup-att
# Fetches the query result and adds the result to the superstate lookup-att operator
# If successful, adds the found word to the operator
# If failure, adds the word 'failure' to the operator
# Example for trying to find an external attribute color
#	 Success: adds ^external color, Failure: adds ^external failure

# Knows a query is processed if there is a success or failure result
sp {lookup-att*propose*complete-lookup-att
   (state <s> ^name lookup-att
              ^smem <smem>)
   (<smem> ^command.query <query>
           ^result.{<< success failure >>} <query>)
   (<query> ^type attribute)
-->
   (<s> ^operator <o> +)
   (<o> ^category object-processing
        ^name complete-lookup-att
        ^query <query>)
}

# Remove the query from the smem.command link
sp {lookup-att*apply*complete-lookup-att*remove*query
   (state <s> ^name lookup-att
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name complete-lookup-att
        ^query <query>)
   (<cmd> ^query <query>)
-->
   (<cmd> ^query <query> -)
}

# Case 1 : external failure
sp {lookup-att*apply*complete-lookup-att*external*failure
   (state <s> ^name lookup-att
              ^operator.name complete-lookup-att
              ^superstate.operator <ss-op>
              ^smem.result.failure)
   (<ss-op> -^external)
-->
   (<ss-op> ^external failure)
}

# Case 2 : internal failure
sp {lookup-att*apply*complete-lookup-att*internal*failure
   (state <s> ^name lookup-att
              ^operator.name complete-lookup-att
              ^superstate.operator <ss-op>
              ^smem.result.failure)
   (<ss-op> -^internal)
-->
   (<ss-op> ^internal failure)
}

# Case 3: external success
sp {lookup-att*apply*complete-lookup-att*external*success
   (state <s> ^name lookup-att
              ^operator <o>
              ^smem.result <result>
              ^superstate.operator <ss-op>)
   (<o> ^name complete-lookup-att
        ^query <query>)
   (<result> ^success <query>
             ^retrieved.external <external-att>)
   (<ss-op> -^external)
-->
   (<ss-op> ^external <external-att>)
}

# Case 4: internal success
sp {lookup-att*apply*complete-lookup-att*internal*success
   (state <s> ^name lookup-att
              ^operator <o>
              ^smem.result <result>
              ^superstate.operator <ss-op>)
   (<o> ^name complete-lookup-att
        ^query <query>)
   (<result> ^success <query>
             ^retrieved.internal <internal-att>)
   (<ss-op> -^internal)
-->
   (<ss-op> ^internal <internal-att>)
}
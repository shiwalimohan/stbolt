## elaborate self
sp {elaborate*self
   (state <s> ^io.input-link <il>
   	      ^topstate <s>)
   (<il> ^self <self>)
 -->
   (<s> ^self <self>)
}

## elaborate objects
### as locations
sp {elaborate*objects*locations
   (state <s> ^topstate <s>)
   (<s> ^object <obj>)
   (<obj> ^property.word << table garbage pantry stove scale >>)
-->
   (<obj> ^category location)
}


### as blocks
sp {elaborate*objects*blocks
   (state <s> ^topstate <s>)
   (<s> ^object <obj>)
   (<obj> -^property.word << table garbage pantry stove scale >>)
-->
   (<obj> ^category block)
}

### copy objects to substate, if not in language problem-space
sp {copy*objects
   (state <s> ^superstate <ss>
      	  -^problem-space << lgsoar message-interpretation >>
	  -^name explain 
	  -^name assign-goal
	  -^name assign-problem-space
	  -^name evaluate-operator
	  -^name selection
	  -^name find-next-episode
	  ^io)
   (<ss> -^problem-space << lgsoar message-interpretation >>
   	 ^object <obj>)
-->
  (<s> ^object <obj>)
}

## elaborate grabbed object from the id
sp {elaborate*grabbed*object
   (state <s> ^self <self>
   	      ^object <obj>
          ^topstate <s>)
   (<self> ^grabbed-object <id> <> -1)
   (<obj> ^id <id>
   	  ^category block)
-->
   (<s> ^grabbed <obj>)
}   

sp {copy*grabbed
   (state <s> ^superstate <ss>
   	      ^io)
   (<ss> ^grabbed <obj>)
-->
   (<s> ^grabbed <obj>)
}

## copy track-relations to substates
sp {copy*track-relations
   (state <s> ^superstate.track-relation <rel>)
-->
   (<s> ^track-relation <rel>)
}


## elaborate state-properties (open/close)
sp {elaborate*object*property*open*close
   (state <s> ^object <obj>
   	      ^topstate <s>)
   (<obj> ^property <statprop>)
   (<statprop> ^name door
   	       ^value <val>)
-->
   (<obj> ^door <val>)
}

sp {elaborate*state*property*open*close
   (state <s> ^object <obj>)
   (<obj> ^door <val>)
-->
   (<s> ^<val> <obj>)
}


## elaborate state-properties (heat)
sp {elaborate*object*property*heat
   (state <s> ^object <obj>
   	      ^topstate <s>)
   (<obj> ^property <sprop>)
   (<sprop> ^name heat
   	    ^value <val>)
-->
   (<obj> ^heat <val>)
}

sp {elaborate*state*property*heat
   (state <s> ^object <obj>)
   (<obj> ^heat <val>)
-->
   (<s> ^<val> <obj>)
}



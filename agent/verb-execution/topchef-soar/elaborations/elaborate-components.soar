## elaborate self
sp {elaborate*self
   (state <s> ^io.input-link <il>
   	      ^topstate <s>)
   (<il> ^self <self>)
 -->
   (<s> ^self <self>)
}

## elaborate objects
### as locations
sp {elaborate*objects*locations
   (state <s> ^topstate <s>)
   (<s> ^object <obj>)
   (<obj> ^property.word << table garbage pantry stove scale >>)
-->
   (<obj> ^category location)
}


### as blocks
sp {elaborate*objects*blocks
   (state <s> ^topstate <s>)
   (<s> ^object <obj>)
   (<obj> -^property.word << table garbage pantry stove scale >>)
-->
   (<obj> ^category block)
}

### copy objects to substate, if not in language problem-space
sp {copy*objects
   (state <s> ^superstate <ss>
   	  -^superstate.operator.name assign-goal
      	  -^problem-space << lgsoar message-interpretation >>
	  -^name explain 
	  -^name assign-goal
	  -^name assign-problem-space
	  -^name evaluate-operator
	  -^name selection
	  -^name find-next-episode)
   (<ss> -^problem-space << lgsoar message-interpretation >>
   	 ^object <obj>)
-->
  (<s> ^object <obj>)
}

## elaborate grabbed object from the id
sp {elaborate*grabbed*object
   (state <s> ^self <self>
   	      ^object <obj>
          ^topstate <s>)
   (<self> ^grabbed-object <id> <> -1)
   (<obj> ^id <id>
   	  ^category block)
-->
   (<s> ^grabbed <obj>)
}   

sp {copy*grabbed
   (state <s> ^superstate <ss>
   	      ^io)
   (<ss> ^grabbed <obj>)
-->
   (<s> ^grabbed <obj>)
}

## copy track-relations to substates
sp {copy*track-relations
   (state <s> ^superstate.track-relation <rel>
   	      ^io)
-->
   (<s> ^track-relation <rel>)
}


## elaborate state-properties (open/close)
sp {elaborate*object*property*open*close
   (state <s> ^object <obj>
   	      ^topstate <s>)
   (<obj> ^property <statprop>)
   (<statprop> ^name door
   	       ^value <val>)
-->
   (<obj> ^door <val>)
   (<s> ^<val> <obj>)
}

sp {elaborate*state*property*open
   (state <s> ^superstate <ss>
   	      ^io)
   (<ss> ^open <val>)
-->
   (<s> ^open <val>)
}

sp {elaborate*state*property*closed
   (state <s> ^superstate <ss>
   	      ^io)
   (<ss> ^closed <val>)
-->
   (<s> ^closed <val>)
}


## elaborate state-properties (heat)
sp {elaborate*object*property*heat
   (state <s> ^object <obj>
   	      ^topstate <s>)
   (<obj> ^property <sprop>)
   (<sprop> ^name heat
   	    ^value <val>)
-->
   (<obj> ^heat <val>)
   (<s> ^<val> <obj>)
}

sp {elaborate*state*property*heat*on
   (state <s> ^superstate <ss>
   	      ^io)
   (<ss> ^on <val>)
-->
   (<s> ^on <val>)
}

sp {elaborate*state*property*heat*off
   (state <s> ^superstate <ss>
   	      ^io)
   (<ss> ^off <val>)
-->
   (<s> ^off <val>)
}




#### elaborate not-track-relation, assumes that at a time only one relation will be tracked !!! this might not work more generally !!

sp {elaborate*no-track*relation
   (state <s> ^name
   	      ^object 
	      ^io)
  -{(<s> ^track-relation <any>)
    (<any> ^complete success)}
-->
   (<s> ^not-tracking-relations true)
}

sp {elaborate*no-track*relation*superstate
   (state <s> ^superstate <ss>)
   (<ss> ^not-tracking-relations true
   	 ^io)
-->
   (<s> ^not-tracking-relations true)
}
   	      
### compute distance for every location and selfs
sp {elaborate*self*location
   (state <s> ^location <loc>
	      ^vpredicates <p>)
-->
   (<p> ^predicate-at <pp>)
   (<pp> ^location <loc>)
}

sp {elaborate*self*location*distance*x
   (state <s> ^vpredicates.predicate-at <pp>
   	      ^self <obj>)
   (<pp>  ^location <loc>)
   (<loc> ^x <loc_x>)
   (<obj> ^x <obj_x>)
   -->
   (<pp> ^distx (- <obj_x> <loc_x>))
}

sp {elaborate*self*location*distance*y
   (state <s> ^vpredicates.predicate-at <pp>
   	      ^self <obj>)
   (<pp> ^location <loc>)
   (<loc> ^y <loc_y>)
   (<obj> ^y <obj_y>)
   -->
   (<pp> ^disty (- <obj_y> <loc_y>))
}

sp {elaborate*relationship*at
   (state <s> ^vpredicates.predicate-at <pp>)
   (<pp> ^distx < 0.25
   	  ^distx > -0.25
	  ^disty < 0.25
	  ^disty > -0.25
	  ^location <loc>)
-->
  (<pp> ^at <in-p>)
  (<in-p> ^location <loc>)
}
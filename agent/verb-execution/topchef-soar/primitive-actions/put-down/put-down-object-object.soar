### put down an object in a location
sp {propose*put-down-object-object*next-to
   (state <s> ^grabbed <obj>
	      ^object <loc>
	     -^problem-space << lgsoar message-interpretation >>
	     -^track-relation.complete success ## SM: HACKY!! figure out a way to to this more generally
	     -^io.input-link.self.action drop) ### either io is absent (explanation), or the arm is not busy
   (<loc> -^category location)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down-object-object
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^category primitive-action) ### a general next-to operator
}

### control in close loop with SVS
## elaborate the operator state
sp {elaborate*op_put-down-object-object
   (state <s> ^name op_put-down-object-object
   	      ^superstate <ss>
	      ^io)
   (<ss> ^operator <sop>)
   (<sop> ^name op_put-down-object-object
   	  ^argument1 <obj>
	  ^argument2 <loc>
	  ^spatial-relationship <rel>)
-->
   (<s> ^argument1 <obj>
   	^argument2 <loc>
	^spatial-relationship <rel>)
}

## find long-term-id related to preposition
sp {propose*svs*lookup-preposition*object
   (state <s> ^name op_put-down-object-object
   	      ^io
	      ^spatial-relationship <rel>
	      -^found-prep <fprep>)
-->
  (<s> ^operator <op> +)
  (<op> ^name lookup-preposition
  	^type <rel>)
}
## determine the location x,y,z using SVS
sp {propose*svs*find-projection*object
   (state <s> ^name op_put-down-object-object
   	      ^io
   	      ^argument1 <obj>
	      ^argument2 <loc>
	      ^spatial-relationship <rel>
	      ^found-prep <fprep>
	      -^projection-result)
-->
   (<s> ^operator <op> +)
   (<op> ^name find-projection
   	^a <obj>
	^b <loc>
	^type <rel>
	^category svs)
}

## set up tracking
## SM 06/17/2012: changed to work with tracking in the superstate
sp {set*svs*tracking*object
   (state <s> ^name op_put-down-object-object
   	      ^projection-result <res>
	      ^io
	      ^argument1 <obj>
	      ^argument2 <loc>
	      ^spatial-relationship <rel>
	      ^found-prep <fprep>
	      ^topstate <ts>)
-->
   (<ts> ^track-relation <relation>)
   (<relation> ^object1 <obj>
   	       ^object2 <loc>
	       ^found-prep <fprep>
	       ^vprep <rel>)
}

## put actions on the output-link
sp {elaborate*put-down*object*object
   (state <s> ^name op_put-down-object-object
   	      ^superstate <ss>
	      ^projection-result <res>)
   (<ss> ^operator <sop>)
   (<sop> ^name op_put-down-object-object)
-->
   (<sop> ^actions.put-down.location <res>		 
   	 ^category primitive-action)
   (<sop> ^kind non-atomic)
}



##############################################################################
# clean up track-result
sp {delete*track-result*object
   (state <s> ^topstate <s>
   	      ^track-relation <any>
   	      ^operator <op>)
   (<op> -^category << primitive-action composite-action >>)
-->
   (<s> ^track-relation <any> -)
}


################################################################################
### action model : action model
sp {apply*put-down*location*action-model*object
   (state <s> ^operator <op>	
	     -^io
	     ^grabbed <obj>)
   (<op> ^name op_put-down-object-object
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^spatial-relationship <rel>)
-->
   (<s> ^grabbed <obj> -
   	^track-relation <rel>)
   (<rel> ^object1 <obj>
          ^object2 <loc>
          ^prep <rel>
          ^complete success)
}

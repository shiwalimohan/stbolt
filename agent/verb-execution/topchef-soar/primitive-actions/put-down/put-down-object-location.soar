### put down an object in a location
sp {propose*put-down-object-location
   (state <s> ^grabbed <obj>
	      ^object <loc>
	     -^problem-space << lgsoar message-interpretation >>
	     -^io.input-link.self.action drop)  ### either io is absent (explanation), or the arm is not busy (real-world)
   -{(<s> ^track-relation <relation>)
     (<relation> ^object1 <obj>
     		 ^object2 <loc>
		 ^vprep in
		 ^complete success)
   }#### the topstate does not have a track-relation that already has what this action is trying to do
   (<loc> ^category location)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down-object-location
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^spatial-relationship in
	 ^category primitive-action)
}

### control in close loop with SVS
## elaborate the operator state
sp {elaborate*op_put-down-object-location
   (state <s> ^name op_put-down-object-location
   	      ^superstate <ss>
	      ^io)
   (<ss> ^operator <sop>)
   (<sop> ^name op_put-down-object-location
   	  ^argument1 <obj>
	  ^argument2 <loc>
	  ^spatial-relationship <rel>)
-->
   (<s> ^argument1 <obj>
   	^argument2 <loc>
	^spatial-relationship <rel>)
}

## find long-term-id related to preposition
sp {propose*svs*lookup-preposition
   (state <s> ^name op_put-down-object-location
   	      ^io
	      ^spatial-relationship <rel>
	      -^found-preposition <fprep>)
-->
  (<s> ^operator <op> +)
  (<op> ^name lookup-preposition
  	^type <rel>)
}
## determine the location x,y,z using SVS
sp {propose*svs*find-projection
   (state <s> ^name op_put-down-object-location
   	      ^io
   	      ^argument1 <obj>
	      ^argument2 <loc>
	      ^spatial-relationship <rel>
	      ^found-preposition <fprep>
	      -^projection-result)
   (<obj> ^id <oid>)
   (<loc> ^id <lid>)
-->
   (<s> ^operator <op> +)
   (<op> ^name find-projection
   	^a <oid>
	^b <lid>
	^type <rel>
	^category svs)
}

## set up tracking
## SM 06/17/2012: changed to work with tracking in the superstate
sp {propose*svs*tracking
   (state <s> ^name op_put-down-object-location
   	      ^projection-result <res>
	      ^io
	      ^argument1 <obj>
	      ^argument2 <loc>
	      ^spatial-relationship <rel>
	      ^found-preposition <fprep>)
-->
   (<ts> ^track-relation <relation>)
   (<relation> ^object1 <obj>
   	       ^object2 <loc>
	       ^found-prep <fprep>
	       ^vprep <rel>)
}

## put actions on the output-link
sp {elaborate*put-down*object*location*action
   (state <s> ^name op_put-down-object-location
   	      ^superstate <ss>
	      ^projection-result <res>)
   (<ss> ^operator <sop>)
   (<sop> ^name op_put-down-object-location)
-->
   (<sop> ^actions.put-down.location <res>		 
   	 ^category primitive-action)
   (<sop> ^kind non-atomic)
}



##############################################################################
# clean up track-result
sp {delete*track-result
   (state <s> ^topstate <s>
   	      ^track-relation<any>
   	      ^operator <op>)
   (<op> -^category << primitive-action composite-action >>)
-->
   (<s> ^track-relation <any> -)
}


################################################################################
### action model : action model
sp {apply*put-down*location*action-model
   (state <s> ^operator <op>	
	     -^io
	     ^grabbed <obj>)
   (<op> ^name op_put-down-object-location
   	 ^argument1 <obj>
	 ^argument2 <loc>)
-->
   (<s> ^grabbed <obj> -
   	^track-relation <rel>)
   (<rel> ^object1 <obj>
          ^object2 <loc>
          ^prep in
          ^complete success)
}


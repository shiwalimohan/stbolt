### put down an object in a location
sp {propose*put-down-object-location*without-door
   (state <s>  ^grabbed <obj>
	      ^object <loc>
	     -^problem-space << lgsoar message-interpretation >>
	     ^name {<any> <> evaluate-operator}
	     ^not-tracking-relations true)
   (<loc> ^category location
   	  -^door)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down-object-location
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^category primitive-action)
}

### 
sp {propose*put-down-object-location*with-door
   (state <s>  ^grabbed <obj>
	      ^object <loc>
	     -^problem-space << lgsoar message-interpretation >>
	     ^name {<any> <> evaluate-operator}
	     ^open <loc>
	     ^not-tracking-relations true)
   (<loc> ^category location
   	  ^door)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down-object-location
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^category primitive-action)
}


### if an operator is trying to achieve what track-relation has tracked to be success
sp {retract*put-down-object-location
   (state <s> ^operator <op>)
   (<op> ^name << op_put-down-object-location op_put-down-object-object >>
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^spatial-relationship <rel>)
   (<s> ^track-relation <tr>)
   (<tr> ^object1 <obj>
     	 ^object2 <loc>
	 ^complete success
	 ^vpredicate <rel>)
-->
  (<s> ^operator <op> -)
}

### prefer other operators if put-down-object-location is success
sp {retract*put-down-object-location
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name << op_put-down-object-location op_put-down-object-object >>
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^spatial-relationship <rel>
	 ^category primitive-action)  
   (<op2> ^category primitive-action)
   (<s> ^track-relation <tr>)
   (<tr> ^object1 <obj>
     	 ^object2 <loc>
	 ^complete success
	 ^vpredicate <rel>)
-->
  (<s> ^operator <op1> < <op2>)
}


### control in close loop with SVS
## elaborate the operator state
sp {elaborate*op_put-down-object-location
   (state <s> ^name  << op_put-down-object-location op_put-down-object-object >>
   	      ^superstate <ss>
	      ^io
	      ^smem.command <scomm>)
   (<ss> ^operator <sop>)
   (<sop> ^name << op_put-down-object-location op_put-down-object-object >>
   	  ^argument1 <obj>
	  ^argument2 <loc>
	  ^spatial-relationship <rel>)
-->
   (<s> ^argument1 <obj>
   	^argument2 <loc>
	^spatial-relationship <rel>)
   (<scomm> ^retrieve <rel>)
}

## determine the location x,y,z using SVS
sp {propose*svs*find-projection
   (state <s> ^name << op_put-down-object-location op_put-down-object-object >>
   	      ^io
   	      ^argument1 <obj>
	      ^argument2 <loc>
	      ^spatial-relationship <rel>
	      -^projection-result)
-->
   (<s> ^operator <op> + >)
   (<op> ^name find-projection
         ^interaction true
   	 ^a <obj>
	 ^b <loc>
	 ^predicate <rel>
	 ^category svs)
}

## set up tracking
## SM 06/17/2012: changed to work with tracking in the superstate
sp {set*svs*tracking
   (state <s> ^name << op_put-down-object-location op_put-down-object-object >>
   	      ^projection-result <res>
	      ^io
	      ^argument1 <obj>
	      ^argument2 <loc>
	      ^spatial-relationship <rel>
	      ^topstate <ts>)
   (<ts> ^object <obj>
         ^object <loc>)
-->
   (<ts> ^track-relation <relation>)
   (<relation> ^object1 <obj>
   	       ^object2 <loc>
	       ^predicate <rel>
	       ^vpredicate <rel>)
}

## put actions on the output-link
sp {elaborate*put-down*object*location*action
   (state <s> ^name << op_put-down-object-location op_put-down-object-object >>
   	      ^superstate <ss>
	      ^projection-result <res>
	      ^io)
   (<ss> ^operator <sop>)
   (<sop> ^name << op_put-down-object-location op_put-down-object-object >>)
-->
   (<sop> ^actions.put-down.location <res>		 
   	 ^category primitive-action)
   (<sop> ^kind non-atomic)
}



##############################################################################
# clean up track-result
sp {delete*track-result
   (state <s> ^topstate <s>
   	      ^track-relation <any>
   	      ^operator <op>)
   (<op> ^category << interaction-stack communication >>)
-->
   (<s> ^track-relation <any> -)
}


################################################################################
### action model : action model
sp {apply*put-down*location*action-model
   (state <s> ^operator <op>	
	     -^io
	     ^grabbed <obj>
	     ^superstate <ss>)
   (<op> ^name << op_put-down-object-location op_put-down-object-object >>
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^spatial-relationship <rel>)
-->
   (<s> ^grabbed <obj> -
   	^track-relation <trel>
	^not-tracking-relations true -)
   (<trel> ^object1 <obj>
          ^object2 <loc>
          ^predicate <rel>
          ^complete success
	  ^vpredicate <rel>)
   (<ss> ^track-relation <trel>
   	 ^not-tracking-relations true -)
}


sp {find-projection*elaborate*bit-flip
   (state <s> ^name find-projection)
-->
   (<s> ^bit flip)
}
   	  
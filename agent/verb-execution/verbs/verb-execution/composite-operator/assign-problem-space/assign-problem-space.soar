### assign a problem space to the current operator being explained based on the information stored in semantic memory. note that only composite operators are stored under problem-space of a operator. primitve actions are proposed by default.


## elaborate assign-problem-space
sp {assign-problem-space*elaborate*state
   (state <s> ^name assign-problem-space
	      ^smem.command <scomm>
	      ^superstate <ss>)
   (<ss> ^operator <sop>)
   (<sop> ^problem-space-id <pid>
	 ^map-id <map>
	 ^procedural-id <proc-id>)
-->
   (<s> ^problem-space-id <pid>
	^map-id <map>
	^proposal-state <ss>
	^procedural-id <proc-id>
   	^bit flip)
   (<scomm> ^retrieve <pid>)
   (force-learn <s>) ## to learn proposal rule chunks 
}
	  

### if there are operator templates in problem-space-id, generate operators based on them

sp {generate*composite-operator*problem-space*operator*template
   (state <s> ^name assign-problem-space
	      -^completed <pop>
	      ^problem-space-id <pid>
	      ^proposal-state <ps>)
   (<pid> ^operator <pop>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name generate-problem-space-operator
   	 ^problem-space-operator-id <pop>
	 ^proposal-state <ps>)
}

sp {retract*generate*composite-operator*if*not*in*explain
   (state <s> ^name assign-problem-space
   	      -^superstate.learning-operator <any>
	      ^operator <op> +
	      ^quiescence t)
   (<op> ^name generate-problem-space-operator)
-->
   (<s> ^operator <op> -)
}

### if state-no-change, write desired to the superstate; occurs only if there are any composite operators in the problem-space
sp {assign-problem-space*write*superstate*desired
   (state <s> ^impasse no-change
   	      ^attribute state
	      ^superstate <ss>
	      ^quiescence t)
   (<ss> ^name assign-problem-space
   	 ^superstate <sss>)
-->
   (<sss> ^generated yes)
}


## if there are no operators in the problem-space, 
sp {generate*composite-operator*problem-space*operator*none
   (state <s> ^name assign-problem-space
   	      ^problem-space-id <pid>
	      ^superstate <ss>
	      ^smem.result.success <any>
	      ^quiescence t)
   (<pid> -^operator <pop>)
-->
   (<ss> ^generated none)
}
   	   
 



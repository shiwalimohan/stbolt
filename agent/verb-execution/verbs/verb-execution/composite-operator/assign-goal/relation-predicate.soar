### elaborate desired
sp {assign-goal*elaborate*desired*state
   (state <s> ^name assign-goal
   	      ^desired <d>
	      ^long-term-id <glid>)
   (<glid> ^<prep> <p1>)
   (<p1> ^type <ltid>
   	 ^category relation)
#   (<ltid> ^relation <relation>)
-->
   (<d> ^track-relation <rel>)
   (<rel> ^spatial-relationship <ltid>
   	  ^_instantiation-of <p1>)
}

### propose lookup preposition if all the augmentation of the long-term preposition id are not retrieved
sp {assign-goal*propose*lookup-preposition
   (state <s> ^name assign-goal
   	      ^long-term-id <glid>
	      -^found-prep.predicate <ltid>)
   (<glid> ^<prep> <p1>)
   (<p1> ^type <ltid>
   	 ^g_prep_name <p-name>
	 ^category relation)
-->
   (<s> ^operator <op> + =)
   (<op> ^name lookup-preposition
   	 ^type <p-name>
	 ^category svs)
}


### connect desired to the objects in the indexed-operator
sp {assign-goal*elaborate*objects*relation
   (state <s> ^name assign-goal
   	      ^desired.track-relation <tr>
	      ^long-term-id <glid>
	      ^superoperator <iop>
	      ^procedural-id.<varg> <sobj>)
   (<glid> ^<prep> <p1>)
   (<tr> ^spatial-relationship <type>)
   (<p1> ^{<parg> <> type} <sobj>
   	 ^type <type>)
   (<iop> ^<varg> <obj>)
-->
   (<tr> ^<parg> <obj>)
  
}


### add rule to use a semantic object
sp {assign-goal*elaborate*semantic-objects*relation
   (state <s> ^name assign-goal
   	      ^desired.track-relation <tr>
	      ^long-term-id <glid>
	      ^superoperator <iop>
	      ^procedural-id.<varg> <sobj>)
   (<glid> ^<prep> <p1>)
   (<tr> ^spatial-relationship <type>)
   (<p1> ^{<parg> <> type} <sobj>
   	 ^type <type>)
   (<iop> ^name <opname>)
-->
   (<tr> ^<parg> <sobj>)
   (<sobj> ^focus (concat <opname> <parg>))
}
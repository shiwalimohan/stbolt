## create the ^desired attribute in the composite operator state
## if the operator is associated with a long-term id corresponding to the goal, retrieve the children.
## if no children are retrieved, pose a 'goal-query'

### elaborate assign-goal
sp {assign-goal*elaborate*state
   (state <s> ^name assign-goal
   	      ^superstate.operator <sop>)
   (<sop> ^name assign-goal
   	  ^long-term-id <glid>
	  ^superoperator <iop>
	  ^procedural-id <pid>) 
-->
  (<s> ^long-term-id <glid>
       ^desired <d>
       ^superoperator <iop>
       ^bit flip
       ^nbit flip
       ^procedural-id <pid>)
#  (force-learn <s>) 
}

### retrieve the long-term-id corresponding to the goal of the indexed-operator
sp {assign-goal*retrieve*long-term-id
   (state <s> ^name assign-goal
   	      ^long-term-id <glid>
	      ^smem.command <scom>)
-->
   (<scom> ^retrieve <glid>)
}

### if there is no predicate attribute on the goal id, no goal is known, begin an interaction
sp {assign-goal*unknown-goal
   (state <s> ^name assign-goal
   	      ^long-term-id <glid>
	      ^smem.result <sres>
	      ^topstate <ts>
	      ^quiescence t)
   (<sres> ^success <glid>) ## correctly retrieved
   (<glid> -^<predicate> <any>) ## no predicates in goal
   (<ts> ^status <stat>)
-->
   (<stat> ^type unknown-goal)
}

### create the desired attribute on the state
sp {assign-goal*propose*retrieve-predicate
   (state <s> ^name assign-goal
   	      ^long-term-id <glid>
	      ^smem.result <sres>
	      -^retrieved-success <p1>)
   (<sres> ^success <glid>)
   (<glid> ^<prep> <p1>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name retrieve-predicate
   	 ^predicate <p1>)
}

### if state-no-change, write desired to the superstate
sp {assign-goal*write*superstate*desired
   (state <s> ^impasse no-change
   	      ^attribute state
	      ^superstate <ss>)
   (<ss> ^name assign-goal
   	 ^superstate <sss>
	 ^desired <d>)
-->
   (<sss> ^desired <d>)
}

#### apply pop-segment on the conversation-stack

sp {pop-segment*dont-learn
   (state <s> ^name pop-segment)
-->
   (dont-learn <s>)
}

sp {apply*pop-segment
   (state <s> ^interaction.stack <cs>
   	      ^operator <op>)
   (<cs> ^segment <top-segment>)
   (<op> ^name pop-segment
   	 ^segment <top-segment>)
   (<top-segment> ^next <next-segment>)
-->
   (<cs> ^segment <top-segment> -)
   (<cs> ^segment <next-segment>)
}

sp {remove*segment*from*segments*set
   (state <s> ^interaction.segments <segs>
   	      ^operator <op>)
   (<op> ^name pop-segment
   	 ^segment <segment>)
   (<segs> ^segment <segment>)
-->
   (<segs> ^segment <segment> -)
}

# AM: putting stack information on the OL link
sp {apply*pop-segment*output-link
   (state <s> ^interaction.stack <stack>
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name pop-segment
        ^segment <seg>)
   (<out> -^pop-segment.segment <seg>)
-->
   (<out> ^pop-segment <pop>)
   (<pop> ^segment <seg>)
}
   
   	 
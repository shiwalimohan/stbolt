### the agent uses background knowledge of interactive dialog to assign purpose to instructor-messages.


## case 1 verb-commands: the purpose of the instructor is for the agent to act the command in the environment.

sp {elaborate*instructor-message*purpose*case1
   (state <s> ^topstate <s>
	       ^instructor-message <m>)
    (<m> ^type verb-command
    	 ^information <info>)
### SM: special handling for "you are done" command. To be fixed later.
    (<info> -^verb.word done)
 -->
    (<m> ^purpose <p>)
    (<p> ^type external-action
    	 ^satisfaction <sat>)
    (<sat> ^action-event <info>
    	   ^learning-event explanation)
}

### case 2 indication of a successful completion: the purpose of the instructor is to indicate that the current action being pursued is successfully completed, and hence an explanation of that composite operator is in order

sp {elaborate*instructor-message*purpose*case1*done
   (state <s> ^topstate <s>
	       ^instructor-message <m>)
    (<m> ^type verb-command
    	 ^information <info>)
    (<info> ^verb.word done)
 -->
    (<m> ^purpose <p>)
    (<p> ^type explanation
    	 ^satisfaction <sat>)
    (<sat> ^learning-event explanation)
}


### case 3 goal-message, purpose: to attach the concept of a goal to a verb
sp {elaborate*instructor-message*purpose*case3
   (state <s> ^topstate <s>
   	      ^instructor-message <m>)
   (<m> ^type goal-relation-message
   	^information <info>)
   -->
   (<m> ^purpose <p>)
   (<p> ^type goal-acquisition
   	^satisfaction <sat>)
   (<sat> ^learning-event verb-semantic-learning
   	  ^information <info>)
}
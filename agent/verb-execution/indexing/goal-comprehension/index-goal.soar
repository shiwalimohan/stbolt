sp {elaborate*state*index-goal
   (state <s> ^name index-goal
   	      ^superstate.operator <sop>)
   (<sop> ^name index-goal
   	  ^information <info>
	  ^indexed-operator <iop>)
   (<iop> ^_metadata.procedural-id <proc>)
   -->
   (<s> ^information <info>
   	^proc-id <proc>
	^indexed-operator <iop>
	^bit flip)
}

### store goal-id with the verb schema
#sp {index-goal*store*goal-id
#   (state <s> ^name index-goal
#   	      ^map-id <met>
#	      ^smem.command <scomm>)
#-->
#   (<s> ^goal-id <gid>)
#   (<met> ^goal-id <gid>)
#   (<scomm> ^store <gid>
#   	    ^store <met>)
#}

### query semantic-memory to retrieve map-id
sp {query*semantic-memory*for*map-id*index-goal
   (state <s> ^name index-goal
   	      ^smem.command <scomm>
	      ^proc-id <pid>)
-->
   (<scomm> ^query <cue>)
   (<cue> ^procedural <proc>)
}

### elaborate map-id index-goal
sp {elaborate*map-id*index-goal
   (state <s> ^smem.result.retrieved <ret>
   	      ^name index-goal)
   (<ret> ^goal <goal-id>
   	  ^problem-space <ps-id>)
-->
   (<s> ^map-id <ret>
   	^goal-id <goal-id>)
}


### create goal predicates for all relations in the goal statement
sp {propose*create-goal-predicate
   (state <s> ^name index-goal
   	      ^goal-id <gid>
	      ^information.relation <rel>
	      ^bit flip
	      ^indexed-operator <iop>
	      ^map-id <map>)
   (<rel> ^word <pword>)
  -{(<s> ^_relation <trel>)
    (<trel> ^preposition <pword>
     	   ^_ltid <lid>)
    (<gid> ^<lid> <any>)}
-->
   (<s> ^operator <op> +)
   (<op> ^name create-goal-predicate
   	 ^goal-id <gid>
	 ^information <rel>
	 ^map-id <map>
	 ^indexed-operator <iop>)
}

### store goal predicates
sp {store*goal-predicate
   (state <s> ^name index-goal
   	      ^goal-id <gid>
	      ^smem.command <scomm>)
   (<gid> ^<predicate> <rel>)
-->
   (<scomm> ^store <rel> <gid>)
}

### result success
sp {write*status*topstate
   (state <s> ^name index-goal
   	      ^goal-id <gid>
	      ^information.relation <rel>
	      ^indexed-operator <iop>
	      ^_relation <trel>)
  (<trel> ^preposition <pword>
   	   ^_ltid <lid>)
 -{(<rel> ^word <pword>)
   (<gid> -^predicate.type <lid>)}
-->
   (<iop> ^goal <gid>)
}

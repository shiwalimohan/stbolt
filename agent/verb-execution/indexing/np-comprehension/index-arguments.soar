### constraints on the objects selection are provided by the linguistic knowledge in the instruction and the objects in the verbs


sp {elaborate*index-arguments
   (state <s> ^name index-argument
   	      ^superstate.operator <sop>)
   (<sop> ^name index-argument
   	  ^cue <cue>
	  ^connector <con>
	  ^argument <obj>
	  ^context-id <con-id>
	  ^status <stat>
	  ^return-all <rall>
	  ^allow-ties <at>)
-->
  (<s> ^cue <cue>
       ^connector <con>
       ^argument <obj>
       ^context-id <con-id>
       ^status <stat>
       ^partial-order <po>
       ^return-all <rall>
       ^allow-ties <at>)
  (<po> ^verb-argument <con>)
}

#### propose index-object (../objects/index-objects/)
sp {propose*index-object*perceptual
   (state <s> ^name index-argument
   	      ^argument <arg>
	      -^match <any>
	      ^connector <con>
	      ^context-id <con-id>
	      ^return-all <rall>)
-->
   (<s> ^operator <o> +)
   (<o> ^name index-object
	^category indexing
	^use-interaction true
  	^infer-match true # AM: Allows index-object to guess a match if there is only one partial match
	^object <arg>
	^context-id <con-id>)
}

sp {index-argument*preceptual*elaborate*index-object*return-all*true
   (state <s> ^name index-argument
              ^operator <o> +
              ^topstate.use-task-knowledge true
              ^argument <arg>
	      ^return-all true
	      ^allow-tie false)
   (<o> ^object <arg>)
-->
   (<o> ^return-all true
        ^allow-ties false)
}

sp {index-argument*preceptual*elaborate*index-object*return-all*false
   (state <s> ^name index-argument
              ^operator <o> +
              ^topstate.use-task-knowledge false
              ^argument <arg>
	      ^return-all true
	      ^allow-ties false)
   (<o> ^object <arg>)
-->
   (<o> ^return-all false
        ^allow-ties true)
}

sp {index-argument*preceptual*elaborate*index-object*goal
   (state <s> ^name index-argument
              ^operator <o> +
              ^argument <arg>
	      ^return-all false
	      ^allow-tie true)
   (<o> ^object <arg>)
-->
   (<o> ^return-all false
        ^allow-ties true)
}

sp {write*matched*object*superstate
   (state <s> ^name index-argument
   	      ^match <obj>
	      ^partial-order <po>
	      ^superstate <ss>
	      ^cue <cue>
	      ^connector <con>
	      ^status <stat>
	      ^argument <arg>)
  (<po> ^ordered true)
-->
  (<cue> ^<con> <obj>)
  (<stat> ^<con> <obj>
  	  ^partial-order <po>)
  (<ss> ^matched <arg>
  	^bit flip
	^partial-order <po>)
}

### copy the correct object if index-object is successful
sp {copy*matched*object*match
   (state <s> ^name index-argument
   	      ^operator <op>
	      ^argument <arg>) 
   (<op> ^match <obj>
   	 ^candidate <can>
   	 ^name index-object)
   (<can> ^object <obj>
   	  ^pref <num>)
-->   
   (<s> ^match <obj>
   	^candidate <ncan>)
   (<ncan> ^object <obj>
   	   ^pref <num>)
}

### assign order
sp {assign*partial*order*index-argument
   (state <s> ^name index-argument	
   	      ^partial-order <po>
	      ^candidate <can>
	      ^match <obj>)
   (<can> ^object <obj>	
   	  ^pref <num>)
-->
   (<po> ^<num> <obj>
   	 ^ordered true)
}
   	  
   	      










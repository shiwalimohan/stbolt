### 
sp {create*cue*verb
   (state <s> ^name create-cue
   	      ^superstate.information <info>
	      ^topstate.interaction.stack.segment.indexing-status <stat>)
-->
   (<s> ^information <info>
   	^bit flip
	^status <stat>)
}

sp {elaborate*verb
   (state <s> ^name create-cue
   	      ^information <info>)
   (<info> ^verb.word <verb>)
-->
   (<s> ^cue <cue>)
   (<cue> ^verb <verb>)
}

sp {elaborate*arguments*direct-object
   (state <s> ^name create-cue
   	      ^information.verb <info>)
   (<info> ^direct-object.object <do>)
-->
   (<s> ^argument <do>)
}

sp {elaborate*arguments*pp-object
   (state <s> ^name create-cue
   	      ^information.verb <info>)
   (<info> ^preposition.object <do>)
-->
   (<s> ^argument <do>)
}

sp {propose*index-argument*direct-object
   (state <s> ^name create-cue
   	      ^information.verb <info>
	      ^cue <cue>
	      -^matched <do>
	      ^bit flip
	      ^status <stat>)
   (<stat> -^direct-object <any>)
   (<info> ^direct-object.object <do>)
   (<cue> -^direct-object <any>)
-->
   (<s> ^operator <op> + >)
   (<op> ^name index-argument
   	 ^cue <cue>
	 ^connector direct-object
	 ^argument <do>
	 ^category indexing
	 ^context-id direct-object
	 ^return-all true
	 ^allow-ties false
	 ^status <stat>)
}


sp {propose*index-argument*preposition
   (state <s> ^name create-cue
   	      ^information.verb <info>
	      ^bit flip
	      -^_relation.preposition <prep>
	      ^status <stat>)
   (<stat> -^_relation <any>)
   (<info> ^preposition.word <prep>)
  -->
   (<s> ^operator <op> + =)
   (<op> ^name index-preposition
	 ^preposition <prep>
	 ^category indexing
	 ^status <stat>)
}

sp {propose*index-argument*preposition*object
   (state <s> ^name create-cue
   	      ^information.verb <info>
	      ^cue <cue>
	      -^matched <obj>
	      ^bit flip
	      ^_relation <rel>
	      ^status <stat>)
   (<stat> -^<p> <any>)
   (<info> ^preposition.object <obj>)
   (<rel> ^preposition <p>)
   (<cue> -^<p> <any>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name index-argument
   	 ^cue <cue>
	 ^argument <obj>
	 ^connector <p>
	 ^category indexing
	 ^return-all true
	 ^allow-ties false
	 ^context-id <p>
	 ^status <stat>)
}

### elaborate previously matched structures
sp {elaborate*cue*arguments*previous*partial-order
   (state <s> ^name create-cue
   	      ^status <stat>
	      ^cue <cue>
	      ^information.verb.direct-object.object <obj>
	      ^superstate <ss>)
   (<stat> ^partial-order <po>)
-->
   (<ss> ^partial-order <po>)
}

sp {elaborate*cue*arguments*previous*indices*direct-object
   (state <s> ^name create-cue
   	      ^status <stat>
	      ^cue <cue>
	      ^information.verb.direct-object.object <obj>)
   (<stat> ^direct-object <val>)
-->
   (<cue> ^direct-object <val>)
   (<s> ^matched <obj>)
}

sp {elaborate*cue*argument*previous*indices*preposition-object
   (state <s> ^name create-cue
   	      ^status <stat>
	      ^cue <cue>
	      ^information.verb.preposition <prep>)
   (<prep> ^word <p>
   	   ^object <obj>)
   (<stat> ^<p> <val>)
-->
   (<cue> ^<p> <val>)
   (<s> ^matched <obj>)
}

sp {elaborate*cue*argument*previous*indices*relation
   (state <s> ^name create-cue
   	      ^status <stat>)
   (<stat> ^_relation <val>)
-->
   (<s> ^_relation <val>)
}

### mark success if only verb
sp {elaborate*verb*copy-success
   (state <s> ^name create-cue
   	      ^information.verb <info>
	      ^superstate <ss>
	      ^cue <cue>)
   (<info> ^word <verb>
   	   -^direct-object <any>
	   -^preposition <any2>)
-->
   (<ss> ^cue <cue>)
}

### mark success if other attributes
sp {elaborate*copy-success*match-complete
   (state <s> ^name create-cue)
   (<s> ^argument <obj>
  	-^matched <obj>)
-->
   (<s> ^match not-complete)
}

sp {write-to-superstate*copy-success*match-complete
   (state <s> ^name create-cue
   	      -^match not-complete
	      ^cue <cue>
	      ^matched <arg>
	      ^superstate <ss>
	      ^information.verb <info>)
-->
   (<ss> ^cue <cue>)
}

sp {write-to-superstate*partial-order
   (state <s> ^name create-cue
	      ^partial-order <po>
	      ^superstate <ss>)
-->
   (<ss> ^partial-order <po>)
}

sp {write-to-superstate*copy-success*match-complete*spatial-relationship
   (state <s> ^name create-cue
   	      -^match not-complete
	      ^_relation <rel>
	      ^matched <arg>
	      ^superstate <ss>
	      ^information.verb <info>)
-->
   (<ss> ^_relation <rel>)
}


   
   



### propose resolve-multiple-matches-dialog for op ties even after procedural constraints
sp {topstate*propose*resolve-multiple-matches-dialog
   (state <s> ^impasse tie
   	      ^superstate <ss>
	      -^learning-operator <any>)
   (<ss> ^indexed-operator <iop>
   	 -^status.type which-question
	 ^topstate <ss>)
-->
   (<ss> ^operator <op> + >)
   (<op> ^name resolve-multiple-matches-dialog
   	 ^indexed-operator <iop>)
}

### propose resolve-multiple-matches-dialog for multiple same arguments in composite verbs
sp {composite-verb*propose*resolve-multiple-matches-dialog
   (state <s> ^superstate <ss>
	      -^learning-operator <any>)
   (<ss> ^operator <op>
   	 ^indexed-operator <iop>)
   (<op> ^<arg> <val1> {<> <val1> <val2>}
   	 ^category composite-action)
-->
   (<s> ^operator <op> + >)
   (<op> ^name resolve-multiple-matches-dialog
   	 ^indexed-operator <iop>)
}
   


#### process multiple matches to one argument, rely on interaction
sp {multiple*argument*matches*elaborate*name
   (state <s> ^name resolve-multiple-matches-dialog
   	      ^superstate.operator <sop>)
   (<sop> ^name resolve-multiple-matches-dialog
   	  ^indexed-operator <iop>)
   (<iop> ^<arg> <val1> {<> <val1> <val2>}
   	  ^_metadata <met>)   
   (<met> ^arg-data <a1>)
   (<a1> ^arg-context <con>)
   (<con> ^object <obj>)
-->
   (<s> ^name complete-index-object
   	^object-desc <obj>
	^indexed-operator <iop>)
}

sp {multiple*argument*match*propose*which-question
   (state <s> ^name complete-index-object
   	      ^indexed-operator <iop>
	      ^object-desc <obj>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name which-question
   	 ^object <obj>)
}

### delete the indexed operator 
sp {multiple*argument*match*delete*indexed-operator
   (state <s> ^operator.name which-question
   	      ^topstate.interaction.stack.segment.purpose <p>)
   (<p> ^indexed-operator <op>)
-->
   (<op> ^clear true)
}

sp {multiple*argument*match*clear*indexed-operator
   (state <s> ^topstate <s>
   	      ^interaction.stack.segment.purpose <p>
	      ^operator.name push-segment)
   (<p> ^indexed-operator <op>)
   (<op> ^clear true)
-->
   (<p> ^indexed-operator <op> -)
}



## prefer which-question to primitives
sp {prefer*which-question*primitive-actions
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name which-question)
   (<op2> ^category primitive-action)
-->
   (<s> ^operator <op2> -)
}

sp {prefer*which-question*evaluate
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name which-question)
   (<op2> ^name << evaluate-operator >>)
-->
   (<s> ^operator <op2> -)
}

sp {prefer*multiple-matches*primitive-actions
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name resolve-multiple-matches-dialog)
   (<op2> ^category primitive-action)
-->
   (<s> ^operator <op1> > <op2>)
}



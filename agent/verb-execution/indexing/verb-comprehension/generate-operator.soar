#### generates a grounded operator
sp {elaborate*generate*operator
   (state <s> ^name generate-operator
   	      ^superstate.operator <sop>)
   (<sop> ^procedural-id <pid>
   	  ^linguistic-id <lid>
	  ^map-id <map>
	  ^cue <cue>)
-->
   (<s> ^procedural-id <pid>
   	^linguistic-id <lid>
	^map-id <map>
	^cue <cue>)
}

sp {generate*operator*name
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>)
   (<pid> ^op_name <op-name>)
-->
   (<s> ^index-operator <op>)
   (<op> ^name <op-name>)
 }

sp {generate*operator*arguments
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^cue <cue>
	      ^index-operator <op>
	      ^topstate.interaction.stack.segment.info.index-object-context <context>)
   (<op> ^_metadata <met>)
   (<cue> ^<connect> <obj>)
   (<lid> ^<connect> <lobj>)
   (<pid> ^{<arg> <> spatial-relationship} <lobj>)
   (<context> ^context-id <connect>)
-->
   (<op> ^<arg> <obj>)
   (<met> ^arg-data <a1>)
   (<a1> ^argument <arg>
   	 ^description <connect>
	 ^context-id <connect>
	 ^arg-context <context>)
}

#### associate spatial-relationships
sp {associate*spatial*relations
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^index-operator <op>
	      ^cue <cue>)
   (<pid> ^spatial-relationship <obj>)
   (<lid> ^<connect> <obj>)
   (<cue> ^<connect> <lobj>)
-->
   (<op> ^spatial-relationship <connect>)
}

sp {copy*map-id*metadata
   (state <s> ^name generate-operator
   	      ^index-operator <op>
	      ^map-id <map>)
-->
   (<op> ^_metadata <met>)
   (<met> ^map-id <map>)
}

sp {generate*operator*arguments*write-to-superstate
   (state <s> ^name generate-operator
   	      ^index-operator <op>
	      ^superstate <ss>)
   (<op> ^_metadata.arg-data <any>)
-->
   (<ss> ^indexed-operator <op>)
}
sp {elaborate*state*index-verb
   (state <s> ^name index-verb
   	      ^superstate.operator <sop>)
   (<sop> ^name index-verb
   	  ^information <info>)
   -->
   (<s> ^information <info>)
}

sp {elaborate*index-verb*self
   (state <s> ^name index-verb)
-->
   (<s> ^index-verb-state <s>)
}
   	      
sp {copy*index-verb*substate
   (state <s> ^superstate.index-verb-state <is>)
-->
   (<s> ^index-verb-state <is>)
}
   	      

### create cue
sp {propose*create*cue
   (state <s> ^name index-verb
   	      ^information <info>
	      -^cue <cue>)
-->
  (<s> ^operator <op> +)
  (<op> ^name create-cue
  	^category indexing)
}

### create memory cue
sp {elaborate*memory*cue
   (state <s> ^name index-verb
   	      ^cue <cue>)
   (<cue> ^verb <vb>)
-->
   (<s> ^mcue <mcue>
   	^temp <t>)
   (<mcue> ^verb <vb>)
}

sp {elaborate*memory*cue*2*tepm
   (state <s> ^name index-verb
   	      ^cue <cue>
	      ^temp <t>)
   (<cue> ^{<arg> <> verb})
-->
   (<t> ^<arg> yes)
}

sp {elaborate*memory*cue*2*mcue
   (state <s> ^name index-verb
	      ^mcue <mcue>
	      ^temp <t>)
   (<t> ^{<arg> <> verb})
-->
   (<mcue> ^<arg> <new-val>)
}

### propose lookup semantic memory
sp {propose*lookup*semantic-memory
   (state <s> ^name index-verb
   	      ^mcue <cue>
	      -^linguistic <any>)
  -->
   (<s> ^operator <op> +)
   (<op> ^name get-linguistic-id
   	 ^cue <cue>
	 ^category indexing)
}


### retrieved id is the lid for the linguistic structure of the verb
sp {propose*get-parent-map
   (state <s> ^name index-verb
   	      ^linguistic <ling>
	      -^map-id <map>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name get-map-id
   	 ^linguistic <ling>
	 ^category indexing)
}

### get procedural knowledge for the map
sp {propose*get-procedural
   (state <s> ^name index-verb
   	      ^map-id <map>
	      -^procedural <proc>)
  -->
  (<s> ^operator <op> + =)
  (<op> ^name get-procedural-id
  	^map-id <map>
	^category indexing)
}

### generate the structure of the new operator
sp {propose*generate-operator
   (state <s> ^name index-verb
   	      ^procedural <pid>
	      ^linguistic <lid>
	      ^map-id <map>
	      ^cue <cue>
	      -^indexed-operator <any>)
-->
  (<s> ^operator <op> +)
  (<op> ^name generate-operator
  	^procedural-id <pid>
	^linguistic-id <lid>
	^cue <cue>
	^map-id <map>
	^category indexing)
}

# ### copy partial-orderings in indexed-operator
# sp {copy*partial-ordering*indexed-operator
#    (state <s> ^name index-verb
#    	      ^indexed-operator <any>
# 	      ^partial-order <po>)
#    (<any> ^_metadata <met>)
# -->
#    (<met> ^partial-order <po>)
# }
   
  
### write to superstate
sp {write*to*superstate
   (state <s> ^name index-verb
   	      ^superstate.interaction.stack.segment.purpose <sp>
	      ^indexed-operator <map>)
#   (<map> ^_metadata.partial-order <po>)
-->
  (<sp> ^indexed-operator <map>)
}



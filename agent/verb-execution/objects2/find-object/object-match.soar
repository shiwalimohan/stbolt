sp {find-object*propose*object-match
   (state <s> ^name find-object
              ^topstate.object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name object-match
        ^category object-processing
        ^object <obj>)
}

sp {find-object*reject*not-match
   (state <s> ^name find-object
              ^operator <o> +
              ^nounjective.label <label>)
   (<o> ^name object-match
        ^object <obj>)
   (<obj> -^property.label <label>)
-->
   (<s> ^operator <o> -
   	^not-match <obj>)
}

#### SM: changed to support multiple object matches, resolution is attempted later based on object affordances.

sp {find-object*apply*object-match
   (state <s> ^name find-object
              ^operator <o> +
	      -^not-match <obj>)
   (<o> ^name object-match
        ^object <obj>)
-->
   (<s> ^operator <o> =)
}

sp {find-object*write*match*superstate-operator
   (state <s> ^name find-object
              ^superstate.operator <sso>	
	      ^operator <o>
	      ^topstate.object <obj>
	      -^not-match <obj>)
   (<o> ^name object-match)
-->
   (<sso> ^match <obj>)
}

sp {find-object*propose*no-match
   (state <s> ^name find-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name no-match
        ^category object-processing)
}

sp {find-object*apply*no-match
   (state <s> ^name find-object
              ^superstate.operator <o>
              ^operator.name no-match)
-->
   (<o> ^match failure)
}
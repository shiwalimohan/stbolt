# Fetch the label for the category-word pair from memory
sp {object-message*propose*lookup-smem*label
   (state <s> ^name object-message
              ^nounjective <nj>)
   (<nj> ^category <cat>
          ^word <word>
         -^label)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-smem
        ^category smem
        ^lookup-type label
        ^query <q>
        ^desired.label true)
   (<q> ^type label-map ^word <word> ^category <cat> ^label <label>)
}

# The label is cached in working memory, use it
sp {object-message*apply*lookup-smem*label*working-mem
   (state <s> ^name object-message
              ^operator <o>
              ^topstate.mappings.label-map <lm>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.word <word>
        ^query.category <cat>)
   (<lm> ^<word>.<cat> <label>)
-->
   (<o> ^result <label>)
}

# Create the new label
sp {object-message*apply*lookup-smem*label*failure*create*new-label
   (state <s> ^name object-message
              ^operator <o>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.word <word>
        ^query.category <cat>
        ^result failure)
-->
   (<o> ^new-label (concat <cat> |-| <word>))
}

# Adds the new-label as a result to the operator
sp {object-message*apply*lookup-smem*label*new-label*add*result
   (state <s> ^name object-message
              ^operator <o>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^new-label <label>
        ^result failure)
-->
   (<o> ^result <label>)
}

# Stores the new label in semantic memory
sp {object-message*apply*lookup-smem*label*new-label*store*smem
   (state <s> ^name object-message
              ^operator <o>
              ^smem.command <smem-cmd>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.word <word>
        ^query.category <cat>
        ^new-label <label>)
-->
   (<smem-cmd> ^store <l>)
   (<l> ^type label-map ^word <word> ^category <cat> ^label <label>)
}

# Successful retrieval
sp {object-message*apply*lookup-smem*label*success
   (state <s> ^name object-message
              ^operator <o>
              ^nounjective <nj>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.word <word>
        ^result {<label> <> failure})
   (<nj> ^word <word>)
-->
   (<nj> ^label <label>)
}

# Send a training label to the visual system
sp {object-message*apply*lookup-smem*send-training-label
   (state <s> ^name object-message
              ^io.output-link <out>
              ^operator <o>
              ^object.id <id>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.category <cat>
        ^result {<label> <> failure})
-->
   (<out> ^send-training-label <stl>)
   (<stl> ^id <id> ^label <label> ^category <cat>)
}

# Updates any objects with the word, for convenience
sp {object-message*apply*lookup-smem*annotate*object
   (state <s> ^name object-message
              ^topstate.object.property <prop>
              ^operator <o>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.category <cat>
        ^query.word <word>
        ^result {<label> <> failure})
   (<prop> ^category <cat>
           ^value <val>)
   (<val> ^label <label>
         -^word <word>)
-->
   (<val> ^word <word>)
}

# Fetch the label for the category-word pair from memory
sp {object-message*propose*lookup-smem*label
   (state <s> ^name object-message
              ^nounjective <nj>)
   (<nj> ^category <cat>
          ^word <word>
         -^label)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-smem
        ^category smem
        ^lookup-type label
        ^query <q>
        ^desired.label true)
   (<q> ^type label-map ^word <word> ^category <cat> ^label <label>)
}

# Successful retrieval
sp {object-message*apply*lookup-smem*label*success
   (state <s> ^name object-message
              ^operator <o>
              ^nounjective <nj>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.word <word>
        ^result {<label> <> failure})
   (<nj> ^word <word>)
-->
   (<nj> ^label <label>)
}

# Send a training label to the visual system
sp {object-message*apply*lookup-smem*label*send-training-label
   (state <s> ^name object-message
              ^io.output-link <out>
              ^operator <o>
              ^object.id <id>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.category <cat>
        ^result {<label> <> failure})
-->
   (<out> ^send-training-label <stl>)
   (<stl> ^id <id> ^label <label> ^category <cat>)
}

sp {object-message*apply*lookup-smem*label*name*object*existing-prop
   (state <s> ^name object-message
              ^operator <o>
              ^object <obj>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.category name
        ^query.word <word>
        ^result {<label> <> failure})
   (<obj> ^property <prop>)
   (<prop> ^category name
          -^value.label <label>)
-->
   (<prop> ^value <v>)
   (<v> ^label <label>
        ^word <word>
        ^confidence high)
}

sp {object-message*apply*lookup-smem*label*name*object*no-existing-prop
   (state <s> ^name object-message
              ^operator <o>
              ^object <obj>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.category name
        ^query.word <word>
        ^result {<label> <> failure})
   (<obj> -^property.category name)
-->
   (<obj> ^property <prop>)
   (<prop> ^category name
           ^value <v>)
   (<v> ^label <label>
        ^word <word>
        ^confidence high)
}
# If a label disappears from the input link this will remove the corresponding value structure

# There is a label on the input-link not in working memory
sp {propose*delete-value
   (state <s> ^topstate <s>
              ^object <obj>
              ^io.input-link.objects.object <io-obj>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^category <> name
           ^value <val>)
   (<val> ^label <label>)
   (<io-obj> ^id <id>
            -^category.<label>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name delete-value
        ^category object-processing
        ^object <obj>
        ^property <prop>
        ^value <val>)
}

# Remove the value from the property
sp {apply*delete-value*remove*value
   (state <s> ^operator <o>)
   (<o> ^name delete-value
        ^property <prop>
        ^value <val>)
-->
   (<prop> ^value <val> -)
}

# If there are no other values associated with that property, remove the property
sp {apply*delete-value*remove*property
   (state <s> ^operator <o>)
   (<o> ^name delete-value
        ^object <obj>
        ^property <prop>
        ^value <val>)
   (<prop> -^value <> <val>)
-->
   (<obj> ^property <prop> -)
}
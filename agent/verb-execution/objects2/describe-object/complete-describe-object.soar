sp {describe-object*propose*complete-describe-object
   (state <s> ^name describe-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-describe-object
        ^description <desc>)
}

sp {describe-object*elaborate*complete-describe-object*name
   (state <s> ^name describe-object
              ^operator <o>
              ^needs-found <nf>)
   (<o> ^name complete-describe-object
        ^description <desc>)
   (<nf> ^query name
         ^result <name>)
-->
   (<desc> ^word <name>)
}

sp {describe-object*elaborate*complete-describe-object*label
   (state <s> ^name describe-object
              ^operator <o>
              ^needs-found <nf>)
   (<o> ^name complete-describe-object
        ^description <desc>)
   (<nf> ^query <> name
         ^result {<word> <> failure})
-->
   (<desc> ^adjective <word>)
}

sp {describe-object*apply*complete-describe-object
   (state <s> ^name describe-object
              ^operator <o>
              ^object
              ^superstate.operator <ss-o>)
   (<o> ^name complete-describe-object
        ^description <desc>)
-->
   (<ss-o> ^description <desc>)
}

sp {describe-object*apply*complete-describe-object*failure
   (state <s> ^name describe-object
              ^operator.name complete-describe-object
             -^object
              ^superstate.operator <ss-o>)
-->
   (<ss-o> ^description failure)
}


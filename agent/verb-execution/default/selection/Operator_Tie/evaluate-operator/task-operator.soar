sp {evaluate-operator*propose*look-ahead
   :default
   (state <s> ^look-ahead-operator <o>
              ^untried-tied-operator <o>)
   -->
   (<s> ^operator <o> +)}

sp {evaluate-operator*propose*look-ahead*reject-all-others
   :default
   (state <s> ^look-ahead-operator <o>
              ^untried-tied-operator <o>
              ^operator { <> <o> <o1> } +)
   -->
   (<s> ^operator <o1> -)}

### Detect that the operator has been tried.

## SM 2012/01: allow composite-operators to finish application

sp {evaluate-operator*elaborate*state*tried-tied-operator*primitive-action
   :default
   (state <s> ^operator <o> 
              ^look-ahead-operator <o>)
   (<o> ^category primitive-action)
   -->
   (<s> ^tried-tied-operator <o>)}

sp {evaluate-operator*elaborate*state*tried-tied-operator*composite-action
   :default
   (state <s> ^superstate <ss>
	      ^desired <d>
	      ^success <d>)
   (<ss> ^operator <o> 
         ^look-ahead-operator <o>)
   (<o> -^category primitive-action)
   -->
   (<ss> ^tried-tied-operator <o>)}


# SBW 5/07: replaced all occurences of -^tried-tied-operator with ^untried-tied-operator
# this takes care of lots of local negations
sp {evaluate-operator*elaborate*state*remove*untried-tied-operator*primitive-action
   :default
   (state <s> ^look-ahead-operator <o> 
              ^operator <o>
              ^untried-tied-operator <o>)
   (<o> ^category primitive-action)
-->
   (<s> ^untried-tied-operator <o> -)
}

sp {evaluate-operator*elaborate*state*remove*untried-tied-operator*composite-action
   :default
   (state <s> ^superstate <ss>
   	      ^desired <d>
	      ^success <d>)
   (<ss> ^look-ahead-operator <o> 
         ^operator <o>
         ^untried-tied-operator <o>)
   (<o> ^category composite-action)
-->
   (<ss> ^untried-tied-operator <o> -)
}
# differs-from-group
#	Query for an object that differs from a group of objects in some way

# Basic query structure
sp {elaborate*ask-query*differs-from-group
   (state <s> ^operator <o> +)
   (<o> ^name ask-query
        ^query.type differs-from-group)
-->
   (<o> ^type different-attribute-question
         ^expected-response bare-attribute-response)
}

# Adds the different object to the fields structure
sp {elaborate*ask-query*differs-from-group*different-obj
   (state <s> ^operator <o>)
   (<o> ^name ask-query
        ^query <q>
        ^fields <f>)
   (<q> ^type differs-from-group
        ^different-obj.external-desc <external-desc>)
-->
   (<f> ^different-object <external-desc>)
}

# Adds group-obj(s) to query structure
sp {elaborate*ask-query*differs-from-group*group-obj
   (state <s> ^operator <o>)
   (<o> ^name ask-query
        ^query <q>
        ^fields <f>)
   (<q> ^type differs-from-group
        ^group-obj.external-desc <external-desc>)
-->
   (<f> ^similar-object <external-desc>)
}

# Adds exception(s) to query structure
sp {elaborate*ask-query*differs-from-group*exception
   (state <s> ^operator <o>)
   (<o> ^name ask-query
        ^query <q>
        ^fields <f>)
   (<q> ^type differs-from-group
        ^exception <att>)
-->
   (<f> ^exception <att>)
}

# copy-att-val
#   Copies each attribute/value pair from the input link into the
#     internal representation of that object

sp {create-object*propose*copy-att-val
   (state <s> ^name create-object
              ^io.input-link <in>
              ^object <obj>)
   (<in> ^objects.object <il-obj>)
   (<il-obj> ^id <id>
             ^category <cat>)
   (<cat> ^name <att>
          ^{<val> <> name} <confidence>)
   (<obj> ^id <id>
         -^mapping.att-val-pair.att.internal <att>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-att-val
        ^att <att>
        ^val <val>
        ^conf <confidence>)
}

sp {create-object*reject*lower-confidence
   (state <s> ^name create-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name create-object
         ^att <att>
         ^conf <c1>)
   (<o2> ^name create-object
         ^att <att>
         ^conf < <c1>)
-->
   (<s> ^operator <o2> -)
}

# Creates the new att-val-pair structure on the object
sp {create-object*apply*copy-att-val
   (state <s> ^name create-object
              ^object.mapping <map>
              ^operator <o>)
   (<o> ^name copy-att-val
        ^att <att>
        ^val <val>)
-->
   (<map> ^att-val-pair <avp>)
   (<avp> ^att <att-node>
          ^val <val-node>)
   (<att-node> ^internal <att>
               ^needs-retrieval true)
   (<val-node> ^internal <val>
               ^needs-retrieval true)
}
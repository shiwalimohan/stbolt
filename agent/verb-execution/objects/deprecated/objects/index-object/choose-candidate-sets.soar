# Rules that decide which candidate sets to use

# IT used
sp {index-object*elaborate*object-to-index*candidate-set*it
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-dialog-context true)
   (<obj> ^description.specifier << it >>)
-->
   (<obj> ^use-focus true)
}

sp {index-object*elaborate*object-to-index*candidate-set*it*no-dialog
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-dialog-context false)
   (<obj> ^description.specifier << it >>)
-->
   (<obj> ^use-all true)
}

# USE THIS/THAT
sp {index-object*elaborate*object-to-index*candidate-set*this*that
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-dialog-context true)
   (<obj> ^description.specifier << this that >>)
-->
   (<obj> ^use-focus true
          ^use-activation true)
}

sp {index-object*elaborate*object-to-index*candidate-set*this*that*no-dialog
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-dialog-context false
              ^topstate.use-activation-context true)
   (<obj> ^description.specifier << this that >>)
-->
   (<obj> ^use-activation true)
}

sp {index-object*elaborate*object-to-index*candidate-set*this*that*no-activation
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-activation-context false)
   (<obj> ^description.specifier << this that >>)
-->
   (<obj> ^use-all true)
}

# USE THE/DEF
sp {index-object*elaborate*object-to-index*candidate-set*definite
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-dialog-context true)
   (<obj> ^description.specifier << the DEF none >>)
-->
   (<obj> ^use-focus true
          ^use-activation true
          ^use-all true)
}

sp {index-object*elaborate*object-to-index*candidate-set*definite*no-dialog
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-dialog-context false
              ^topstate.use-activation-context true)
   (<obj> ^description.specifier << the DEF none >>)
-->
   (<obj> ^use-activation true
          ^use-all true)
}

sp {index-object*elaborate*object-to-index*candidate-set*definite*no-activation
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-activation-context false)
   (<obj> ^description.specifier << the DEF none >>)
-->
   (<obj> ^use-all true)
}


sp {index-object*elaborate*object-to-index*candidate-set*none
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-dialog-context true)
   (<obj> -^description.specifier)
-->
   (<obj> ^use-focus true
          ^use-activation true
          ^use-all true)
}

sp {index-object*elaborate*object-to-index*candidate-set*none*no-dialog
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-dialog-context false
              ^topstate.use-activation-context true)
   (<obj> -^description.specifier)
-->
   (<obj> ^use-activation true
          ^use-all true)
}

sp {index-object*elaborate*object-to-index*candidate-set*none*no-activation
   (state <s> ^name index-object
              ^object-to-index <obj>
              ^topstate.use-activation-context false)
   (<obj> -^description.specifier)
-->
   (<obj> ^use-all true)
}


# USE INDEF
sp {index-object*elaborate*object-to-index*candidate-set*indefinite
   (state <s> ^name index-object
              ^object-to-index <obj>)
   (<obj> ^description.specifier << a INDEF >>)
-->
   (<obj> ^use-all true)
}



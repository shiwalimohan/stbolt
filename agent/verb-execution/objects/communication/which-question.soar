# Adds a segment to the interaction stack to deal with the which question

sp {propose*push-which-question
   (state <s> ^topstate <s>
              ^instructor-message <msg>)
   (<msg> ^type object-question
          ^information.question-word which)
-->
   (<s> ^operator <o> +)
   (<o> ^name push-which-question
        ^category communication)
}

sp {apply*push-which-question
   (state <s> ^operator.name push-which-question
              ^instructor-message <msg>)
   (<msg> ^information.object <desc>)
-->
   (<msg> ^purpose <p>)
   (<p> ^type which-question
        ^parameters <params>
        ^satisfaction <sat>)
   (<sat> ^action-event point)
   (<params> ^description <desc>)
}

# Returns points to the described object

sp {which-question*propose*index-object
   (state <s> ^topstate <s>
             -^instructor-message <msg>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
          ^originator instructor)
   (<p> ^type which-question)
-->
   (<s> ^operator <o> +)
   (<o> ^name index-object
        ^return-all false
        ^use-interaction true
        ^allow-ties true
        ^category object-processing
        ^object <obj>)
}

sp {which-question*index-object*elaborate*word
   (state <s> ^operator <o> +
              ^interaction.stack.segment.purpose <p>)
   (<p> ^type which-question
        ^parameters.description.word {<word> <> object })
   (<o> ^name index-object
        ^object <obj>)
-->
   (<obj> ^word <word>)
}

sp {which-question*apply*index-object
   (state <s> ^operator <o>
              ^interaction.stack.segment.purpose.type which-question
              ^status <status>)
   (<o> ^name index-object
        ^finished true)
-->
   (<status> ^performed.action-event point)
}

sp {recogition-question*apply*failure*send-message
   (state <s> ^operator <o>
              ^io.output-link <out>
              ^interaction.stack.segment.purpose.type which-question)
   (<o> ^name index-object
        ^match failure)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type no-object
          ^fields <f>)
}

sp {recogition-question*apply*index-object*point
   (state <s> ^operator <o>
              ^io.output-link <out>
              ^topstate.object <obj>
              ^interaction.stack.segment.purpose.type which-question)
   (<o> ^name index-object
        ^match.id <id>)
   (<obj> ^id <id>
          ^pose <pose>)
-->
   (<out> ^point <point>)
   (<point> ^id <id>
            ^pose <pose>)
}

sp {finish*index-object*test*print*match*failure
   (state <s> ^topstate <s>
              ^interaction.stack.segment.purpose.type which-question
              ^operator <op>)
   (<op> ^name index-object
         ^match failure)
-->
   (write (concat |No Match| (crlf)))
}

sp {finish*index-object*test*print*match*success
   (state <s> ^topstate <s>
              ^interaction.stack.segment.purpose.type which-question
              ^operator <op>)
   (<op> ^name index-object
         ^match {<m> <> failure})
   (<m> ^id <id>)
-->
   (write (concat |Object Match: | <id> (crlf)))
}
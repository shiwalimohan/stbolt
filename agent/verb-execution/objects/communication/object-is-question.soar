# First index the object the question is being asked about

sp {object-is-question*propose*index-object
   (state <s> ^topstate <s>
             -^instructor-message <msg>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose.type object-is-question
          ^originator instructor
         -^info.object-match <m>)
-->
   (<s> ^operator <o> +)
   (<o> ^name index-object
        ^return-all false
        ^use-interaction true
        ^allow-ties true
        ^infer-match false
        ^category indexing)
}

sp {object-is-question*index-object*elaborate*single-object
   (state <s> ^operator <o> +
              ^interaction.stack.segment.purpose <p>)
   (<p> ^type object-is-question
        ^parameters.object <obj>
       -^parameters.relation)
   (<o> ^name index-object)
-->
   (<o> ^object <obj>)
}

sp {object-is-question*index-object*elaborate*relation
   (state <s> ^operator <o> +
              ^interaction.stack.segment.purpose <p>)
   (<o> ^name index-object)
   (<p> ^type object-is-question
        ^parameters.relation <rel>)
   (<rel> ^object1 <obj1>
          ^object2 <obj2>)
-->
   (<o> ^object <obj1>
        ^object <obj2>
        ^relation <rel>)
}

sp {object-is-question*apply*index-object
   (state <s> ^operator <o>
              ^interaction.stack.segment <seg>)
   (<o> ^name index-object
        ^finished true
        ^match <match>)
   (<seg> ^info <info>
          ^purpose.type object-is-question)
-->
   (<info> ^object-match <match>)
}

sp {object-is-question*apply*index-object*success*store*referent
   (state <s> ^operator <o>
              ^interaction.stack.segment.purpose.type object-is-question
              ^topstate.interaction.stack.segment.info <info>
              ^smem.command <cmd>)
   (<o> ^name index-object
        ^finished true
        ^match {<obj> <> failure})
   (<obj> ^smem-rep <rep>)
-->
   (<info> ^referent-object <obj>)
   (<cmd> ^store <rep>)
}

sp {object-is-question*propose*attend-to-scene
   (state <s> ^topstate <s>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose.type object-is-question
          ^originator instructor
          ^info <info>)
   (<info> ^object-match.id <id>
          -^attended-to <id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name attend-to-scene
        ^category smem
        ^use-all-objects false
        ^object <id>)
}

sp {object-is-question*apply*attend-to-scene
   (state <s> ^topstate <s>
              ^operator <o>
              ^interaction.stack.segment.info <info>)
   (<o> ^name attend-to-scene
        ^object <id>
        ^status complete)
-->
   (<info> ^attended-to <id>)
}
        

# Lookup the given word
sp {object-is-question*propose*lookup-word
   (state <s> ^topstate <s>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
          ^originator instructor
          ^info.attended-to <id>)
   (<p> ^type object-is-question
        ^parameters.questioned <q>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-word
        ^category smem
        ^word <q>
        ^use-interaction true)
}

sp {object-is-question*apply*lookup-word*mark*performed
   (state <s> ^operator.name lookup-word
              ^interaction.stack.segment.purpose.type object-is-question
              ^status <status>
              ^lookup-word-result <res>)
-->
	(<status> ^performed.dialog-event.type single-word-response)
    (<s> ^lookup-word-result <res> -)
}

sp {object-is-question*apply*lookup-word*match
   	(state <s> ^operator.name lookup-word
              ^interaction.stack.segment <seg>
              ^status <status>
              ^lookup-word-result <res>
              ^io.output-link <out>)
    (<seg> ^purpose.type object-is-question
           ^info.object-match <m>)
    (<res> ^sense.label <label>)
    (<m> ^property.label <label>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type single-word-response
          ^fields.word Yes)
}

sp {object-is-question*apply*lookup-label*no-match
    (state <s> ^operator.name lookup-word
              ^interaction.stack.segment <seg>
              ^status <status>
              ^lookup-word-result <res>
              ^io.output-link <out>)
    (<seg> ^purpose.type object-is-question
           ^info.object-match <m>)
    (<res> ^sense.label <label>)
    (<m> -^property.label <label>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type single-word-response
          ^fields.word No)
}

# call: index-object
sp {this-object-message*propose*index-object*this
   (state <s> ^name this-object-message
             -^this-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name index-object
        ^category object-processing
        ^object.specifier this
        ^return-all false
        ^use-interaction true
        ^allow-ties true)
}

sp {this-object-message*apply*index-object*success
   (state <s> ^name this-object-message
              ^operator <o>)
   (<o> ^name index-object
        ^finished true
        ^match {<obj> <> failure})
-->
   (<s> ^this-object <obj>)
}

sp {this-object-message*apply*index-object*success*store*referent
   (state <s> ^name this-object-message
              ^operator <o>
              ^topstate.interaction.stack.segment.info <info>)
   (<o> ^name index-object
        ^finished true
        ^match {<obj> <> failure})
-->
   (<info> ^referent-object <obj>)
}

sp {this-object-message*apply*index-object*failure
   (state <s> ^name this-object-message
              ^operator <o>)
   (<o> ^name index-object
        ^finished true
        ^match failure)
-->
   (<s> ^this-object failure)
}
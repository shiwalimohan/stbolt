# Operator: smem-retrieve
#   Used to lookup the structure for a Long-Term Identifier (LTI)

#(<s> ^operator <o> +)
#(<o> ^name smem-retrieve
#     ^identifier <lti>) <- long-term identifier (required)

# Will put a retrieve for the given identifier on the smem command link
# When finished, it will put a smem-retrieve-result on the state, 
#   Which will either be ^smem-retrieve-result <lti> or ^smem-retrieve-result failure

sp {apply*smem-retrieve*create-command
   (state <s> ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name smem-retrieve
        ^identifier <lti>)
-->
   (<cmd> ^retrieve <lti>)
}

sp {apply*smem-retrieve*remove*retrieve
   (state <s> ^operator <o>
              ^smem <smem>)
   (<o> ^name smem-retrieve
        ^identifier <lti>)
   (<smem> ^result.{<< success failure >>} <lti>
           ^command <cmd>)
   (<cmd> ^retrieve <lti>)
-->
   (<cmd> ^retrieve <lti> -)
}
  

sp {apply*smem-retrieve*success
   (state <s> ^operator <o>
              ^smem.result.success <lti>)
   (<o> ^name smem-retrieve
        ^identifier <lti>)
-->
   (<s> ^smem-retrieve-result <lti>)
}

sp {apply*smem-retrieve*failure
   (state <s> ^operator <o>
              ^smem.result.failure <lti>)
   (<o> ^name smem-retrieve
        ^identifier <lti>)
-->
   (<s> ^smem-retrieve-result failure)
}
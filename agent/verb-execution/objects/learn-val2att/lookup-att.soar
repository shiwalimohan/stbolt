# See if the attribute given matches one already known

sp {learn-val2att*propose*lookup-att
   (state <s> ^name learn-val2att
              ^external-att <external-att>
             -^internal-att)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-att
        ^category object-processing
        ^external <external-att>)
}

# Copy the retrieved attribute to the state
sp {learn-val2att*apply*lookup-att*mark*internal-att
   (state <s> ^name learn-val2att
              ^operator <o>)
   (<o> ^name lookup-att
        ^internal <internal-att>
        ^external <external-att>)
-->
   (<s> ^internal-att <internal-att>)
}

###### The attribute lookup failed ######
# Updates objects with the external-att representation
sp {learn-val2att*apply*lookup-att*failure
   (state <s> ^name learn-val2att
              ^operator <o>
              ^external-val <external-val>
              ^topstate.object.mapping.att-val-pair <av-pair>)
   (<o> ^name lookup-att
        ^internal failure
        ^external <external-att>)
   (<av-pair> ^val.external <external-val>
              ^att <att>)
-->
   (<att> ^external <external-att>)
}

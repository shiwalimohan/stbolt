sp {train-label*propose*send-training-label
   (state <s> ^name train-label
              ^example-object.property.name <prop-name>
              ^sense.property <prop>
             -^trained)
   (<prop> ^type << visual measurable >>
           ^il-name <prop-name>)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-training-label
        ^category object-processing)
}

sp {train-label*apply*send-training-label*send
   (state <s> ^name train-label
              ^operator.name send-training-label
              ^io.output-link <out>
              ^example-object.id <id>
              ^sense <sense>)
   (<sense> ^label <label>
            ^property.il-name <prop-name>)
-->
   (<out> ^send-training-label <stl>)
   (<stl> ^id <id> ^label <label> ^category <prop-name>)
   (<s> ^ol-action <stl>)
}

sp {send-training-label*propose*wait-for-ack
   (state <s> ^superstate.name train-label
              ^name send-training-label
             -^operator.name wait-for-ack)
-->
   (<s> ^operator <o> + >)
   (<o> ^name wait-for-ack
        ^category object-processing)
}


sp {train-label*apply*send-training-label*receive-ack
   (state <s> ^name train-label
              ^operator.name send-training-label
              ^ol-action <action>)
   (<action> ^status)
-->
   (<s> ^trained true
        ^ol-action <action> -)
}
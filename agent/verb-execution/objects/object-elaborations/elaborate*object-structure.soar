######################################
# Pose and Bounding Box
######################################
sp {elaborate*object*pose
   (state <s> ^topstate <s>
              ^io.input-link.objects.object <il-obj>
              ^object <obj>)
   (<il-obj> ^id <id>
             ^pose <pose>)
   (<obj> ^id <id>)
-->
   (<obj> ^pose <pose>)
}

sp {elaborate*object*bbox
   (state <s> ^topstate <s>
              ^io.input-link.objects.object <il-obj>
              ^object <obj>)
   (<il-obj> ^id <id>
             ^bbox <pose>)
   (<obj> ^id <id>)
-->
   (<obj> ^bbox <pose>)
}

#######################################
# Default Name
#######################################
sp {elaborate*object*default*name
   (state <s> ^topstate <s>
              ^object <obj>)
   (<obj> ^property <name>)
   (<name> ^name name
          -^value.word <> object)
-->
   (<name> ^value.word object)
}   
   
sp {elaborate*object*shape*name
   (state <s> ^topstate <s>
              ^object <obj>)
   (<obj> ^property <shape>
          ^property <name>)
   (<shape> ^name shape
            ^word <l>)
   (<name> ^name name)
-->
   (<name> ^value.word <l>)
}

######################################
# Confidences
######################################
# Uses the confidence information to give a estimate of how accurate the visual label is
sp {elaborate*visual-properties*confidences*high
   (state <s> ^topstate <s>
              ^object.property.value <value>)
   (<value> ^confidence-value > .7)
-->
   (<value> ^confidence high)
}

sp {elaborate*visual-properties*confidences*medium
   (state <s> ^topstate <s>
              ^object.property.value <value>)
   (<value> ^confidence-value <= .7 > .3)
-->
   (<value> ^confidence medium)
}

sp {elaborate*visual-properties*confidences*low
   (state <s> ^topstate <s>
              ^object.property.value <value>)
   (<value> ^confidence-value < .3)
-->
   (<value> ^confidence low)
}

########################################
# Unambiguous words/labels
########################################
sp {elaborate*object*word*unambiguous
   (state <s> ^topstate <s>
              ^object.property <prop>)
   (<prop> ^value.word <word>
          -^value.word <> <word>)
-->
   (<prop> ^word <word>)
}

sp {elaborate*object*word*using*label
   (state <s> ^topstate <s> 
              ^object.property <prop>)
   (<prop> ^label <label>
           ^value <value>)
   (<value> ^label <label>
            ^word <word>)
-->
   (<prop> ^word <word>)
}

sp {elaborate*object*label*unambiguous
   (state <s> ^topstate <s>
              ^object.property <prop>)
   (<prop> ^value.label <label>
          -^value.label <> <label>)
-->
   (<prop> ^label <label>)
}

# Choose the word with the highest confidence
sp {elaborate*object*label*high*confidence
   (state <s> ^topstate <s>
              ^object.property <prop>)
   (<prop> ^value <v1>)
   (<v1> ^label <label>
         ^confidence high)
   -{(<prop> ^value <v2>)
     (<v2> ^label <> <label>
           ^confidence high)}
-->
   (<prop> ^label <label>)
}

sp {elaborate*object*label*medium*confidence
   (state <s> ^topstate <s>
              ^object.property <prop>)
   (<prop> ^value <v1>)
   (<v1> ^label <label>
         ^confidence medium)
   -{(<prop> ^value <v2>)
     (<v2> ^label <> <label>
           ^confidence << high medium >>)}
-->
   (<prop> ^label <label>)
}


            
# If there is an object with a known attribute and unknown value then 
#	the user is asked what that value is

sp {propose*value-question
   (state <s> ^superstate nil
              ^object <obj>
             -^queries.query.att-val-pair <av-pair>)
   (<obj> ^mapping.att-val-pair <av-pair>
          ^unique true)
   (<av-pair> ^att.external <external-att>
             -^val.external)
-->
   (<s> ^operator <o> + =)
   (<o> ^name value-question
        ^category object-processing
        ^query <q>)
   (<q> ^type value-question
        ^object <obj>
        ^att-val-pair <av-pair>)
}

# Puts a query structure on the queries structure
sp {apply*value-question
   (state <s> ^io.output-link <out>
              ^operator <o>
              ^queries <qs>)
   (<o> ^name value-question
        ^query <q>)
   (<q> ^object <obj>
        ^att-val-pair <av-pair>)
-->
   (<qs> ^query <q2>)
   (<q2> ^type value-question
         ^object <obj>
         ^att-val-pair <av-pair>)
}
         
##############################################################
# lookup-category
#   For each property with a word, look up that word's category
###############################################################

# For each property, look up the category
sp {index-object*propose*lookup-smem*category
   (state <s> ^name index-object
              ^index-object-context.object.property <prop>
              ^parameters.use-interaction <ask>)
   (<prop> -^category
            ^word <word>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-smem
        ^category smem
        ^ask-user <ask>
        ^lookup-type category
        ^query <q>
        ^desired.category true)
   (<q> ^type word2cat ^word <word> ^category <cat>)
}

# go through words in alphabetical order
sp {index-object*prefer*lookup-smem*category*lower-word
   (state <s> ^name index-object
              ^operator <o1> + 
              ^operator <o2> +)
   (<o1> ^name lookup-smem
         ^lookup-type category
         ^query.word <w1>)
   (<o2> ^name lookup-smem
         ^lookup-type category
         ^query.word <w2> < <w1>)
-->
   (<s> ^operator <o1> < <o2>)
}

# lookup-category success: mark the property
sp {index-object*apply*lookup-smem*category*success
   (state <s> ^name index-object
              ^operator <o>
              ^index-object-context.object.property <prop>)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result {<result> <> failure}
       -^multiple-results)
   (<prop> ^word <word>)
-->
   (<prop> ^category <result>)
}

# Failed Retrieval and wont ask user, remove the property from the object
sp {index-object*apply*lookup-smem*category*failure*remove*property
   (state <s> ^name index-object
              ^operator <o>
              ^index-object-context.object <obj>
              ^parameters.use-interaction false)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result failure)
   (<obj> ^property <prop>)
   (<prop> ^word <word>)
-->
   (<obj> ^property <prop> -)
}

# IF the retrieval was a generic word, then remove it
sp {index-object*apply*lookup-smem*category*generic*remove
   (state <s> ^name index-object
              ^operator <o>
              ^index-object-context.object <obj>)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result generic)
   (<obj> ^property <prop>)
   (<prop> ^word <word>)
-->
   (<obj> ^property <prop> -)
}

##############################################################
# lookup-label
#   For each word/category pair, look up the internal label
###############################################################

# Fetch the label for the category-word pair from memory
sp {index-object*propose*lookup-smem*label
   (state <s> ^name index-object
              ^index-object-context.object.property <prop>)
   (<prop> ^word <word>
           ^category {<cat> <> name }
          -^label)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-smem
        ^category smem
        ^lookup-type label
        ^query <q>
        ^desired.label true)
   (<q> ^type label-map ^word <word> ^category <cat> ^label <label>)
}

# Successful retrieval - add the label to the object
sp {index-object*apply*lookup-smem*label*success*add*to*object
   (state <s> ^name index-object
              ^operator <o>
              ^index-object-context.object.property <prop>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.word <word>
        ^result {<label> <> failure})
   (<prop> ^word <word>)
-->
   (<prop> ^label <label>)
}



sp {attend-to-scene*propose*create-state-value
   (state <s> ^name attend-to-scene
              ^object-pair <op>
              ^parameters.create-delete-only false)
   (<op> ^il <il-obj>
         ^ts <ts-obj>)
   (<il-obj> ^property <il-prop>)
   (<il-prop> ^type state
              ^name <prop-name>
              ^value <word>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^name <prop-name>
             -^value.word <word>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-state-value
        ^category object-processing
        ^level value
        ^property <ts-prop>
        ^value <word>)
}

sp {attend-to-scene*reject*create-state-value*not-specified
   (state <s> ^name attend-to-scene
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name create-state-value
        ^property.name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-scene*apply*create-state-value
   (state <s> ^name attend-to-scene
              ^operator <o>)
   (<o> ^name create-state-value
        ^property <ts-prop>
        ^value <word>)
-->
   (<ts-prop> ^value.word <word>)
}
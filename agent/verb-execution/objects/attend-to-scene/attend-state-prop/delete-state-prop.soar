sp {attend-to-scene*propose*delete-state-prop
   (state <s> ^name attend-to-scene
              ^object-pair <op>
              ^parameters.create-delete-only false)
   (<op> ^il <il-obj>
         ^ts <ts-obj>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^type state
              ^name <prop-name>)
   (<il-obj> -^property.name <prop-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name delete-state-prop
        ^category object-processing
        ^level property
        ^property <ts-prop>
        ^object <ts-obj>)
}

sp {attend-to-scene*reject*delete-state-prop*not-specified
   (state <s> ^name attend-to-scene
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name delete-state-prop
        ^property.name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-scene*apply*delete-state-prop
   (state <s> ^name attend-to-scene
              ^operator <o>)
   (<o> ^name delete-state-prop
        ^property <prop>
        ^object <obj>)
-->
   (<obj> ^property <prop> -)
}
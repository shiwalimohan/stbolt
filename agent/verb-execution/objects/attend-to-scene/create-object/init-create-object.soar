sp {create-object*propose*create-object-structure
   (state <s> ^name create-object
             -^new-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-object-structure
        ^category object-processing)
}

sp {create-object*apply*create-object-structure*temp-object
   (state <s> ^name create-object
              ^operator.name create-object-structure
              ^source-object.id <id>)
-->
   (<s> ^temp-object <obj>)
   (<obj> ^id <id>)
}

sp {create-object*apply*create-object-structure*name-property
   (state <s> ^name create-object
              ^operator.name create-object-structure
              ^temp-object <obj>)
-->
   (<obj> ^property <prop>)
   (<prop> ^name name
           ^type linguistic)
}

sp {create-object*apply*create-object-structure*copy*name
   (state <s> ^name create-object
              ^operator.name create-object-structure
              ^temp-object.property <prop>
              ^source-object.name <name>)
   (<prop> ^name name)
-->
   (<prop> ^value.word <name>)
}

sp {create-object*apply*create-object-structure*copy*structure
   (state <s> ^name create-object
              ^operator.name create-object-structure
              ^temp-object <obj>)
   (<obj> ^property.name name)
-->
   (<s> ^temp-object <obj> -
        ^new-object <obj>)
}
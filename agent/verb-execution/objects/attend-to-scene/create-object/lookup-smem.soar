sp {create-object*propose*lookup-smem
   (state <s> ^name create-object
              ^new-object <obj>)
   (<obj> -^smem-rep)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-smem
        ^category object-processing)
}

sp {create-object*apply*lookup-smem
   (state <s> ^name create-object
              ^operator.name lookup-smem
              ^smem.command <cmd>
              ^new-object.id <id>)
-->
   (<cmd> ^query <q>)
   (<q> ^wme-type object
        ^id <id>)
}

sp {create-object*apply*lookup-smem*failure*create*rep
   (state <s> ^name create-object
              ^operator.name lookup-smem
              ^new-object <obj>
              ^smem <smem>)
   (<smem> ^result.failure <cue>
           ^command <cmd>)
   (<cmd> ^query <cue>)
   (<obj> ^id <id>)
-->
   (<cmd> ^query <cue> -)
   (<obj> ^smem-rep <rep>)
   (<rep> ^id <id>
          ^wme-type object)
}
        
sp {create-object*apply*lookup-smem*success*copy*rep
   (state <s> ^name create-object
              ^operator.name lookup-smem
              ^new-object <obj>
              ^smem <smem>)
   (<smem> ^result.success <cue>
           ^result.retrieved <lti>
           ^command <cmd>)
   (<cmd> ^query <cue>)
-->
   (<cmd> ^query <cue> -)
   (<obj> ^smem-rep <lti>)
}

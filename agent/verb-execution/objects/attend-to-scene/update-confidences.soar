sp {attend-to-scene*propose*update-confidences
   (state <s> ^name attend-to-scene
              ^parameters.create-delete-only false
             -^updated-confidences)
-->
   (<s> ^operator <o> +)
   (<o> ^name update-confidences
        ^category object-processing
        ^level confidence)
}

sp {attend-to-scene*apply*update-confidences*add
   (state <s> ^name attend-to-scene
              ^operator.name update-confidences
              ^object-pair <op>)
   (<op> ^il <il-obj>
         ^ts <ts-obj>)
   (<il-obj> ^property <il-prop>)
   (<il-prop> ^type visual
              ^name <prop-name>
              ^values.<label> <conf>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^name <prop-name>
              ^value <val>)
   (<val> ^label <label>)
-->
   (<val> ^confidence-value <conf>)
}

sp {attend-to-scene*apply*update-confidences*remove
   (state <s> ^name attend-to-scene
              ^operator.name update-confidences
              ^object-pair <op>)
   (<op> ^il <il-obj>
         ^ts <ts-obj>)
   (<il-obj> ^property <il-prop>)
   (<il-prop> ^type visual
              ^name <prop-name>
              ^values.<label>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^name <prop-name>
              ^value <val>)
   (<val> ^label <label>
          ^confidence-value <cv>)
-->
   (<val> ^confidence-value <cv> -)
}

sp {attend-to-scene*apply*update-confidences*mark*updated
   (state <s> ^name attend-to-scene
              ^operator.name update-confidences)
-->
   (<s> ^updated-confidences true)
}
sp {attend-to-scene*propose*create-visual-prop
   (state <s> ^name attend-to-scene
              ^object-pair <op>
              ^parameters.create-delete-only false)
   (<op> ^il <il-obj>
         ^ts <ts-obj>)
   (<il-obj> ^property <il-prop>)
   (<il-prop> ^type visual
              ^name <prop-name>)
   (<ts-obj> -^property.name <prop-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-visual-prop
        ^category object-processing
        ^level property
        ^property <il-prop>
        ^object <ts-obj>)
}

sp {attend-to-scene*reject*create-visual-prop*not-specified
   (state <s> ^name attend-to-scene
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name create-visual-prop
        ^property.name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-scene*apply*create-visual-prop
   (state <s> ^name attend-to-scene
              ^operator <o>)
   (<o> ^name create-visual-prop
        ^property <prop>
        ^object <obj>)
   (<prop> ^name <prop-name>)
-->
   (<obj> ^property <prop-copy>)
   (<prop-copy> ^name <prop-name>
                ^type visual)
}
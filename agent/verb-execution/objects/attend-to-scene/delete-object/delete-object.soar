sp {attend-to-scene*propose*delete-object
   (state <s> ^name attend-to-scene
              ^topstate.object <obj>
              ^io.input-link <in>)
   (<in> -^objects.object.id <id>
# XXX: Not Needed?          ^self.action << wait failure >>
          ^time.seconds <cur-time>)
   (<obj> ^id <id>
          ^delete-time < <cur-time>
         -^held)
-->
   (<s> ^operator <o> + =)
   (<o> ^name delete-object
        ^category object-processing
        ^level object
        ^object <obj>)
}

sp {attend-to-scene*reject*delete-object*not-specified
   (state <s> ^name attend-to-scene
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name delete-object
        ^object.id <id>)
   (<p> ^use-all-objects false
       -^use-object <id>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-scene*apply*delete-object
   (state <s> ^name attend-to-scene
              ^operator <o>
              ^topstate <top>)
   (<o> ^name delete-object 
        ^object <obj>)
-->
   (<top> ^object <obj> -)
}

sp {index-object*propose*lookup-word
   (state <s> ^name index-object
              ^index-object-context.object.property <prop>
              ^parameters.use-interaction <ask>)
   (<prop> -^category
            ^word <word>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-word
        ^category smem
        ^word <word>
        ^use-interaction <ask>)
}

# go through words in alphabetical order
sp {index-object*prefer*lookup-word*lower-word
   (state <s> ^name index-object
              ^operator <o1> + 
              ^operator <o2> +)
   (<o1> ^name lookup-word
         ^word <w1>)
   (<o2> ^name lookup-word
         ^word <w2> < <w1>)
-->
   (<s> ^operator <o1> < <o2>)
}

sp {index-object*apply*lookup-word*remove*result
   (state <s> ^name index-object
              ^operator.name lookup-word
              ^lookup-word-result <res>)
-->
   (<s> ^lookup-word-result <res> -)
}

# lookup-category success: mark the property
sp {index-object*apply*lookup-word*success
   (state <s> ^name index-object
              ^operator.name lookup-word
              ^lookup-word-result <res>
              ^index-object-context.object.property <prop>)
   (<res> ^word <word> ^sense <sense>)
   (<sense> ^label <label>
            ^property.name <prop-name>)
   (<prop> ^word <word>)
-->
   (<prop> ^category <prop-name>
           ^label <label>)
}

# If the word is a generic, remove it from the object
sp {index-object*apply*lookup-word*remove*generic
   (state <s> ^name index-object
              ^operator <o>
              ^index-object-context.object <obj>
              ^lookup-word-result <res>)
   (<o> ^name lookup-word
        ^word <word>)
   (<obj> ^property <prop>)
   (<prop> ^word <word>)
   (<res> ^word <word>
          ^sense.property.name generic)
-->
   (<obj> ^property <prop> -)
}

# Special case where the word is the name of the object
sp {index-object*apply*lookup-word*copy*name
   (state <s> ^name index-object
              ^operator <o>
              ^index-object-context.object <obj>
              ^lookup-word-result <res>)
   (<o> ^name lookup-word
        ^word <word>)
   (<obj> ^property <prop>)
   (<prop> ^word <word>)
   (<res> ^word <word>
          ^sense.property.name name)
-->
   (<prop> ^category name)
}

# Failed Retrieval and wont ask user, remove the property from the object
sp {index-object*apply*lookup-word*failure*remove*property
   (state <s> ^name index-object
              ^operator <o>
              ^lookup-word-result failure
              ^index-object-context.object <obj>
              ^parameters.use-interaction false)
   (<o> ^name lookup-word
        ^word <word>)
   (<obj> ^property <prop>)
   (<prop> ^word <word>)
-->
   (<obj> ^property <prop> -)
}

sp {index-object*elaborate*final-matches
   (state <s> ^name index-object)
-->
   (<s> ^final-matches <fm>)
}

# Elaborations for copying parameters from the top state

sp {index-object*elaborate*label-query
   (state <s> ^name index-object
              ^index-object-context.object.property <prop>)
   (<prop> ^word <word>
          -^label <label>)
-->
   (<s> ^label-query.word <word>)
}

sp {index-object*elaborate*boolean*parameter*copy
   (state <s> ^name index-object
              ^superstate.operator <ss-op>)
   (<ss-op> ^{<par> << use-interaction allow-ties return-all >>} <val>)
-->
   (<s> ^<par> <val>)
}

sp {index-object*elaborate*use-interaction*default
   (state <s> ^name index-object
              -^superstate.operator.use-interaction)
-->
   (<s> ^use-interaction false)
}

sp {index-object*elaborate*allow-ties*default
   (state <s> ^name index-object
              -^superstate.operator.allow-ties)
-->
   (<s> ^allow-ties false)
}

sp {index-object*elaborate*return-all*default
   (state <s> ^name index-object
              -^superstate.operator.return-all)
-->
   (<s> ^return-all false)
}

# Added to the final-matches WME when there are no relations
sp {index-object*elaborate*final-matches*object
   (state <s> ^name index-object
              ^final-matches <fm>
              ^index-object-context <context>)
   (<context> ^object <obj>
             -^relation)
-->
   (<fm> ^object <obj>)
}


sp {index-object*elaborate*match*failure*use-interaction*false
   (state <s> ^name index-object
              ^final-matches <fm>
              ^index-object-context.use-interaction false
              ^object-matches <matches>)
   (<matches> -^match <> failure)
-->
   (<fm> ^match failure)
}

sp {index-object*elaborate*match*failure*no-teaching
   (state <s> ^name index-object
              ^final-matches <fm>
              ^object-matches <matches>)
   (<matches> -^match <> failure
               ^object.no-teaching)
-->
   (<fm> ^match failure)
}

sp {index-object*elaborate*final-match*no-relation
   (state <s> ^name index-object
              ^final-matches <fm>
              ^object-matches.match {<m> <> failure}
             -^index-object-context.relation)
-->
   (<fm> ^match <m>)
}
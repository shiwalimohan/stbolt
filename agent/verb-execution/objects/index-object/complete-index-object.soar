sp {complete*index-object*mark-finished
   (state <s> ^name index-object
              ^operator.name << index-object-failure complete-index-object >>
              ^superstate.operator <ss-op>)
-->
   (<ss-op> ^finished true)
}

sp {complete*index-object*destroy-context
   (state <s> ^operator <o>
              ^topstate <top>)
   (<o> ^name index-object
        ^finished true)
   (<top> ^index-object-context <context>)
-->
   (<top> ^index-object-context <context> -)
}

# As soon as we know that the index will fail, we mark it on the superstate operator
sp {index-object*propose*index-object-failure
   (state <s> ^name index-object
              ^final-matches.match failure)
-->
   (<s> ^operator <o> +)
   (<o> ^name index-object-failure
        ^category indexing)
}

sp {index-object*apply*index-object-failure
   (state <s> ^name index-object
              ^operator.name index-object-failure
              ^superstate.operator <ss-op>)
-->
   (<ss-op> ^match failure)
}

# complete-index-object: copy matches to the superstate operator
sp {index-object*propose*complete-index-object
   (state <s> ^name index-object
              ^final-matches.match {<m> <> failure})
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-index-object
        ^category indexing
        ^match <m>)
}

sp {index-object*elaborate*complete-index-object*allow-ties*false
   (state <s> ^name index-object
              ^allow-ties false
              ^operator <o> +)
   (<o> ^name complete-index-object)
-->
   (<s> ^operator <o> =)
}

sp {index-object*elaborate*complete-index-object*return-all*true
   (state <s> ^name index-object
              ^return-all true
              ^operator <o> +
              ^final-matches.match {<m> <> failure})
   (<o> ^name complete-index-object)
-->
   (<o> ^match <m>)
}

sp {index-object*apply*complete-index-object*copy-match
   (state <s> ^name index-object
              ^superstate.operator <ss-op>
              ^operator <o>)
   (<o> ^name complete-index-object
        ^match <m>)
-->
   (<ss-op> ^match <m>)
}
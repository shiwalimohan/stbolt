### if indexing configuration is set to focus-based-resolution, prefer matches that are on the interaction stack

sp {index-object*elaborate*focus-based-resolution
   (state <s> ^name index-object
   	      ^topstate <ts>
	      ^operator <o> +)
   (<ts> ^configuration.indexing.objects.focus-based-resolution true
	 ^interaction.referents.objects.object <obj>)
   (<o> ^match <obj>)
-->
   (<s> ^operator <o> >)
   (<o> ^match-type focus)
}

### if allow-ties=false, then return one match on the interaction stack
# sp {index-object*elaborate*focus-based-resolution*all-ties*false
#    (state <s> ^name index-object
#               ^allow-ties false
#               ^operator <o> +
# 	      ^topstate.indexing.objects.focus-based-resolution true)
#    (<o> ^name complete-index-object
#    	^match-type focus)
# -->
#    (<s> ^operator <o> =)
# }

### if allow-ties=false, then return one match on the interaction stack
sp {index-object*elaborate*focus-based-resolution*all-ties*false
   (state <s> ^name index-object
              ^operator <o1> +
	      ^operator <o2> +)
   (<o1> ^match-type focus)
   (<o2> ^match-type focus)
-->
   (<s> ^operator <o1> = <o2>)
}

### if return-all=true, then return all matches
sp {index-object*elaborate*focus-based-resolution*return-all*true
   (state <s> ^name index-object
              ^return-all true
              ^operator <o>
              ^final-matches.match {<m> <> <obj> <> failure}
	      ^topstate.configuration.indexing.objects.focus-based-resolution true
	      ^topstate.interaction.referents.objects.object <m>)
   (<o> ^name complete-index-object
   	^match-type focus
	^match <obj>)
-->
   (<o> ^match <m>)
}
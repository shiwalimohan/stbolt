# 0: create-context
sp {index-object*prefer*create-context*more
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name create-context)
   (<o2> ^name << lookup-word add-candidates attend-to-scene find-matches >>)
-->
   (<s> ^operator <o1> > <o2>)
}

# 1: lookup-word
sp {index-object*prefer*lookup*over*add-candidates
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name lookup-word)
   (<o2> ^name << add-candidates attend-to-scene find-matches >>)
-->
   (<s> ^operator <o1> > <o2>)
}

# 2: attend-to-scene (create-delete)
sp {index-object*prefer*attend-to-scene*create-delete
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene
         ^type create-delete)
   (<o2> ^name << add-candidates find-matches >>)
-->
   (<s> ^operator <o1> > <o2>)
}                  

# 3: add-candidates (focus > activation > all)
sp {index-object*prefer*add-candidates*over*find-matches
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name add-candidates)
   (<o2> ^name find-matches)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {index-object*add-candidates*prefer*focus*others
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name add-candidates
         ^candidate-type focus)
   (<o2> ^name add-candidates
         ^candidate-type <> focus)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {index-object*add-candidates*activation*over*all
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name add-candidates
         ^candidate-type activation)
   (<o2> ^name add-candidates
         ^candidate-type all)
-->
   (<s> ^operator <o1> > <o2>)
}

# 4: attend-to-scene (candidates)
sp {index-object*preferattend-to-scene*candidates*more
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene
         ^type candidates)
   (<o2> ^name find-matches)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {index-object*preferattend-to-scene*candidates*less
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name add-candidates)
   (<o2> ^name attend-to-scene
         ^type candidates)
-->
   (<s> ^operator <o1> > <o2>)
}

# 5: find-matches

# Last: complete-index-object
sp {index-object*prefer*all*over*complete-index-object
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name <> complete-index-object
         ^category indexing)
   (<o2> ^name complete-index-object)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {index-object*prefer*all*over*index-object-failure
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name <> index-object-failure
         ^category indexing)
   (<o2> ^name index-object-failure)
-->
   (<s> ^operator <o1> > <o2>)
}



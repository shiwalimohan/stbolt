### if indexing configuration is set to activation-based-resolution, prefer matches that are highly activated

sp {index-object*elaborate*activation-based-resolution
   (state <s> ^name index-object
   	      ^topstate <ts>
	      ^operator <o> +
	      ^highest-activation <obj>)
   (<ts> ^configuration.indexing.objects.activation-based-resolution true)
   (<o> ^match <obj>)
-->
   (<s> ^operator <o> >)
   (<o> ^match-type activation)
}

### prefer objects in focus over objects that are highly activated
sp {index-object*elaborate*preference*focus*activation
   (state <s> ^name index-object
   	      ^topstate <ts>
	      ^operator <o1> +
	      ^operator {<o2> <> <o1>} +)
   (<o1> ^match-type focus)
   (<o2> ^match-type activation)	 
-->
   (<s> ^operator <o1> > <o2>)
}


#### determine which object has the highest activation

sp {index-object*create-cue*look-up*object*smem
   (state <s> ^name index-object)
-->
   (<s> ^description <cue>)
}

sp {index-object*populate*cue*description
   (state <s> ^name index-object
   	      ^description <cue>
	      ^index-object-context.object.property <n>)
   (<n> ^category <arg>
   	^label <val>)
-->
   (<cue> ^<arg> <val>)
}

sp {cue*semantic-memory*object*activation
   (state <s> ^name index-object
   	      ^description <desc>
	      ^smem.command <scomm>
	      ^operator.name identify-object)
-->
   (<scomm> ^query <desc>)
}

sp {smem*object*object*found
   (state <s> ^name index-object
   	      ^smem.result.retrieved <act-obj>)
-->
   (<s> ^highest-activation <act-obj>)
}
  
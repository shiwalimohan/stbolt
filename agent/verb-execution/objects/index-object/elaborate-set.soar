### elaborate the RE
sp {copy*referring*expression
   (state <s> ^name index-object
   	      ^superstate.operator.object <obj>)
-->
   (<s> ^referring-expression <obj>)
}

### elaborate the set in which the objects should match based on the RE used
#### 'it' matches to objects on the interaction stack
sp {elaborate*context*it
   (state <s> ^name index-object
	      ^referring-expression <cc>
	      ^topstate.interaction.referents.objects <ref>)
   (<cc> ^specifier it)
-->
   (<s> ^set <ref>)
 }

### this/that NP matches to highly activated objects
sp {elaborate*context*that
   (state <s> ^name index-object
	      ^referring-expression <cc>
	      ^activated <act>
	      ^topstate.interaction.referents.objects <ref>)
   (<cc> ^specifier << this that >>)
-->
   (<s> ^set <act>)
}

#### 'the'/'a' matches to all objects
sp {elaborate*context*topstate*the*a
   (state <s> ^name index-object
	      ^referring-expression <cc>
	      ^topstate <ts>)
   (<cc> ^specifier << DEF INDEF >>)
-->
   (<s> ^set <ts>)
}


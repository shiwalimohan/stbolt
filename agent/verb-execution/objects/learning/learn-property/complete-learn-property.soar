# Stores the property information in semantic memory and marks the 
#   property as learned on the supersate.operator
sp {learn-property*propose*complete-learn-property
   (state <s> ^name learn-property)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-learn-property
        ^category smem)
}

sp {learn-property*prefer*all*over*complete-learn-property
   (state <s> ^name learn-property
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name <> complete-learn-property
         ^category smem)
   (<o2> ^name complete-learn-property)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {learn-property*apply*complete-learn-property*create-property
   (state <s> ^name learn-property
              ^operator.name complete-learn-property
              ^property-name <prop-name>
              ^property-type <prop-type>)
-->
   (<s> ^property <prop>)
   (<prop> ^wme-type property
           ^type <prop-type>
           ^name <prop-name>)
}

sp {learn-property*apply*complete-learn-property*copy*params
   (state <s> ^name learn-property
              ^operator.name complete-learn-property
              ^property <prop>
              ^{<param> << il-name measure-verb op-name >>} <val>)
-->
   (<prop> ^<param> <val>)
}

sp {learn-property*apply*complete-learn-property*store
   (state <s> ^name learn-property
              ^operator.name complete-learn-property
              ^property <prop>
              ^smem.command <cmd>)
-->
   (<cmd> ^store <prop>)
   (<s> ^stored true)
}

sp {learn-property*apply*complete-learn-property*mark*learned
   (state <s> ^name learn-property
              ^operator.name complete-learn-property
              ^superstate.operator <ss-op>
              ^stored true)
-->
   (<ss-op> ^learned true)
}
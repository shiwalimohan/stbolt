# Ask the instructor what type of property this is (visual, measurable, etc.)
sp {learn-property*propose*ask-property-type
   (state <s> ^name learn-property
              ^property-name <prop-name>
             -^property-type)
-->
   (<s> ^operator <o> +)
   (<o> ^name ask-property-type
        ^category communication)
}

sp {learn-property*apply*ask-property-type*create*status
   (state <s> ^name learn-property 
              ^operator.name ask-property-type
              ^property-name <prop-name>
              ^topstate.status <status>)
-->
   (<status> ^type ask-property-type
         ^state-name topstate
         ^purpose <p>)
   (<p> ^type ask-property-type
        ^context <context>
        ^parameters <params>
        ^satisfaction.dialog-event.type object-message)
   # Segment context - the word being asked about
   (<context> ^cause ask-property-type
              ^property <prop-name>)
   # Segment parameters - A message to send 
   (<params> ^message <msg>)
   (<msg> ^type ask-property-type
          ^originator agent
          ^fields.property <prop-name>)
}
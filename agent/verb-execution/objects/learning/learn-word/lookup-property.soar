# If the type of the word has been given (property-name)
#   then look up that property in smem 
sp {learn-word*propose*lookup-property
   (state <s> ^name learn-word
              ^property-name {<prop-name> <> comparison}
             -^property <prop>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-property
        ^category smem
        ^property-name <prop-name>
        ^use-interaction true)
}

sp {learn-word*reject*lookup-property*generic
   (state <s> ^name learn-word
              ^operator <o> +)
   (<o> ^name lookup-property
        ^property-name << one property >>)
-->
   (<s> ^operator <o> -)
}

sp {learn-word*apply*lookup-property
   (state <s> ^name learn-word
              ^operator.name lookup-property
              ^lookup-property-result <prop>)
-->
   (<s> ^property <prop>
        ^lookup-property-result <prop> -)
}
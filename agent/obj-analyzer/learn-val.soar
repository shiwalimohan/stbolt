sp {propose*learn-val
   (state <s> ^name obj-analyzer
              ^learned-val <lv>)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name learn-val
        ^learned-val <lv>)
}

sp {apply*learn-val*update*smem
   (state <s> ^name obj-analyzer
              ^operator <o>
              ^smem.command <smem-cmd>)
   (<o> ^name learn-val
        ^learned-val <lv>)
   (<lv> ^internal <internal-val>
         ^external <external-val>)
-->
   (<smem-cmd> ^store <val>)
   (<val> ^type value ^internal <internal-val> ^external <external-val>)
}

sp {apply*learn-val*update*object
   (state <s> ^name obj-analyzer
              ^operator <o>
              ^object <obj>)
   (<o> ^name learn-val
        ^learned-val <lv>)
   (<lv> ^internal <internal-val>
         ^external <external-val>)
   (<obj> ^mapping.att-val-pair <av-pair>)
   (<av-pair> ^val <val>
              ^att.external <external-att>)
   (<val> ^internal <internal-val>)
-->
   (<obj> ^changed true
          ^<external-att> <external-val>)
   (<val> ^external <external-val>)
}

sp {apply*learn-val*remove*learned-val
   (state <s> ^name obj-analyzer
              ^operator <o>
              ^learned-val <lv>)
   (<o> ^name learn-val
        ^learned-val <lv>)
-->
   (<s> ^learned-val <lv> -)
}
              
sp {process-message*propose*mark-processed
   (state <s> ^name process-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name mark-processed)
}

sp {process-message*perfer*any*over*mark-processed
   (state <s> ^name process-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name mark-processed)
   (<o2> ^name <> mark-processed)
-->
   (<s> ^operator <o1> < <o2>)
}

sp {process-message*apply*mark-processed
   (state <s> ^name process-message
              ^operator.name mark-processed
              ^message.id <id>
              ^top-state <top>)
-->
   (<top> ^processed <id>)
}

sp {process-message*apply*mark-processed*remove*old*id
   (state <s> ^name process-message
              ^operator.name mark-processed
              ^message.id <id>
              ^top-state <top>)
   (<top> ^processed {<old-id> <> <id>})
-->
   (<top> ^processed <old-id> -)
}

sp {process-message*apply*mark-processed*remove*old*current-question
   (state <s> ^name process-message
              ^operator.name mark-processed
              ^top-state <top>)
   (<top> ^current-question <cq>)
-->
   (<top> ^current-question <cq> -)
}
#	Query for an unknown value for a known attribute
#	process-attribute-value operator
#		Associates the word with the unknown value and learns it
# 	Also removes obsolete queries from the top-state


# handles messages responding to attribute-value queries
sp {process-message*propose*process-attribute-value
   (state <s> ^name process-message
             -^learned-val.internal <internal-val>
              ^current-query.query <q>
              ^message.type bare-value-response)
   (<q> ^type attribute-value
         ^att-val-pair.val.internal <internal-val>)
-->
   (<s> ^operator <o> +)
   (<o> ^name process-attribute-value)
}

# Learns the value for that attribute
sp {process-message*apply*process-attribute-value
   (state <s> ^name process-message
              ^operator.name process-attribute-value
              ^message <msg>
              ^current-query.query.att-val-pair.val.internal <internal-val>)
   (<msg> ^type bare-value-response
          ^fields.value <response>)
-->
   (<s> ^learned-val <v>)
   (<v> ^internal <internal-val>
        ^external <response>)
}

# removes other queries asking for the same value
sp {process-message*apply*learn-val*remove*attribute-value
   (state <s> ^operator <o>
              ^top-state <top>)
   (<o> ^name learn-val
        ^learned-val.internal <internal-val>)
   (<top> ^query {<q> <> <current-query>}
          ^current-query.query <current-query>)
   (<q> ^type attribute-value
        ^att-val-pair.val.internal <internal-val>)
-->
   (<top> ^query <q> -)
}

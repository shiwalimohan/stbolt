#	Query for an object that differs from a group of objects in some way
#	process-differs-from-group operator
#		Associates the word with the unknown attribute and learns it

# handles messages responding to differs-from-group queries
sp {process-message*propose*process-differs-from-group
   (state <s> ^name process-message
             -^superstate.learned-att.internal <internal-att>
              ^current-query.query <q>)
   (<q> ^type differs-from-group
         ^internal-att <internal-att>)
-->
   (<s> ^operator <o> +)
   (<o> ^name process-differs-from-group)
}

# learns the attribute given in the message
sp {process-message*apply*process-differs-from-group
   (state <s> ^name process-message
              ^operator.name process-differs-from-group
              ^message <msg>
              ^current-query.query.internal-att <internal-att>
              ^top-state <top>)
   (<msg> ^type bare-attribute-response
          ^fields.attribute <response>)
-->
   (<top> ^learned-att <a>)
   (<a> ^internal <internal-att>
        ^external <response>)
}

# removes other queries asking for the same attribute
sp {process-message*apply*process-differs-from-group*remove*similar*queries
   (state <s> ^name process-message
              ^current-query.query <q>
              ^superstate <ss>)
   (<q> ^type differs-from-group
        ^internal-att <att>)
   (<ss> ^query {<query2> <> <q>})
   (<query2> ^type << differs-from-group shared-att-val >>
             ^internal-att <att>)
-->
   (<ss> ^query <query2> -)
}
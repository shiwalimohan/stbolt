#	Query for an object that differs from a group of objects in some way
#	process-differs-from-group operator
#		Associates the word with the unknown attribute and learns it

# handles messages responding to differs-from-group queries
sp {process-message*propose*process-differs-from-group
   (state <s> ^name process-message
             -^learned-att.internal <internal-att>
              ^current-query.query <q>
              ^message.type bare-attribute-response)
   (<q> ^type differs-from-group
         ^internal-att <internal-att>)
-->
   (<s> ^operator <o> +)
   (<o> ^name process-differs-from-group)
}

# learns the attribute given in the message
sp {process-message*apply*process-differs-from-group
   (state <s> ^name process-message
              ^operator.name process-differs-from-group
              ^message <msg>
              ^current-query.query.internal-att <internal-att>)
   (<msg> ^type bare-attribute-response
          ^fields.attribute <response>)
-->
   (<s> ^learned-att <a>)
   (<a> ^internal <internal-att>
        ^external <response>)
}

# removes other queries asking for the same attribute
sp {process-message*apply*learn-att*remove*differs-from-group
   (state <s> ^operator <o>
              ^top-state <top>)
   (<o> ^name learn-att
        ^learned-att.internal <internal-att>)
   (<top> ^query {<q> <> <current-query>}
          ^current-query.query <current-query>)
   (<q> ^type differs-from-group
        ^internal-att <internal-att>)
-->
   (<top> ^query <q> -)
}
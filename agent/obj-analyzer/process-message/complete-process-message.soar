# When no other operators are proposed to process the message then 
#	this operator will exit the substate by marking the message processed on the top-state

# The operator is always proposed, but given a worst preference
sp {process-message*propose*complete-process-message
   (state <s> ^name process-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-process-message)
}

# Marks the message processed on the top state
sp {process-message*apply*complete-process-message
   (state <s> ^name process-message
              ^operator.name complete-process-message
              ^message <msg>
              ^top-state <top>)
-->
   (<top> ^processed <msg>)
}

# Removes old processed messages from the top state
sp {process-message*apply*complete-process-message*remove*old*id
   (state <s> ^name process-message
              ^operator.name complete-process-message
              ^message <msg>
              ^top-state <top>)
   (<top> ^processed {<old-msg> <> <msg>})
-->
   (<top> ^processed <old-msg> -)
}

# Removes the current-query from the top-state
sp {process-message*apply*complete-process-message*remove*old*current-query
   (state <s> ^name process-message
              ^operator.name complete-process-message
              ^top-state <top>)
   (<top> ^current-query <cq>)
   (<cq> ^query <q>)
-->
   (<top> ^current-query <cq> -
          ^query <q> -)
}

# Removes the query from the top-state
sp {process-message*apply*complete-process-message*remove*old*query
   (state <s> ^name process-message
              ^operator.name complete-process-message
              ^current-query.query <q>
              ^top-state <top>)
   (<top> ^query <q>)
-->
   (<top> ^query <q> -)
}
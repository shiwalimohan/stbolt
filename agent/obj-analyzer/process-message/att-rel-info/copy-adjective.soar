sp {shared-att-val*propose*copy-adjective
   (state <s> ^name att-rel-info
             -^object.desc.<att> <adj>
              ^adjective <adj>
             -^copied <adj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-adjective
        ^adjective <adj>)
}

# If it finds the attribute somewhere else it copies it
sp {shared-att-val*elaborate*copy-adjective*attribute
   (state <s> ^name att-rel-info
              ^operator <o> +
              ^top-state.object.desc.{<att> <> unknown} <adj>)
   (<o> ^name copy-adjective
        ^adjective <adj>)
-->
   (<o> ^attribute <att>)
}

# Creates a new av-pair without the attibute
sp {att-rel-info*apply*copy-adjective*new*av-pair
   (state <s> ^name att-rel-info
              ^operator <o>
              ^object.mapping <map>)
   (<o> ^name copy-adjective
        ^adjective <adj>
       -^attribute)
-->
   (<map> ^att-val-pair <av-pair>)
   (<av-pair> ^att <att>
              ^val.external <adj>)
}

# Creates a new av-pair with the attribute
sp {att-rel-info*apply*copy-adjective*new*av-pair*known-att
   (state <s> ^name att-rel-info
              ^operator <o>
              ^object.mapping <map>)
   (<o> ^name copy-adjective
        ^adjective <adj>
        ^attribute <att>)
-->
   (<map> ^att-val-pair <av-pair>)
   (<av-pair> ^att.external <att>
              ^val.external <adj>)
}

sp {att-rel-info*apply*copy-adjective*mark*copied
   (state <s> ^name att-rel-info
              ^operator <o>)
   (<o> ^name copy-adjective
        ^adjective <adj>)
-->
   (<s> ^copied <adj>)
}
# Returns the description of the object currently being pointed to

# handles messages responding to description-request queries
sp {process-message*propose*process-description-request
   (state <s> ^name process-message
              ^message.type description-request
             -^put-on-ol
              ^io <io>
              ^top-state.object <obj>)
   (<io> ^input-link.pointed-object <id>
         ^output-link <ol>)
   (<obj> ^id <id>
          ^external-desc <desc>)
-->
   (<s> ^operator <o> +)
   (<o> ^name process-description-request
        ^query <q>)
   (<q> ^type description
        ^originator agent
        ^fields.object <desc>)
}

sp {process-message*apply*description-request*generic-name
   (state <s> ^name process-message
              ^operator <o>)
   (<o> ^name process-description-request
        ^query.fields.object <obj>)
   (<obj> -^word <> object)
-->
   (<obj> ^word object)
}

sp {process-message*apply*process-description-request
   (state <s> ^name process-message
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name process-description-request
        ^query <q>)
-->
   (<ol> ^query <q>)
   (<s> ^put-on-ol true)
}
# type-of-value
#	Query for the attribute of a known value
#	process-type-of-value operator
#		Associates the word with the unknown value and learns it
# 	Also removes obsolete queries from the top-state

# when the type-of-value is received it looks to see if it knows it from smem
sp {process-message*propose*lookup-att
   (state <s> ^name process-message
             -^internal-att
              ^current-query.query.type type-of-value
              ^message <m>)
   (<m> ^type bare-attribute-response
        ^fields.attribute <external-att>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-att
        ^external <external-att>)
}

sp {process-message*apply*lookup-att*failure
   (state <s> ^name process-message
              ^operator <o>
              ^current-query.query.value <external-val>
              ^top-state.object.mapping.att-val-pair <av-pair>)
   (<o> ^name lookup-att
        ^internal failure
        ^external <external-att>)
   (<av-pair> ^val.external <external-val>
              ^att <att>)
-->
   (<att> ^external <external-att>)
   (<s> ^internal-att failure)
}

sp {process-message*apply*lookup-att*success*mark-internal-att
   (state <s> ^name process-message
              ^operator <o>
              ^current-query.query.type type-of-value)
   (<o> ^name lookup-att
        ^internal {<internal-att> <> failure}
        ^external <external-att>)
-->
   (<s> ^internal-att <internal-att>)
}

sp {process-message*type-of-value*propose*comprehend-att
   (state <s> ^name process-message
              ^current-query.query.type type-of-value
              ^internal-att failure
             -^processed.<att-type> <attribute>
              ^top-state.object <obj>)
   (<obj> ^mapping.att-val-pair <av-pair>)
   (<av-pair> ^att-type {<< internal external >> <att-type>}
              ^att.<att-type> <attribute>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name comprehend-att
        ^attribute <attribute>
        ^type <att-type>)
}

sp {process-message*apply*lookup-att*success*learn-val
   (state <s> ^name process-message
              ^operator <o>
              ^current-query.query <q>
              ^top-state.object.mapping <map>)
   (<q> ^type type-of-value
        ^value <external-val>)
   (<o> ^name lookup-att
        ^internal {<internal-att> <> failure})
   (<map> ^att-val-pair.val.external <external-val>
          ^att-val-pair <av-pair>)
   (<av-pair> ^att.internal <internal-att>
              ^val.internal <internal-val>)
-->
   (<s> ^learned-val <v>)
   (<v> ^internal <internal-val>
        ^external <external-val>)
}

# Removes other queries that ask for the same value
sp {process-message*apply*process-type-of-value*remove*similar*queries
   (state <s> ^name process-message
              ^current-query.query <q>
              ^superstate <ss>)
   (<q> ^type type-of-value
        ^value <val>)
   (<ss> ^query {<query2> <> <q>})
   (<query2> ^type type-of-value
             ^value <val>)
-->
   (<ss> ^query <query2> -)
}


# removes other queries asking for the same value
sp {apply*learn-val*remove*type-of-value
   (state <s> ^operator <o>
              ^top-state <top>)
   (<o> ^name learn-val
        ^learned-val.internal <external-val>)
   (<top> ^query {<q> <> <current-query>}
          ^current-query.query <current-query>)
   (<q> ^type type-of-value
        ^value <external-val>)
-->
   (<top> ^query <q> -)
}

# Removes other queries that ask for the same value
sp {process-message*apply*process-type-of-value*remove*similar*queries
   (state <s> ^name process-message
              ^current-query.query <q>
              ^superstate <ss>)
   (<q> ^type type-of-value
        ^value <val>)
   (<ss> ^query {<query2> <> <q>})
   (<query2> ^type type-of-value
             ^value <val>)
-->
   (<ss> ^query <query2> -)
}

#	Query for a group of objects that share a common attribute
#	process-shared-att-val operator
#		Associates the word with the unknown attribute and learns it

# handles messages responding to shared-att-val queries
sp {process-message*propose*process-shared-att-val
   (state <s> ^name process-message
             -^learned-att.internal <internal-att>
              ^current-query.query <q>
              ^message.type bare-attribute-response)
   (<q> ^type shared-att-val
         ^internal-att <internal-att>)
-->
   (<s> ^operator <o> +)
   (<o> ^name process-shared-att-val)
}

# learns the attribute given in the message
sp {process-message*apply*process-shared-att-val
   (state <s> ^name process-message
              ^operator.name process-shared-att-val
              ^message <msg>
              ^current-query.query.internal-att <internal-att>)
  	(<msg> ^type bare-attribute-response
      		^fields.attribute <response>)
-->
   (<s> ^learned-att <a>)
   (<a> ^internal <internal-att>
        ^external <response>)
}

# removes other queries asking for the same attribute
sp {process-message*apply*learn-att*remove*shared-att-val
   (state <s> ^operator <o>
              ^top-state <top>)
   (<o> ^name learn-att
        ^learned-att.internal <internal-att>)
   (<top> ^query {<q> <> <current-query>}
          ^current-query.query <current-query>)
   (<q> ^type shared-att-val
        ^internal-att <internal-att>)
-->
   (<top> ^query <q> -)
}
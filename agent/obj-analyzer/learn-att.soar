sp {propose*learn-att
   (state <s> ^name obj-analyzer
              ^learned-att <la>)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name learn-att
        ^learned-att <la>)
}

sp {apply*learn-att*update*smem
   (state <s> ^name obj-analyzer
              ^operator <o>
              ^smem.command <smem-cmd>)
   (<o> ^name learn-att
        ^learned-att <la>)
   (<la> ^internal <internal-att>
         ^external <external-att>)
-->
   (<smem-cmd> ^store <att>)
   (<att> ^type attribute ^internal <internal-att> ^external <external-att>)
}

sp {apply*learn-att*update*object
   (state <s> ^name obj-analyzer
              ^operator <o>
              ^object <obj>)
   (<o> ^name learn-att
        ^learned-att <la>)
   (<la> ^internal <internal-att>
         ^external <external-att>)
   (<obj> ^mapping.att-val-pair.att <att>)
   (<att> ^internal <internal-att>)
-->
   (<obj> ^changed true)
   (<att> ^external <external-att>)
}

sp {apply*learn-att*remove*learned-att
   (state <s> ^name obj-analyzer
              ^operator <o>
              ^learned-att <la>)
   (<o> ^name learn-att
        ^learned-att <la>)
-->
   (<s> ^learned-att <la> -)
}
              
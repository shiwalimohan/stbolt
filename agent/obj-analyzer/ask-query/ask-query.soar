# ask-query
#   ask-query is proposed for each query
#		then each operator is elaborated with the structure that should go on the output link
#	Preferences determine which queries are better than others
#	When an ask-query operator is chosen, it adds the query structure to the output link 
#		and creates a current-query structure

sp {propose*ask-query
   (state <s> ^name obj-analyzer
              ^query <q>
             -^current-query)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name ask-query
        ^query <q>
        ^output-query <oq>)
}

sp {apply*ask-query*put-on-output-link
   (state <s> ^name obj-analyzer
              ^io.output-link <out>
              ^operator <o>)
   (<o> ^name ask-query
        ^output-query <oq>)
-->
   (<out> ^query <oq>)
}

sp {apply*ask-query*mark*current-query
   (state <s> ^name obj-analyzer
              ^operator <o>)
   (<o> ^name ask-query
        ^query <q>)
-->
   (<s> ^current-query <cq>)
   (<cq> ^type query
         ^query <q>)
}
# differs-from-group
#	Query for an object that differs from a group of objects in some way

# Basic query structure
sp {elaborate*ask-query*differs-from-group
   (state <s> ^name obj-analyzer
              ^operator <o>)
   (<o> ^name ask-query
        ^query <q>
        ^output-query <oq>
        ^fields <f>)
   (<q> ^type differs-from-group
        ^different-obj.external-desc <external-desc>)
-->
   (<oq> ^type different-attribute-question)
   (<f> ^different <external-desc>)
}

# Adds group-obj(s) to query structure
sp {elaborate*ask-query*differs-from-group*group-obj
   (state <s> ^name obj-analyzer
              ^operator <o>)
   (<o> ^name ask-query
        ^query <q>
        ^fields <f>)
   (<q> ^type differs-from-group
        ^group-obj.external-desc <external-desc>)
-->
   (<f> ^similar <external-desc>)
}

# Adds exception(s) to query structure
sp {elaborate*ask-query*differs-from-group*exception
   (state <s> ^name obj-analyzer
              ^operator <o>)
   (<o> ^name ask-query
        ^query <q>
        ^fields <f>)
   (<q> ^type differs-from-group
        ^exception <att>)
-->
   (<f> ^exception <att>)
}

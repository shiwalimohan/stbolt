# When a new attribute is learned, these rules try to find values of that attribute that can also be learned

# Learn the value
sp {learn-att*propose*learn-val
   (state <s> ^name learn-att
              ^learned-att <la>
        	  -^learned-val <external-val>
              ^top-state.object.mapping <map>)
   (<la> ^internal <internal-att>
         ^external <external-att>)
   (<map> ^att-val-pair <int-av-pair>
          ^att-val-pair {<ext-av-pair> <> <int-av-pair>})
   (<int-av-pair> ^att.internal <internal-att>
                  ^val.internal <internal-val>)
   (<ext-av-pair> ^att.external <external-att>
                  ^val.external <external-val>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name learn-val
        ^learned-val <lv>)
   (<lv> ^internal <internal-val>
         ^external <external-val>)
}

sp {prefer*learn-val*over*store-att
   (state <s> ^name learn-att
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name learn-val)
   (<o2> ^name store-att)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {apply*learn-val-from-att*mark*learned
   (state <s> ^name learn-att
              ^operator <o>)
   (<o> ^name learn-val
        ^learned-val.external <external-val>)
-->
   (<s> ^learned-val <external-val>)
}
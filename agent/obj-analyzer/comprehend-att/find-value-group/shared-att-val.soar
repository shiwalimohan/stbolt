sp {find-value-group*propose*shared-att-val
   (state <s> ^name find-value-group
              ^att-to-comp <att-to-comp>
             -^compared <att-to-comp>)
   -{(<s> ^common-av-pair <av-pair>)
   	 (<av-pair> ^type unknown
                ^att {<att> <> <att-to-comp>})}
-->
   (<s> ^operator <o> + =)
   (<o> ^name shared-att-val
        ^query <q>)
   (<q> ^type shared-att-val
   		^internal-att <att-to-comp>)
}

sp {find-value-group*elaborate*shared-att-val*group-obj
   (state <s> ^name find-value-group
              ^operator <o> +
              ^object <obj>)
   (<o> ^name shared-att-val
        ^query <q>)
-->
   (<q> ^group-obj <obj>)
}

sp {find-value-group*elaborate*shared-att-val*exception
   (state <s> ^name find-value-group
              ^operator <o> +
              ^att-to-comp <att-to-comp>
              ^common-av-pair <common-av-pair>
              ^object.mapping.att-val-pair <av-pair>)
   (<o> ^name shared-att-val
        ^query <q>)
   (<common-av-pair> ^type known
                     ^att {<att> <> <att-to-comp>})
   (<av-pair> ^att.internal <att>
              ^att.external <external-att>)
-->
   (<q> ^exception <external-att>)
}

sp {find-value-group*reject*shared-att-val*not-unique
   (state <s> ^name find-value-group
              ^operator <o> +)
   (<o> ^name shared-att-val
        ^query <q>)
   (<q> ^group-obj.unique false)
-->
   (<s> ^operator <o> -)
}

sp {find-value-group*apply*shared-att-val*mark-compared
   (state <s> ^name find-value-group
              ^operator <o>
              ^att-to-comp <att>)
   (<o> ^name shared-att-val)
-->
   (<s> ^compared <att>)
}

sp {find-value-group*apply*shared-att-val*create-query
   (state <s> ^name find-value-group
              ^operator <o>
              ^top-state <top>)
   (<o> ^name shared-att-val
        ^query <q>)
-->
   (<top> ^query <q>)
}
   

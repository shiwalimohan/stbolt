sp {find-value-group*elaborate*common-av-pair
   (state <s> ^name find-value-group
              ^object <obj>)
   (<obj> ^mapping.att-val-pair <av-pair>)
   (<av-pair> ^att.<att-type> <att>
              ^val.<att-type> <val>)
   -{(<s> ^object.mapping.att-val-pair <av-pair2>)
     (<av-pair2> ^att.<att-type> <att>
                ^val.<att-type> <> <val>)}
-->
   (<s> ^common-av-pair <avp>)
   (<avp> ^att <att>
          ^val <val>
          ^att-type <att-type>)
}

sp {find-value-group*elaborate*common-av-pair*known*internal
   (state <s> ^name find-value-group
              ^common-av-pair <common-av-pair>
              ^object <obj>)
   (<common-av-pair> ^att <att>
                     ^att-type internal)
   (<obj> ^mapping.att-val-pair.att <attribute>)
   (<attribute> ^internal <att>
                ^external <external>)
-->
   (<common-av-pair> ^known true)
}

sp {find-value-group*elaborate*common-av-pair*known*external
   (state <s> ^name find-value-group
              ^common-av-pair <common-av-pair>
              ^object <obj>)
   (<common-av-pair> ^att <att>
                     ^att-type external)
   (<obj> ^mapping.att-val-pair.att <attribute>)
   (<attribute> ^internal <internal>
                ^external <att>)
-->
   (<common-av-pair> ^known true)
}

sp {find-value-group*elaborate*common-av-pair*unknown*internal
   (state <s> ^name find-value-group
              ^common-av-pair <common-av-pair>
              ^object <obj>)
   (<common-av-pair> ^att <att>
                     ^att-type internal)
   (<obj> ^mapping.att-val-pair.att <attribute>)
   (<attribute> ^internal <att>
                -^external)
-->
   (<common-av-pair> ^known false)
}

sp {find-value-group*elaborate*common-av-pair*unknown*external
   (state <s> ^name find-value-group
              ^common-av-pair <common-av-pair>
              ^object <obj>)
   (<common-av-pair> ^att <att>
                     ^att-type external)
   (<obj> ^mapping.att-val-pair.att <attribute>)
   (<attribute> -^internal
                ^external <att>)
-->
   (<common-av-pair> ^known false)
}

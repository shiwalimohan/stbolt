# complete-lookup-val
# Fetches the query result and adds the result to the superstate lookup-val operator
# If successful, adds the found word to the operator
# If failure, adds the word 'failure' to the operator
# Example for trying to find an external value color
#	 Success: adds ^external color, Failure: adds ^external failure

# Knows a query is processed if there is a success or failure result
sp {lookup-val*propose*complete-lookup-val
   (state <s> ^name lookup-val
              ^smem <smem>)
   (<smem> ^command.query <query>
           ^result.{<< success failure >>} <query>)
   (<query> ^type value)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-lookup-val
        ^query <query>)
}

# Remove the query from the smem.command link
sp {lookup-val*apply*complete-lookup-val*remove*query
   (state <s> ^name lookup-val
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name complete-lookup-val
        ^query <query>)
   (<cmd> ^query <query>)
-->
   (<cmd> ^query <query> -)
}

# Case 1 : external failure
sp {lookup-val*apply*complete-lookup-val*external*failure
   (state <s> ^name lookup-val
              ^operator.name complete-lookup-val
              ^superstate.operator <ss-op>
              ^smem.result.failure)
   (<ss-op> -^external)
-->
   (<ss-op> ^external failure)
}

# Case 2 : internal failure
sp {lookup-val*apply*complete-lookup-val*internal*failure
   (state <s> ^name lookup-val
              ^operator.name complete-lookup-val
              ^superstate.operator <ss-op>
              ^smem.result.failure)
   (<ss-op> -^internal)
-->
   (<ss-op> ^internal failure)
}

# Case 3: external success
sp {lookup-val*apply*complete-lookup-val*external*success
   (state <s> ^name lookup-val
              ^operator <o>
              ^smem.result <result>
              ^superstate.operator <ss-op>)
   (<o> ^name complete-lookup-val
        ^query <query>)
   (<result> ^success <query>
             ^retrieved.external <external-val>)
   (<ss-op> -^external)
-->
   (<ss-op> ^external <external-val>)
}

# Case 4: internal success
sp {lookup-val*apply*complete-lookup-val*internal*success
   (state <s> ^name lookup-val
              ^operator <o>
              ^smem.result <result>
              ^superstate.operator <ss-op>)
   (<o> ^name complete-lookup-val
        ^query <query>)
   (<result> ^success <query>
             ^retrieved.internal <internal-val>)
   (<ss-op> -^internal)
-->
   (<ss-op> ^internal <internal-val>)
}
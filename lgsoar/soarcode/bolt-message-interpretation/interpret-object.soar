
sp {interpret-object*elaborate*interpretation*on*state
   (state <s> ^name interpret-object)
-->
   (<s> ^interpretation <int>)
}

sp {interpret-object*elaborate*primary-object*from-operator
   (state <s> ^name interpret-object
              ^interpretation <int>
              ^superstate.operator.primary-object <po>)
   (<po> ^predicates.interpretation <point>)
-->
   (<s> ^primary-object <po>)
   (<int> ^object <point>)
}

sp {interpret-object*elaborate*no-predicates*return
   (state <s> ^name interpret-object
              ^predicates <pred>
             -^predicates.predicate 
              ^interpretation <int>
              ^primary-object)
-->
   (<pred> ^interpretation <int>)
}


sp {interpret-object*propose*infer-primary-object
   (state <s> ^name interpret-object
             -^primary-object
              ^objects.object <ob>)
-->
   (<s> ^operator <o> +)
   (<o> ^name infer-primary-object
        ^object <ob>)
}

sp {interpret-object*infer-primary-object*prefer*same-object-equally
   (state <s> ^name interpret-object
              ^operator <o1> + <o2> +)
   (<o1> ^name infer-primary-object
         ^object <ob>)
   (<o2> ^name infer-primary-object
         ^object <ob>)
-->
   (<s> ^operator <o1> = <o2>)
}

sp {interpret-object*infer-primary-object*prefer*first-parameter
   (state <s> ^name interpret-object
              ^operator <o1> + <o2> +
              ^predicates.predicate <pd>
             -^predicates.predicate.inner1 <ob2>)
   (<pd> ^inner1 <ob>
         ^inner2)
   (<o1> ^name infer-primary-object
         ^object <ob>)
   (<o2> ^name infer-primary-object
         ^object <ob2>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {interpret-object*infer-primary-object*prefer*first-parameter*chain
   (state <s> ^name interpret-object
              ^operator <o1> + <o2> +
              ^predicates.predicate <p1>
              ^predicates.predicate <p2>)
   (<p1> ^inner1 <i1>
         ^inner2 <i2>)
   (<p2> ^inner1 <i2>
         ^inner2 <i3>)
   (<o1> ^name infer-primary-object
         ^object <i1>)
   (<o2> ^name infer-primary-object
         ^object <i2>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {interpret-object*apply*infer-primary-object
   (state <s> ^name interpret-object
              ^interpretation <int>
              ^operator <o>)
   (<o> ^name infer-primary-object
        ^object <ob>)
   (<ob> ^predicates.interpretation <point>)
-->
   (<s> ^primary-object <ob>)
   (<int> ^object <point>)
}
          

sp {interpret-object*elaborate*primary-object-relation
   (state <s> ^name interpret-object
              ^primary-object <po>
              ^predicates.predicate <pred>)
   (<pred> ^<< inner1 inner2 inner3 >> <po>
           ^inner2 <exists>)
-->
   (<s> ^primary-object-relation <por>)
   (<por> ^relation <pred>
          ^predicates <ps>)
}

# find all predicates referencing things related to the primary-object-relation

sp {interpret-object*elaborate*primary-object-relation*predicate
   (state <s> ^name interpret-object
              ^predicates.predicate <pred>
              ^object-info <oi>
             -^primary-object-relation.relation <pred>
              ^primary-object-relation <por>)
   (<por> ^relation <rel>         
          ^predicates <ps>)
   (<rel> ^<< inner1 inner2 inner3 >> <ro>)
   (<oi> ^id <ro>
         ^linked-predicate <pred>)
-->
   (<ps> ^predicate <pred>)
}

# for instance:
# on(N2,N3)
# on(N3,N4)
# above(N2,N5)
#
# we now have two primary-object-relations, for on(N2,N3) and another for above(N2,N5)
# the first has on(N3,N4) in its predicates, the second has nothing in its
# predicates

sp {interpret-object*propose*interpret-relation
   (state <s> ^name interpret-object
              ^primary-object-relation <per>)
   (<per> ^relation <rel>
          ^predicates <pred>)
   (<pred> -^interpretation)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interpret-relation
        ^relation <rel>
        ^predicates <pred>)
}

sp {interpret-object*elaborate*relation-interpretation
   (state <s> ^name interpret-object
              ^interpretation <int>
              ^primary-object-relation.predicates.interpretation.relation <pint>)
-->
   (<int> ^relation <pint>)
} 

sp {interpret-object*elaborate*done
   (state <s> ^name interpret-object
              ^predicates <pred>
              ^interpretation <int>
              ^primary-object-relation.predicates.interpretation <exists>)
  -{(<s> ^primary-object-relation.predicates <pred2>)
    (<pred2> -^interpretation)}
-->
   (<pred> ^interpretation <int>)
}


# generally, interpretation subgoals terminate by adding an ^interpretation
# structure to the ^predicates structure on their state, which is the
# elaborated to the ^predicates structure on the interpretation operator

sp {interpretation*elaborate*objects-to-substate
   (state <s> ^problem-space message-interpretation
              ^superstate.objects <ob>)
-->
   (<s> ^objects <ob>)
}

sp {interpretation*elaborate*predicates
   (state <s> ^superstate.operator <o>)
   (<o> ^kind message-interpretation)
-->
   (<s> ^predicates <px>)
}

sp {interpretation*elaborate*unignored-predicates
   (state <s> ^superstate.operator <o>
              ^predicates <p>)
   (<o> ^kind message-interpretation
        ^predicates.predicate <pred>)
   (<pred> -^ignored
           -^accounted-for)
-->
   (<p> ^predicate <pred>)
}

sp {interpretation*elaborate*interpretation-to-operator
   (state <s> ^problem-space message-interpretation
              ^superstate.operator.predicates <ssop>
              ^predicates.interpretation <int>)
-->
   (<ssop> ^interpretation <int>)
}
              

sp {misc*elaborate*interpretation-operators
   (state <s> ^operator <o> +)
   (<o> ^name << interpret-object-message interpret-verb-command 
                 interpret-object-question interpret-relation-question 
                 interpret-object interpret-primitive-object interpret-relation 
                 interpret-verb interpret-preposition
                 interpret-goal-relation-message interpret-goal-object-message
                 interpret-single-word-response finish-interpretation >>)
-->
   (<o> ^kind message-interpretation)
}

# sp {misc*interpret-old-messages-before-lgsoar
#    (state <s> ^superstate nil
#               ^operator <o1> +
#               ^operator <o2> +)
#    (<o1> ^name interpret-sentence)
#    (<o2> ^kind message-interpretation)
# -->
#    (<s> ^operator <o2> > <o1>)
# }

sp {misc*elaborate*message-interpretation*problem-space
   (state <s> ^superstate.operator.kind message-interpretation)
-->
   (<s> ^problem-space message-interpretation)
}

sp {interpretation*prefer*interpret-primitive
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-primitive-object)
   (<o2> ^kind message-interpretation
        -^name interpret-primitive-object)
-->
   (<s> ^operator <o1> > <o2>)
}

# determine linked sets of predicates
# note that these are only 2d+ predicates, so plain properties won't be picked
# up. This is usually desirable, since those should all be absorbed in
# interpret-primitive-object
sp {interpretation*elaborate*predicate-sets*object
   (state <s> ^problem-space message-interpretation
              ^predicates.predicate <pred>)
   (<pred> ^<< inner1 inner2 inner3 >> <ob>)
-->
   (<s> ^object-info <obj>)
   (<obj> ^id <ob>
          ^associated <ob>)
}

sp {interpretation*elaborate*predicates*objects*association
   (state <s> ^problem-space message-interpretation
              ^predicates.predicate <pred>
              ^object-info <oi>)
   (<oi> ^associated <a>)
   (<pred> ^<< inner1 inner2 inner3 >> <a>
           ^<< inner1 inner2 inner3 >> {<> <a> <b>})
-->
   (<oi> ^associated <b>
         ^linked-predicate <pred>)
}


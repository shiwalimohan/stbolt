
# test sentence: What is the color of this?
# PASSED: DEF(N2) color(N2) of(N2,N5) this(N5) what(N2)

sp {interpret-relation-question*elaborate*init
   (state <s> ^name interpret-relation-question)
-->
   (<s> ^interpretation <int>)
}

sp {interpret-relation-question*propose*identify-question-word
   (state <s> ^name interpret-relation-question
             -^interpretation.question-word)
-->
   (<s> ^operator <o> +)
   (<o> ^name identify-question-word)
}

sp {interpret-relation-question*apply*identify-question-word
   (state <s> ^name interpret-relation-question
              ^operator.name identify-question-word
              ^interpretation <int>
              ^all-predicates.predicate <pred>
              ^all-predicates.predicate <pred2>)
   (<pred> ^outer {<< what which >> <qword>}
           ^inner1 <prop-questioned>)
   (<pred2> ^inner1 <prop-questioned>
            ^inner2 <any>) # verify the thing being asked about is in the relation
-->
   (<int> ^question-word <qword>)
   (<s> ^exclude <pred>)
}

sp {interpret-relation-question*propose*collect-predicates
   (state <s> ^name interpret-relation-question
              ^interpretation.question-word
             -^predicates-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-predicates)
}

sp {interpret-relation-question*apply*collect-predicates*root
   (state <s> ^name interpret-relation-question
              ^operator.name collect-predicates)
-->
   (<s> ^relation-predicates <op>)
}

sp {interpret-relation-question*apply*collect-predicates
   (state <s> ^name interpret-relation-question
              ^operator.name collect-predicates
              ^relation-predicates <op>
             -^exclude <p>
              ^all-predicates.predicate <p>)
-->
   (<op> ^predicate <p>)
   (<s> ^predicates-collected true)
}

sp {interpret-relation-question*propose*interpret-relation
   (state <s> ^name interpret-relation-question
              ^relation-predicates <p>
              ^predicates-collected
             -^relation-interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-relation
        ^predicates <p>)
}

sp {interpret-relation-question*elaborate*return-interpretation
   (state <s> ^name interpret-relation-question
              ^all-predicates <p>
              ^interpretation <int>
              ^relation-predicates.interpretation.relation <oi>) # comes from interpret-relation
-->
   (<int> ^relation <oi>)
   (<p> ^interpretation <int>)
}

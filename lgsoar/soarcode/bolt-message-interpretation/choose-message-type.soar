
# verb-commands have the HEARER in a relation
sp {top-state*interpret-messages*propose*interpret-verb-command
   (state <s> ^superstate nil
              ^lgsoar-predicates <pred>)
   (<pred> -^interpretation
            ^predicate <p1>
            ^predicate <p2>)
   (<p1> ^outer HEARER
         ^inner1 <hearer>)
   (<p2> ^inner1 <hearer>
         ^inner2 <i2>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-verb-command
        ^predicates <pred>)
}

# anything might be an object-message
sp {top-state*interpret-messages*propose*interpret-object-message
   (state <s> ^superstate nil
              ^lgsoar-predicates <pred>)
   (<pred> -^interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object-message
        ^predicates <pred>)
}

# relation-message must have a 2-parameter predicate
sp {top-state*interpret-messages*propose*interpret-relation-message
   (state <s> ^superstate nil
              ^lgsoar-predicates <pred>)
   (<pred> -^interpretation
            ^predicate <p1>)
   (<p1> ^inner1 <i1>
         ^inner2 <i2>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-relation-message
        ^predicates <pred>)
}

# object-questions must have a what or which pred
sp {top-state*interpret-messages*propose*interpret-object-question
   (state <s> ^superstate nil
              ^lgsoar-predicates <pred>)
   (<pred> -^interpretation
            ^predicate <p1>)
   (<p1> ^outer << what which >>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object-question
        ^predicates <pred>)
}

# relation-questions must have a what or which pred and a relation
sp {top-state*interpret-messages*propose*interpret-relation-question
   (state <s> ^superstate nil
              ^lgsoar-predicates <pred>)
   (<pred> -^interpretation
            ^predicate <p1>
            ^predicate <p2>)
   (<p1> ^outer << what which >>)
   (<p2> ^inner1 <i1>
         ^inner2 <i2>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-relation-question
        ^predicates <pred>)
}

# other commands:
# goal info ??

# verb-completion-message: revised from verb command if past tense is detected
# (could be "you" based, but "you should put the block on the table" has same
# DRS as "you put the block on the table")

# single-word response:  need to add metalevel for unparsable sentences -> raw
# word representation

sp {misc*elaborate*interpretation-operators
   (state <s> ^operator <o> +)
   (<o> ^name << interpret-object-message interpret-relation-message interpret-verb-command interpret-object-question interpret-relation-question interpret-object interpret-relation >>)
-->
   (<o> ^kind interpretation)
}

sp {top-state*interpret-messages*prefer*anything-over-object-message
   (state <s> ^superstate nil
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-object-message)
   (<o2> ^kind interpretation
        -^name interpret-object-message)
-->
   (<s> ^operator <o2> > <o1>)
}

sp {top-state*interpret-messages*prefer*relation-question-over-relation
   (state <s> ^superstate nil
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-relation-message)
   (<o2> ^kind interpret-relation-question)
-->
   (<s> ^operator <o2> > <o1>)
}

sp {top-state*interpret-messages*prefer*verb-command-over-any
   (state <s> ^superstate nil
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-verb-command)
   (<o2> ^kind interpretation
        -^name interpret-verb-command)
-->
   (<s> ^operator <o1> > <o2>)
}

# generally, interpretation subgoals terminate by adding an ^interpretation
# structure to the ^all-parameters structure on their state, which is the
# same as the ^predicates structure on the interpretation operator

sp {interpretation*elaborate*predicates-to-substate
   (state <s> ^superstate.operator <o>)
   (<o> ^kind interpretation
        ^predicates <p>)
-->
   (<s> ^all-predicates <p>)
}


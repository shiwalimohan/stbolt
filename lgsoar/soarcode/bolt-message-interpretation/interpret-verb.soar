# transitive verb:
# test sentence: Put the block on the table.
# PASSED: DEF(N3) DEF(N4) HEARER(N5) block(N3) on(put,N4) put(N5,N3) table(N4)

# structure needed:
# verb.word put
#     .direct-object.word block
#     .preposition.word on
#                 .object.word table

# interpret-object needs: DEF(N3) block(N3)
# interpret-preposition needs: DEF(N4) on(x,N4)  table(N4)

# intransitive verb:
# test sentence: Go to the stove.
# PASSED: DEF(N3) HEARER(N4) go(N4) stove(N3) to(go,N3)

# structure needed:
# verb.word go
#     .preposition.word to
#                 .object.word stove

# interpret-preposition needs: DEF(N3) stove(N3) to(x,N3)

sp {interpret-verb*elaborate*init
   (state <s> ^name interpret-verb)
-->
   (<s> ^interpretation <it>)
}

sp {interpret-verb*propose*identify-verb-word
   (state <s> ^name interpret-verb
             -^interpretation.word <vb>)
-->
   (<s> ^operator <o> +)
   (<o> ^name identify-verb-word)
}

sp {interpret-verb*apply*identify-verb-word
   (state <s> ^name interpret-verb
              ^operator.name identify-verb-word
              ^interpretation <int>
              ^all-predicates.predicate <hearer>
              ^all-predicates.predicate {<> <hearer> <vw>} )
   (<hearer> ^outer HEARER
             ^inner1 <hearer-obj>)
   (<vw> ^outer <word>
         ^inner1 <hearer-obj>)
-->
   (<int> ^word <word>)
   (<s> ^accounted-for <hearer>
        ^accounted-for <vw>
        ^verb-predicate <vw>)
}

sp {interpret-verb*elaborate*no-preposition
   (state <s> ^name interpret-verb
              ^all-predicates <ap>
              ^verb-predicate <verbp>)
  -{(<ap> ^predicate {<> <verbp> <pred>} )
    (<pred> ^inner2 <any>)} # no remaining 2d preds: must not have a preposition
-->
   (<s> ^has-preposition false)
}

sp {interpret-verb*elaborate*has-preposition
   (state <s> ^name interpret-verb
              ^all-predicates <ap>
              ^verb-predicate <verbp>)
    (<ap> ^predicate {<> <verbp> <pred>})
    (<pred> ^inner2 <any>)
-->
   (<s> ^has-preposition true)
}

sp {interpret-verb*elaborate*has-direct-object
   (state <s> ^name interpret-verb
              ^verb-predicate.inner2 <do>)
-->
   (<s> ^direct-object <do>)
}

sp {interpret-verb*elaborate*no-direct-object
   (state <s> ^name interpret-verb
              ^verb-predicate
              -^verb-predicate.inner2 <do>)
-->
   (<s> ^no-direct-object true)
}

sp {interpret-verb*propose*collect-object-predicates
   (state <s> ^name interpret-verb
              ^direct-object <do>
              ^interpretation.word <exists>
             -^object-predicates-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-object-predicates)
}

sp {interpret-verb*apply*collect-object-predicates*init
   (state <s> ^name interpret-verb
              ^operator.name collect-object-predicates)
-->
   (<s> ^object-predicates <rp>)
}

sp {interpret-verb*apply*collect-object-predicates*collect
   (state <s> ^name interpret-verb
              ^operator.name collect-object-predicates
              ^direct-object <do>
              ^object-predicates <rp>
              ^all-predicates <ap>
             -^accounted-for <pred>)
   (<ap> ^predicate <pred>)
   (<pred> ^inner1 <do>)
-->
   (<rp> ^predicate <pred>)
   (<s> ^object-predicates-collected true
        ^accounted-for <pred>)
}

sp {interpret-verb*propose*interpret-object
   (state <s> ^name interpret-verb
              ^object-predicates-collected true
              ^object-predicates <op>
             -^object-predicates.interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object
        ^predicates <op>)
}

sp {interpret-object*propose*collect-preposition-predicates
   (state <s> ^name interpret-verb
              ^has-preposition
             -^preposition-predicates-collected
              ^object-predicates.interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-preposition-predicates)
}

sp {interpret-object*propose*collect-preposition-predicates*no-object
   (state <s> ^name interpret-verb
              ^no-direct-object
             -^preposition-predicates-collected
              ^has-preposition)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-preposition-predicates)
}

sp {interpret-verb*apply*collect-preposition-predicates*init
   (state <s> ^name interpret-verb
              ^operator.name collect-preposition-predicates)
-->
   (<s> ^preposition-predicates <rp>)
}

sp {interpret-verb*apply*collect-preposition-predicates*collect
   (state <s> ^name interpret-verb
              ^operator.name collect-preposition-predicates
              ^preposition-predicates <rp>
              ^all-predicates <ap>
             -^accounted-for <pred>)
   (<ap> ^predicate <pred>)
-->
   (<rp> ^predicate <pred>)
   (<s> ^preposition-predicates-collected true
        ^accounted-for <pred>)
}

sp {interpret-verb*propose*interpret-preposition
   (state <s> ^name interpret-verb
              ^preposition-predicates-collected true
              ^preposition-predicates <op>
             -^preposition-predicates.interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-preposition
        ^predicates <op>)
}

sp {interpret-verb*elaborate*preposition-interpretation
   (state <s> ^name interpret-verb
              ^preposition-predicates.interpretation.preposition <ri>
              ^interpretation <int>)
-->
   (<int> ^preposition <ri>)
}

sp {interpret-verb*elaborate*direct-object
   (state <s> ^name interpret-verb
              ^object-predicates.interpretation.object <ri>
              ^interpretation <int>)
-->
   (<int> ^direct-object <ri>)
}

sp {interpret-verb*elaborate*fully-interpreted*simple
   (state <s> ^name interpret-verb
              ^no-direct-object
              ^has-preposition false
              ^interpretation.word <exists>)
-->
   (<s> ^fully-interpreted true)
}

sp {interpret-verb*elaborate*fully-interpreted*both
   (state <s> ^name interpret-verb
              ^direct-object
              ^has-preposition true
              ^interpretation.direct-object <exists>
              ^interpretation.preposition <exists2>)
-->
   (<s> ^fully-interpreted true)
}

sp {interpret-verb*elaborate*fully-interpreted*do
   (state <s> ^name interpret-verb
              ^direct-object
              ^has-preposition false
              ^interpretation.direct-object <exists>)
-->
   (<s> ^fully-interpreted true)
}

sp {interpret-verb*elaborate*fully-interpreted*prep
   (state <s> ^name interpret-verb
              ^no-direct-object
              ^has-preposition true
              ^interpretation.preposition <exists>)
-->
   (<s> ^fully-interpreted true)
}

sp {interpret-verb*elaborate*return-interpretation
   (state <s> ^name interpret-verb
              ^all-predicates <ap>
              ^interpretation <int>
              ^fully-interpreted true)
-->
   (<ap> ^interpretation <int>)
}

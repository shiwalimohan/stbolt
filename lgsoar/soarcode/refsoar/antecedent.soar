## SBW 3/21/2012
# renaming this to antecedent.soar, extracting new anaphor.soar from
# lgrefsoar.soar

# File: anaphor.soar
# Author: William Taysom
# Created: Mar. 18, 2001
#
# Desc: Implements anaphor antecedent finding operators. (cleanup)
#
# Anaphor Rules Implemented:
#			anaphor-set-antecedent, anaphor-find-antecedent
#
# TO DO:
# anaphor-find-antecedent

# anaphor-set-antecedent

sp	{Link*propose*anaphor-set-antecedent*simple-clause-structure
	(state <s> ^DRS <d> ^antecedent <ante>)
	(<d> ^cond <verb-cond-new>)
	(<verb-cond-new> ^kind verbal ^subj { <u> <> <ante> } ^verb <verb>)
	(<u> ^word <word>)
	(<word> -^wvalue << I You He She It you he she it We we They they My my
						Your your Our our His his Her her Its its Their
						their >>)
  -{(<d> ^cond <verb-cond-old>)
  	(<verb-cond-old> ^kind verbal-pred ^subj <ante>)}
-->
	(<s> ^operator <o> + )
	(<o> ^name set-antecedent ^antecedent <u>  ^ordering -2)}

sp	{Link*apply*anaphor-set-antecedent
	(state <s> ^operator <o> ^antecedent <ante>)
	(<o> ^name set-antecedent ^antecedent <u> )
-->
	(<s> ^antecedent <ante> - <u> +)
}

# anaphor-find-antecedent

sp	{Link*propose*anaphor-find-antecedent
	(state <s> ^DRS <d> ^analysis <a>)
	(<d> ^dref <u>)
	(<u> ^word <word>)
	(<word> ^wvalue << I You He She It you he she it We we They they My my
					   Your your Our our His his Her her Its its Their
					   their >> ^wcount <pos>)
	(<a> -^find-antecedent <u>)
-->
	(<s> ^operator <o> + >)
	(<o> ^name find-antecedent ^anaphor <u> ^ordering -1 )}

sp	{Link*apply*anaphor-find-antecedent
	(state <s> ^operator <o> ^antecedent <ante> ^DRS <d> ^analysis <a>)
	(<o> ^name find-antecedent ^anaphor <u> )
-->
	(<d> ^cond <equ> +)
	(<equ> ^kind equ ^left-side <u> ^right-side <ante>)
	(<a> ^find-antecedent <u> +)
}

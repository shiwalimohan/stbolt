# File: promote.soar
# Author: William Taysom
# Created: July 7, 2001
#
# Desc: Promotes semantics from ref to object.
#
# Promotions:
#		basic (70-60), feature (70)
#
# 7 June 2011: soar9 conversion: DWL removed attr prefs, added o-support
#

## basic promotion

sp {promote*propose*direct-promotion
   (state <s> 
   #^count <count> 
   ^refsets.refs <refs> ^model <m>)
   (<refs> ^count <count> ^ref <main> <arg>)
   (<main> ^pointer <imain> ^head.wcount <main-wcount>)
   (<arg> ^pointer <iarg> ^head.wcount <arg-wcount>)
   (<imain> -^<aug> <iarg>)
   # this is different than the indirect case
   (<main> ^<aug> <arg>)
-->
   (<s> ^operator <o> +)
   (<o> ^name basic ^priority 70 ^imain <imain> ^aug <aug> ^iarg <iarg>
   	 ^order <arg-wcount> ^level-two-order <main-wcount> ^complete? no)}

sp {promote*propose*indirect-promotion
   (state <s> 
   #^count <count> 
   ^refsets.refs <refs> ^model <m>)
   (<refs> ^count <count> ^ref <main> <arg>)
   (<main> ^pointer <imain> ^head.wcount <main-wcount>)
   (<arg> ^pointer <iarg> ^head.wcount <arg-wcount>)
   (<imain> -^aug <iarg>)
   # this is different than the direct case
   (<iarg> ^{<> aug <aug>} <imain>)
-->
   (<s> ^operator <o> +)
   (<o> ^name basic ^priority 60 ^imain <imain> ^aug aug ^iarg <iarg>
   	 ^order <arg-wcount> ^level-two-order <main-wcount> ^complete? no)}

sp {promote*apply*direct-promotion
   :o-support
   (state <s> ^operator <o>)
   (<o> ^name basic ^imain <imain> ^aug <aug> ^iarg <iarg>)
-->
   (<imain> ^<aug> <iarg> +)
   (<o> ^complete?x yes +)}

## feature promotion

sp {promote*propose*feature-promotion*infl
   (state <s> ^count <count> ^refsets.refs <refs> ^model <m>)
   (<refs> ^count <count> ^ref <main>)
   (<main> ^pointer <imain> ^infl <feature> ^head.wcount <main-wcount>)
   (<imain> -^infl <feature>)
-->
   (<s> ^operator <o> +)
   (<o> ^name feature ^priority 70 ^imain <imain> ^aug infl ^feature <feature>
   	 ^order <main-wcount> ^level-two-order 0 ^complete? no)}

sp {promote*propose*feature-promotion*neg
   (state <s> ^count <count> ^refsets.refs <refs> ^model <m>)
   (<refs> ^count <count> ^ref <main>)
   (<main> ^pointer <imain> ^neg not ^head.wcount <main-wcount>)
   (<imain> -^neg not)
-->
   (<s> ^operator <o> +)
   (<o> ^name feature ^priority 70 ^imain <imain> ^aug neg ^feature not
   	 ^order <main-wcount> ^level-two-order 1 ^complete? no)}

sp {promote*propose*feature-promotion*mode
   (state <s> ^count <count> ^refsets.refs <refs> ^model <m>)
   (<refs> ^count <count> ^ref <main>)
   (<main> ^pointer <imain> ^mode <mode> ^head.wcount <main-wcount>)
   (<imain> -^mode <mode>)
-->
   (<s> ^operator <o> +)
   (<o> ^name feature ^priority 70 ^imain <imain> ^aug mode ^feature <mode>
   	 ^order <main-wcount> ^level-two-order 2 ^complete? no)}

sp {promote*apply*feature-promotion
  :o-support
   (state <s> ^operator <o>)
   (<o> ^name feature ^priority 70 ^imain <imain> ^aug <aug> ^feature <feature> ^complete? no)
-->
   (<imain> ^<aug> <feature>)
   (<o> ^complete?x yes +)}
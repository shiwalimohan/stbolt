# BOLT/LGSoar integration productions go here

source bolt-preprocessing/load.soar
source bolt-message-interpretation/load.soar

# replace productions in refsoar/activate.soar to put lgsoar in a substate
excise top-state*lg*init*sentence
excise global*apply*processing-complete

sp {top-state*propose*preprocess-sentence
  (state <s> ^superstate nil
             ^io.input-link.lg.original-sentence <sent>)
  (<sent> -^preprocessed)
-->
  (<s> ^operator <o> +)
  (<o> ^name preprocess-sentence
       ^kind interpretation
       ^sentence <sent>)
}

sp {top-state*propose*evaluate-parse
  (state <s> ^superstate nil
             ^io.input-link.lg.sentence <sent>)
   (<sent> -^parse-valid)
-->
  (<s> ^operator <o> +)
  (<o> ^name evaluate-parse
       ^sentence <sent>
       ^kind interpretation)
}

sp {top-state*propose*interpret-sentence
  (state <s> ^superstate nil
             ^io.input-link.lg.sentence <sent>)
   (<sent> -^final-predicates
            ^parse-valid true)
-->
  (<s> ^operator <o> +)
  (<o> ^name interpret-sentence
       ^sentence <sent>
       ^kind interpretation)
}

sp {elaborate*prefer-older-sentences
   (state <s> ^superstate nil
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-sentence
         ^sentence.count <ct1>)
   (<o2> ^name interpret-sentence
         ^sentence.count > <ct1>)
-->
   (<s> ^operator <o1> > <o2>)
}

# prefer any operator without a priority (non-LGSoar) to one with a priority
sp {prefer*non-LG
   (state <s> ^operator <o> +
              ^operator <o2> +)
   (<o> ^priority <any>)
   (<o2> -^priority)
-->
   (<s> ^operator <o2> > <o>)
}

sp {elaborate*process-over-interpret
   (state <s> ^superstate nil
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-sentence)
   (<o2> ^name process-message)
-->
   (<s> ^operator <o2> > <o1>)
}

sp {interpret-sentence*elaborate*init
   (state <s> ^superstate.operator <sso>)
   (<sso> ^name interpret-sentence
          ^sentence <sent>)
-->
   (<s> ^io.lgsentence <sent>)
}

sp {global*apply*processing-complete*save-predicates
  (state <s> ^operator.name processing-complete
             ^io.lgsentence <lgs>
             ^final-predicates <fp>)
-->
   (<lgs> ^final-predicates <fp>)
}

sp {top-state*lgsoar-predicates*to-state
   (state <s> ^io.input-link.lg.sentence <lgs>)
   (<lgs> ^final-predicates <fp>)
-->
   (<s> ^lgsoar-predicates <fp>)
}

sp {global*propose*handle-single-word
   (state <s> ^problem-space lgsoar
              ^count <cnt>
             -^predicates-generated
              ^io.lgsentence <sen>)
   (<sen> ^count <cnt>
          ^words <wds>)
   (<wds> ^word.wvalue <w1>
          -^word.wvalue {<> <w1> <> LEFT-WALL <> RIGHT-WALL <> |.|})
-->
   (<s> ^operator <o> +)
   (<o> ^name handle-single-word
        ^priority 1000
        ^word <w1>)
}

sp {global*apply*handle-single-word
   (state <s> ^problem-space lgsoar
              ^operator <o>
              ^final-predicates <fp>)
   (<o> ^name handle-single-word
        ^word <wd>)
-->
   (<s> ^predicates-generated true)
   (<fp> ^predicate <pred>)
   (<pred> ^outer single-word
           ^inner1 <wd>)
}


# experimental role-preposition handling:
# "The color of the block is red"
# DEF(N2)
# DEF(N3)
# of(N2,N3)
# block(N3)
# color(N2)
# of(color,N2,N3) <== adds this
# red(N2)
# This makes "the color of the block is red" different from "the red of the
# block is color"

sp {global*apply*generate-predicates*role-preposition
   (state <s> ^model.idea <idea> ^final-predicates <fp> 
              ^operator.name generate-predicates) 
   (<idea> ^nuc <nucval> ^preposition-lex <plex> ^<plex> <pobjval>)
   (<pobjval> ^nuc <nuc2val> ^aug <aug2val> ^preposition-lex <pl2> ^<pl2> <prep>)
-->
   (<pobjval> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer1 <nuc2val>
           ^outer2 <pl2>
           ^inner1 <pobjval>
           ^inner2 <prep>)
}

sp {global*apply*generate-predicates*role-preposition*copula*ext
   (state <s> ^model.idea <idea> ^final-predicates <fp> 
              ^operator.name generate-predicates) 
   (<idea> ^ext <ext> ^nuc <nucval> ^annotation copula) 
   (<ext> ^nuc <thisnucval> ^preposition-lex <pl> ^<pl> <prep>)
-->
   (<ext> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer1 <thisnucval>
           ^outer2 <pl>
           ^inner1 <prep>
           ^inner2 <ext>)
}

sp {global*apply*generate-predicates*role-preposition*copula*int
   (state <s> ^model.idea <idea> ^final-predicates <fp> 
              ^operator.name generate-predicates) 
   (<idea> ^int <int> ^ext <ext> ^nuc <nucval> ^annotation copula) 
   (<int> ^nuc <thisnucval> ^preposition-lex <pl> ^<pl> <prep>)
-->
   (<int> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer1 <thisnucval>
           ^outer2 <pl>
           ^inner1 <prep>
           ^inner2 <ext>)
}

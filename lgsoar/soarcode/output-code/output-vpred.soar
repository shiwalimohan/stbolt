
###################### these output terms for verbal external and internal arguments
# output the external and internal argument (if it exists) for the verb
sp {global*apply*generate-predicates*output*ext/int*arg
   (state <s> ^model.idea <idea> ^final-predicates <fp> ^final-predicate-strings <fps>)
   (state <s> ^operator.name generate-predicates 
   #^count <count> 
   ^refsets.refs.count <count>)
   (<idea> ^<< ext int >> <thisroleval> ^nuc <nucval> ^annotation pred-adj/noun-tested)
   (<thisroleval> ^nuc <thisnucval> -^annotation pred-adj/noun
   #^annotation pred-prep 
   -^annotation in-n-n)
-->
   (<thisroleval> ^annotation pred-dumped +)
   (<fps> ^predicate (concat <thisnucval> |(| <thisroleval> |)|))
   (<fp> ^predicate <pred>)
   (<pred> ^outer <thisnucval>
           ^inner1 <thisroleval>)
#   (write (crlf) ext/int-arg: <thisnucval> |(| <thisroleval> |)|)
}

# now output the term for the predicate adjective; NOTE: have to find the subject's variable
sp {global*apply*generate-predicates*output*int*pred-adj
   (state <s> ^model.idea <idea> ^final-predicates <fp> ^final-predicate-strings <fps>)
   (state <s> ^operator.name generate-predicates 
   #^count <count> 
   ^refsets.refs.count <count>)
   (<idea> ^int <thisroleval> ^nuc <nucval>)
   (<thisroleval> ^nuc <thisnucval> ^annotation pred-adj/noun ^aug.ext <thesubject> -^annotation pred-pp
   #^annotation pred-prep 
   -^annotation in-n-n)
-->
   (<thisroleval> ^annotation pred-dumped +)
   (<idea> ^annotation pred-dumped +) # a white lie 
   (<fps> ^predicate (concat <thisnucval> |(| <thesubject> |)|))
   (<fp> ^predicate <pred>)
   (<pred> ^outer <thisnucval>
           ^inner1 <thesubject>)
#   (write (crlf) int*pred-adj: <thisnucval> |(| <thesubject> |)|)
}

# intransitive predicate terms
sp {global*apply*generate-predicates*output*predicate-itrx
   (state <s> ^model.idea <idea> ^final-predicates <fp> ^final-predicate-strings <fps>)
   (state <s> ^operator.name generate-predicates 
   #^count <count> 
   ^refsets.refs.count <count>)
   (<idea> ^ext <extval> ^nuc <nucval> -^int <any> -^annotation copula)
   (<extval> ^nuc <extnucval> ^annotation pred-dumped)
-->
   (<idea> ^annotation pred-dumped + ^annotation intransitive +)
   (<fps> ^predicate (concat <nucval> |(| <extval> |)|))
   (<fp> ^predicate <pred>)
   (<pred> ^outer <nucval>
           ^inner1 <extval>)
#   (write (crlf) predicate-itrx: | | <nucval> |(| <extval> |)|)
}


# transitive predicate terms
sp {global*apply*generate-predicates*output*predicate-trx
   (state <s> ^model.idea <idea> ^final-predicates <fp> ^final-predicate-strings <fps>)
   (state <s> ^operator.name generate-predicates 
   #^count <count> 
   ^refsets.refs.count <count>)
   (<idea> ^ext <extval> ^nuc <nucval> ^int <intval> -^int {<> <intval> <int2val>} -^annotation copula)
   (<extval> ^nuc <extnucval> ^annotation pred-dumped)
-->
   (<extval> ^annotation pred-dumped +)
   (<intval> ^annotation pred-dumped +)
   (<idea> ^annotation pred-dumped + ^annotation transitive +)
   (<fps> ^predicate (concat <nucval> |(| <extval> |,| <intval> |)|))
   (<fp> ^predicate <pred>)
   (<pred> ^outer <nucval>
           ^inner1 <extval>
           ^inner2 <intval>)
#   (write (crlf) predicate-trx: | | <nucval> |(| <extval> |,| <intval> |)|)
}

# the man sent the lawyer a letter .

# ditransitive predicates
sp {global*apply*generate-predicates*output*predicate-ditrx
   (state <s> ^model.idea <idea> ^final-predicates <fp> ^final-predicate-strings <fps>)
   (state <s> ^operator.name generate-predicates 
   #^count <count> 
   ^refsets.refs.count <count>)
   (<idea> ^ext <extval> ^nuc <nucval> ^int <intval> ^int {<> <intval> <int2val>})
   (<extval> ^nuc <extnucval> ^annotation pred-dumped)
   (<intval> ^wcount <w1val>)
   (<int2val> ^wcount {< <w1val> <w2val>})
-->
   (<extval> ^annotation pred-dumped +)
   (<intval> ^annotation pred-dumped +)
   (<int2val> ^annotation pred-dumped +)
   (<idea> ^annotation pred-dumped +)
   (<fps> ^predicate (concat <nucval> |(| <extval> |,| <intval> |,| <int2val> |)|))
   (<fp> ^predicate <pred>)
   (<pred> ^outer <nucval>
           ^inner1 <extval>
           ^inner2 <intval>
           ^inner3 <int2val>)
#   (write (crlf) predicate-ditrx: | | <nucval> |(| <extval> |,| <intval> |,| <int2val> |)|)
}


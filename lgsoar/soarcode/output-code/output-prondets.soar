# dumps feature values (definite or indefinite)
# The man kicked a bottle.
sp {global*apply*generate-predicates*cligdump*feature-feature
   (state <s> ^operator <o> ^count <count> ^refsets <refsets> ^final-predicates <fp>)
   (<refsets> ^refs <refs>)
   (<refs> ^count <count>)
   (<o> ^name generate-predicates
        ^annotation copulaflag-set)
   (state <s> ^anaphor.centers.center.ref <ref>)
   (<ref> ^desc <desc> ^pointer <pointer>)
   (<desc> ^feature <feavalue> ^word <word>)
   (<word> ^wcount <countvalue> ^wvalue <wordvalue>)
   (<pointer> ^nuc <nounvalue>)
  -{(<pointer> ^aug <aug>)
    (<aug> ^annotation copula
           ^int <pointer>)}
-->
   (<pointer> ^feat-dumped <feavalue> +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer <feavalue>
           ^inner1 <pointer>)
   #(write (crlf) feature-feature: | | <feavalue> |(| <pointer> |)|)
}

# SBW adding this to fix problems where "is" causes the DEF/INDEF to attach to
# the wrong object
sp {global*apply*generate-predicates*cligdump*feature-feature*copula
   (state <s> ^operator <o> ^count <count> ^refsets <refsets> ^final-predicates <fp>)
   (<refsets> ^refs <refs>)
   (<refs> ^count <count>)
   (<o> ^name generate-predicates)
   (state <s> ^anaphor.centers.center.ref <ref>)
   (<ref> ^desc <desc> ^pointer <pointer>)
   (<desc> ^feature <feavalue> ^word <word>)
   (<word> ^wcount <countvalue> ^wvalue <wordvalue>)
   (<pointer> ^nuc <nounvalue>
              ^aug <pointer2>)
   (<pointer2> ^annotation copula
               ^int <pointer>
               ^ext <pointer3>)
-->
   (<pointer> ^feat-dumped <feavalue> +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer <feavalue>
           ^inner1 <pointer3>)
  # (write (crlf) feature-feature: | | <feavalue> |(| <pointer3> |)|)
}

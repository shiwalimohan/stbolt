

### output predicate for predicate PP
# the block is on the table .
sp {global*apply*generate-predicates*output*predicate-PP
   (state <s> ^model.idea <idea>
              ^final-predicates <fp>
              ^operator.name generate-predicates) 
   (<idea> ^ext <extval> ^annotation copula ^annotation pred-pp ^preposition-lex <plex> ^<plex> <plexval>)
   (<extval> ^nuc <extnucval> ^annotation pred-dumped)
   (<plexval> ^nuc <pobjlex>) 
-->
   (<plexval> ^annotation pred-dumped +)
   (<idea> ^annotation pred-dumped +) # a white lie
   (<fp> ^predicate <pred>)
   (<pred> ^outer <plex>
           ^inner1 <extval>
           ^inner2 <plexval>)
   (<fp> ^predicate <pred2>)
   (<pred2> ^outer PRIMARY_OBJECT
           ^inner1 <extval>)
#   (write (crlf) predicate-pp: | | <plex> |(| <extval> |,| <plexval> |)|)
}


### output predicate for PP on external arg
# the man with teeth barfed .
# the big man with teeth barfed .
### copula predicate has PP
# one criterion is history of lymphoma
sp {global*apply*generate-predicates*output*nuc+ppobj*predicate
   (state <s> ^operator <o> ^final-predicates <fp> ^model.idea <idea>)
   (<o> ^name generate-predicates ^annotation predflags-set)
   (<idea> -^relclause <relval> 
    #-^aug <augval> DWL: it's an ext so the predicate is an ^aug
           -^int <intval> -^ext <extval>
            ^nuc <nucval>
            ^preposition-lex <plex> ^<plex> <pobjval> -^annotation pred-pp)
   (<pobjval> ^nuc <nuc2val> ^aug <aug2val>)

    # SBW added
  -{(<idea> ^aug <idea2>)
    (<idea2> ^annotation copula
             ^int <idea>)}
-->
   (<pobjval> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer <plex>
           ^inner1 <idea>
           ^inner2 <pobjval>)
#    (write (crlf) nuc-ppobj*seq: <plex> |(| <idea> |,| <pobjval> |)|)
}

# SBW added this
sp {global*apply*generate-predicates*output*nuc+ppobj*predicate*through-copula
   (state <s> ^operator <o> ^final-predicates <fp> 
              ^model <m*1>)
   (<o> ^name generate-predicates ^annotation predflags-set)
   (<m*1> ^idea <idea>)
   (<idea> -^relclause <relval> 
    #-^aug <augval> DWL: it's an ext so the predicate is an ^aug
           -^int <intval> -^ext <extval>
            ^nuc <nucval>
            ^preposition-lex <plex> ^<plex> <pobjval> -^annotation pred-pp)
   (<pobjval> ^nuc <nuc2val> ^aug <aug2val>)

   (<idea> ^aug <idea2>)
   (<idea2> ^annotation copula
            ^int <idea>
            ^ext <idea3>)
-->
   (<pobjval> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer <plex>
           ^inner1 <idea3>
           ^inner2 <pobjval>)
#    (write (crlf) nuc-ppobj*seq: <plex> |(| <idea3> |,| <pobjval> |)|)
}

# can't output predicate for PP on internal arg because LG parser always attaches PP to verb
# police chased the man with a limp
# in any case, handle PP attachments to verb; for now just use verb lex as first argument
# this should handle intransitives, transitives, and ditransitives

sp {global*apply*generate-predicates*output*PP*v-attached
   (state <s> ^operator <o> ^final-predicates <fp>
              ^model.idea <idea>)
   (<o> ^name generate-predicates ^annotation predflags-set)
   (<idea> ^nuc <nucval> ^preposition-lex <plex> ^<plex> <pobjval> -^annotation copula -^annotation pred-pp
          -^aug.<< int int-second >> <idea>) # SBW added: idea must be an int of a verb (attached pp)
    # somewhat of a hack---without this we get with(x1,x2) and with(verb,X2) for some reason
   (<idea> -^aug.annotation copula)
    # SBW adding similar hack for preposition-lex
    # this makes "the ball is to the left of the block" the same as "put the
    # ball to the left of the block". In both cases, need to stop of(left,N7)
   (<idea> -^aug.preposition-lex) 
-->
   (<pobjval> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer <plex>
           ^inner1 <nucval>
           ^inner2 <pobjval>)
    #(write (crlf) nuc-ppobj*seq: <plex> |(| <nucval> |,| <pobjval> |)|)
}

### output a predicate for the object of any pp
# the happy dogs with a penchant for ridiculous drama chased the ugly cat .
sp {global*apply*generate-predicates*cligdump*ppobj*arg
   (state <s> ^model.idea <idea> ^final-predicates <fp>
              ^operator.name generate-predicates)
   (<idea> ^nuc <nucval> ^preposition-lex <plex> ^<plex> <pobjval>)
   (<pobjval> ^nuc <nuc2val> ^aug <aug2val>)
-->
   (<pobjval> ^annotation pred-dumped +)
   #(<nuc2val> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer <nuc2val>
           ^inner1 <pobjval>)
#   (write (crlf) ext/int-arg: <nuc2val> |(| <pobjval> |)|)
}


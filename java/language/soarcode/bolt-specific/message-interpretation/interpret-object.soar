
sp {interpret-object*elaborate*interpretation*on*state
   (state <s> ^name interpret-object)
-->
   (<s> ^interpretation <int>)
}

sp {interpret-object*elaborate*primary-object*from-operator
   (state <s> ^name interpret-object
              ^interpretation <int>
              ^superstate.operator.primary-object <po>)
   (<po> ^predicates.interpretation <point>)
-->
   (<s> ^primary-object <po>)
   (<int> ^object <point>)
}

sp {interpret-object*elaborate*no-relevant-predicates*return
   (state <s> ^name interpret-object
              ^predicates <pred>
              ^interpretation <int>
              ^primary-object <po>)
  -{(<s> ^predicates.predicate <pr>)
    (<pr> ^<< inner1 inner2 inner3 >> <po>
          ^inner2 <exists>
         -^is-verb true)}
-->
   (<pred> ^interpretation <int>)
}


sp {interpret-object*propose*infer-primary-object
   (state <s> ^name interpret-object
             -^primary-object
              ^objects.object <ob>)
-->
   (<s> ^operator <o> +)
   (<o> ^name infer-primary-object
        ^object <ob>)
}

sp {interpret-object*infer-primary-object*prefer*same-object-equally
   (state <s> ^name interpret-object
              ^operator <o1> + <o2> +)
   (<o1> ^name infer-primary-object
         ^object <ob>)
   (<o2> ^name infer-primary-object
         ^object <ob>)
-->
   (<s> ^operator <o1> = <o2>)
}

sp {interpret-object*infer-primary-object*prefer*first-parameter
   (state <s> ^name interpret-object
              ^operator <o1> + <o2> +
              ^predicates.predicate <pd>)
   (<pd> ^inner1 <ob>
         ^inner2)
   (<o1> ^name infer-primary-object
         ^object <ob>)
   (<o2> ^name infer-primary-object
         ^object <ob2>)
  -{(<s> ^predicates.predicate <other>)
    (<other> ^inner1 <ob2>
             ^inner2 <exists>)}
-->
   (<s> ^operator <o1> > <o2>)
}

sp {interpret-object*infer-primary-object*prefer*first-parameter*chain
   (state <s> ^name interpret-object
              ^operator <o1> + <o2> +
              ^predicates.predicate <p1>
              ^predicates.predicate <p2>)
   (<p1> ^inner1 <i1>
         ^inner2 <i2>)
   (<p2> ^inner1 <i2>
         ^inner2 <i3>)
   (<o1> ^name infer-primary-object
         ^object <i1>)
   (<o2> ^name infer-primary-object
         ^object <i2>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {interpret-object*apply*infer-primary-object
   (state <s> ^name interpret-object
              ^interpretation <int>
              ^operator <o>)
   (<o> ^name infer-primary-object
        ^object <ob>)
   (<ob> ^predicates.interpretation <point>)
-->
   (<s> ^primary-object <ob>)
   (<int> ^object <point>)
}
          

sp {interpret-object*elaborate*primary-object-relation
   (state <s> ^name interpret-object
              ^primary-object <po>
              ^predicates.predicate <pred>)
   (<pred> ^<< inner1 inner2 inner3 >> <po>
           ^inner2 <exists>
          -^is-verb true)
-->
   (<s> ^primary-object-relation <por>)
   (<por> ^relation <pred>
          ^predicates <ps>)
}

sp {interpret-object*elaborate*primary-object-relation*predicate
   (state <s> ^name interpret-object
              ^predicates.predicate <pred>
             -^primary-object-relation.relation <pred>
              ^primary-object-relation.predicates <ps>)
-->
   (<ps> ^predicate <pred>)
}


sp {interpret-object*propose*interpret-relation
   (state <s> ^name interpret-object
              ^primary-object-relation <per>)
   (<per> ^relation <rel>
          ^predicates <pred>)
   (<pred> -^interpretation)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interpret-relation
        ^relation <rel>
        ^predicates <pred>)
}

sp {interpret-object*elaborate*relation-interpretation
   (state <s> ^name interpret-object
              ^interpretation <int>
              ^primary-object-relation.predicates.interpretation.relation <pint>)
-->
   (<int> ^relation <pint>)
} 

sp {interpret-object*elaborate*done
   (state <s> ^name interpret-object
              ^predicates <pred>
              ^interpretation <int>
              ^primary-object-relation.predicates.interpretation <exists>)
  -{(<s> ^primary-object-relation.predicates <pred2>)
    (<pred2> -^interpretation)}
-->
   (<pred> ^interpretation <int>)
}

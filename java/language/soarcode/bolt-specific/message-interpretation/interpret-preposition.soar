sp {interpret-preposition*elaborate*init
   (state <s> ^name interpret-preposition)
-->
   (<s> ^interpretation <it>)
   (<it> ^preposition <rm>)
}

sp {interpret-preposition*propose*identify-preposition
   (state <s> ^name interpret-preposition
             -^preposition-identified
              ^superstate.operator.preposition <pr>)
-->
   (<s> ^operator <o> +)
   (<o> ^name identify-preposition
        ^preposition <pr>)
}


sp {interpret-preposition*apply*identify-preposition
   (state <s> ^name interpret-preposition
              ^operator <o>
              ^interpretation.preposition <rm>)
   (<o> ^name identify-preposition
        ^preposition <pr>)
   (<pr> ^outer <preposition>
         ^inner1 <p1-val>
         ^inner2 <p2-val>)
-->
   (<s> ^preposition-identified true
        ^object <p2-val>)
   (<rm> ^word <preposition>)
   (<pr> ^accounted-for true)
}

sp {interpret-preposition*apply*identify-preposition*two-part-outer*of
   (state <s> ^name interpret-preposition
              ^operator <o>
              ^interpretation.preposition <rm>)
   (<o> ^name identify-preposition
        ^preposition <pr>)
   (<pr> ^outer1 <o1>
         ^outer2 {<o2> of}
         ^inner1 <p1-val>
         ^inner2 <p2-val>)
-->
   (<s> ^preposition-identified true
        ^object <p2-val>)
   (<rm> ^word (concat <o1> |-| <o2>))
   (<pr> ^accounted-for true)
}

sp {interpret-preposition*propose*collect-object
   (state <s> ^name interpret-preposition
              ^preposition-identified true
             -^object-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-object)
}

sp {interpret-preposition*apply*collect-object*base
   (state <s> ^name interpret-preposition
              ^object <object>
              ^operator.name collect-object)
-->
   (<s> ^object-predicates <cp>)
}

sp {interpret-preposition*apply*collect-object
   (state <s> ^name interpret-preposition
              ^operator.name collect-object
              ^object-predicates <cp>
              ^predicates.predicate <pred>)
-->
   (<cp> ^predicate <pred>)
}

sp {interpret-preposition*apply*collect-object*done
   (state <s> ^name interpret-preposition
              ^operator.name collect-object
              ^object-predicates <cp>)
-->
   (<s> ^object-collected true)
}

sp {interpret-preposition*propose*interpret-object
   (state <s> ^name interpret-preposition
              ^object-collected true
              ^object <po>
              ^object-predicates <cp>)
   (<cp> -^interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object
        ^predicates <cp>
        ^primary-object <po>)
}

sp {interpret-preposition*elaborate*object-interpretation
   (state <s> ^name interpret-preposition
              ^object-predicates <p1p>
              ^interpretation.preposition <rm>)
   (<p1p> ^interpretation.object <int>)
-->
   (<rm> ^object <int>)
}

sp {interpret-preposition*elaborate*object-interpretation*subrelation
   (state <s> ^name interpret-preposition
              ^object-predicates <p1p>
              ^interpretation.preposition <rm>)
   (<p1p> ^interpretation.relation <int>)
-->
   (<rm> ^relation <int>)
}

sp {interpret-preposition*elaborate*return-interpretation
   (state <s> ^name interpret-preposition
              ^predicates <p>
              ^interpretation <i>)
   (<i> ^preposition.object <object>)
-->
   (<p> ^interpretation <i>)
}
